;
;	L-os Angeles ETC
;
XCINIT:
BASE8:	LD	A,($BF)		;WK1FD0
	RES	4,A
	CALL	_S1FD0
CINIT:
	LD	A,$C3
	LD	(_RDFAT),A
	LD	(_SETTMS),A
	LD	HL,(_FATBF1)
	LD	(_FATBF),HL
	LD	HL,DTBUF
	LD	(_DTBUF),HL
	LD	(_DIRBF),HL
	LD	(_GCBUF),HL
	LD	A,GCEH
	LD	(_GCEH),A
	LD	A,1
	LD	(_CLLN),A
	LD	HL,(_NOPEN+1)
	LD	DE,(_NOPENX+1)
	XOR	A
	SBC	HL,DE
	RET	NZ
	LD	HL,NOPEN
	LD	(_NOPEN+1),HL
	RET
;
CAP:
	CP	"a"
	RET	C
	CP	"z"+1
	RET	NC
	SUB	$20
	RET
;
HLHEX:
	PUSH	HL
	PUSH	DE
	CALL	HLHEX5
	JR	C,HLHEX3
	LD	L,A
	LD	H,0
HLHEX1:
	CALL	HLHEX6
	JR	C,HLHEX2
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	A,L
	LD	L,A
	LD	A,$00
	ADC	A,H
	LD	H,A
	JR	HLHEX1
HLHEX2:
	POP	AF
	POP	AF
	XOR	A
	RET
HLHEX3:
	POP	DE
	POP	HL
	SCF
	RET
HLHEX4:
	INC	DE
HLHEX5:
	LD	A,(DE)
	CP	$20
	JR	Z,HLHEX4
HLHEX6:
	LD	A,(DE)
	CALL	_HEX
	RET	C
	INC	DE
	RET
AHEX:
	PUSH	BC
	PUSH	DE
	CALL	HLHEX5
	JR	C,AHEX3
	LD	C,A
	CALL	HLHEX6
	JR	C,AHEX1
	LD	B,A
	LD	A,C
	RLCA
	RLCA
	RLCA
	RLCA
	ADD	A,B
	LD	C,A
AHEX1:
	LD	A,C
AHEX2:
	POP	BC
	POP	BC
	OR	A
	RET
AHEX3:
	LD	A,(DE)
	CP	$3B
	JR	Z,AHEX4
	POP	DE
	POP	BC
	SCF
	RET
AHEX4:
	INC	DE
	LD	A,(DE)
	INC	DE
	JR	AHEX2
;
EXBAT2:
	CALL	_FGETC
	RET	C
	CP	$FE
	JP	Z,MSXBIN
;
	CALL	_KYBFC
	LD	B,0
XBAT0:
	AND	A
	DEC	B
	RET	Z
	CP	"%"
	JR	Z,XBAT2
	CP	$1A
	RET	Z
	CP	$0A
	CALL	NZ,_KYBFS
XBAT1:
	CALL	NC,_FGETC
	JR	NC,XBAT0
	RET
;
XBAT2:
	CALL	_FGETC
	RET	C
	DEC	A
	AND	7
	INC	A
	LD	C,A
	LD	DE,(EXPAT+1)
XBAT3:
	LD	A,(DE)
	OR	A
	JR	Z,XBAT1
	CALL	_SPSK
	DEC	C
	JR	Z,XBAT5
XBAT4:
	LD	A,(DE)
	CP	$21
	JR	C,XBAT3
	INC	DE
	JR	XBAT4
XBAT5:
	LD	A,(DE)
	CP	$21
	CCF
	JR	NC,XBAT1
	CALL	_KYBFS
	INC	DE
	JR	XBAT5
;
MONMZ2:
	INC	HL
	LD	(MONDADD+1),HL
	JR	MONS
;
MONMZ:
	LD	A,"."
	CALL	_PRINT
	LD	HL,(MONDADD+1)
	CALL	_PRTHL
	CALL	_PRNTS
	LD	A,(HL)
	CALL	_PRTHX
	LD	A,"-"
	JR	MONMX
MONS:
MONMF:	LD	A,$00
	OR	A
	JR	NZ,MONMZ
MON:
	LD	A,"*"
MONMX:
	CALL	_PRINT
	LD	DE,(_KBFAD)
	CALL	_GETL
	CALL	MONANL
	JR	MONS
MONANL:
	LD	A,(DE)
	INC	DE
	CP	"*"
	JR	NZ,MONA2
	LD	A,(DE)
	CALL	CAP
	INC	DE
	CP	"T"
	JP	Z,MONT
	CP	"G"
	JP	Z,MONG
	CP	"M"
	JR	Z,MONM
	CP	"D"
	JR	Z,MOND
	CP	"F"
	JP	Z,MONF
	CP	"R"
	JR	Z,MONR
	CP	"!"
	RET	NZ
MONR:
	POP	AF
	XOR	A
	RET
;
MONM:
	LD	A,1
	LD	(MONMF+1),A
	CALL	_HLHEX
	RET	C
	JR	MONA6
;
MONA2:
	CP	":"
	JR	NZ,MONA2X
	CALL	_HLHEX
	RET	C
MONA3:
	LD	B,0
MONA4:
	CALL	_2HEX
	JR	C,MONA5
	LD	(HL),A
	INC	HL
	INC	B
	JR	MONA4
;
MONA5:
	LD	A,B
	OR	A
	JR	NZ,MONA6
	CALL	MONA6
MONME:
	XOR	A
	LD	(MONMF+1),A
	RET
;
MONA2X:
	CP	"."
	JR	NZ,MONME
	CALL	_HLHEX
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	CALL	_SPSK
	LD	A,(DE)
	OR	A
	JR	NZ,MONA3
	INC	HL
	JR	MONA6
;
MOND:
	CALL	_HLHEX
	JR	NC,MOND1
MONDADD:LD	HL,0
MOND1:
	PUSH	HL
	CALL	_HLHEX
	POP	DE
	JR	C,MOND1X
	SBC	HL,DE
	LD	A,H
	OR	A
	EX	DE,HL
	JR	NZ,MOND1X
	LD	A,E
	RRCA
	RRCA
	RRCA
	RRCA
	AND	$0F
	INC	A
	LD	C,A
	JR	MOND2
MOND1X:
	LD	C,16
MOND2:
	LD	A,(_WIDTH)
	ADD	A,-80
	SBC	A,A
	AND	8
	ADD	A,8
	LD	B,A
	CALL	MONDX
	DEC	C
	JR	NZ,MOND2
MONA6:
	LD	(MONDADD+1),HL
	XOR	A
	RET
;
MONF:
	CALL	_HLHEX
	RET	C
	LD	(MFST+1),HL
	CALL	_HLHEX
	RET	C
	LD	(MFSE+1),HL
	LD	HL,(_KBFAD)
	LD	B,-1
MONF1:
	INC	B
	CALL	_2HEX
	LD	(HL),A
	INC	HL
	JR	NC,MONF1
	LD	A,B
	OR	A
	RET	Z
	LD	(MFSL+1),A
;
MFSE:	LD	HL,0
MFST:	LD	DE,0
	SBC	HL,DE
	LD	B,H
	LD	C,L
	INC	BC
	LD	HL,(_KBFAD)
	EX	DE,HL
	LD	A,(DE)
MONF2:
	CPIR
	CALL	Z,MONFS
	RET	PO
	JR	MONF2
;
MONFS:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
MFSL:	LD	B,1
MONFS1:
	DEC	B
	JR	Z,MONFSX
	INC	DE
	LD	A,(DE)
	CP	(HL)
	INC	HL
	JR	Z,MONFS1
MONFSX2:
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET
;
MONFSX:
	POP	HL
	PUSH	HL
	DEC	HL
	LD	A,(MFSL+1)
	LD	B,A
	CALL	MONDX
	JR	MONFSX2
;
MONDX:
	PUSH	BC
	LD	A,":"
	CALL	_PRINT
	CALL	_PRTHL
MOND3:
	CALL	_PRNTS
	LD	A,(HL)
	INC	HL
	CALL	_PRTHX
	DJNZ	MOND3
	CALL	_MPRNT
	DB	" /",0
	POP	BC
	LD	D,0
	LD	E,B
	SBC	HL,DE
MOND4:
	LD	A,(HL)
	INC	HL
	CP	$20
	JR	NC,MOND5
	LD	A,"."
MOND5:
	CALL	_PRINT
	DJNZ	MOND4
	CALL	_PRNTS
	JP	_LTNL
;
MONG:
	CALL	_HLHEX
	RET	C
	JP	(HL)
;
MONT:
	CALL	_HLHEX
	RET	C
	LD	(MTS+1),HL
	CALL	_HLHEX
	RET	C
	LD	(MTE+1),HL
	CALL	_HLHEX
	RET	C
	LD	(MTD+1),HL
;
MTE:	LD	HL,0
MTS:	LD	DE,0
	SBC	HL,DE
	RET	C
	LD	B,H
	LD	C,L
	EX	DE,HL
;
MTD:	LD	DE,0
	PUSH	HL
	SBC	HL,DE
	POP	HL
	JR	C,MONT2
;
	INC	BC
	LDIR
	RET
;
MONT2:
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	INC	BC
	LDDR
	RET
