;
;	L-os Angeles INIT
;
	ORG	$2400
;
INIT:
	LD	SP,(_STKAD)
;
	LD	HL,_HOT
	LD	(_USR),HL
	LD	A,$CD
	LD	HL,$09A1
	LD	($010F),A
	LD	($0110),HL
;
	LD	BC,$1FC4
	LD	A,$0C
	OUT	(C),A
;
	LD	C,$C0
	DB	$ED,$71
;
	INC	C
	LD	A,40
	OUT	(C),A
;
	INC	C
	DB	$ED,$71
;
	INC	C
	DB	$ED,$71
;
	LD	C,$C5
	DB	$ED,$71
;
TEXT:
	LD	C,$B0
	LD	A,$80
	OUT	(C),A
	LD	D,7
	LD	C,$B9
	LD	HL,TEXTDT
TEXT1:
	INC	B
	OUTI
	INC	C
	DEC	D
	JR	NZ,TEXT1
	LD	BC,$1FB0
	DB	$ED,$71
;
	LD	C,$C4
	DB	$ED,$71
;
	LD	BC,$1A01	;printer init
	IN	A,(C)
	BIT	3,A
	JR	Z,PRN
	LD	C,$03
	LD	A,$0F
	OUT	(C),A
PRN:
	LD	A,$E6
	CALL	AT_COMOUT
	CALL	AT_IN49SB
	CALL	AT_IN49SB
	CP	$01
	JR	Z,NORMAL
	LD	BC,$3FD0
	OUT	(C),C
	LD	A,$37
	OUT	($D0),A
	IN	A,(C)
	CP	C
	JR	Z,INIT0
NORMAL:
	LD	HL,$2700	;for normal X1
	LD	DE,CPU
	LD	BC,CPUE-CPU
	LDIR
	LD	HL,$2700+CPUE-CPU
	LD	DE,SCR1
	LD	BC,AT_SCRE-AT_SCR1
	LDIR

	LD	HL,0
	LD	(X1PAT),HL

	LD	A,0AFH		;XOR	A
	LD	(X1KPAT),A

	LD	HL,SCRNX
	LD	(_SCRN+1),HL
;
	LD	HL,C_VER
	LD	(_VER+1),HL
	LD	HL,C_DRD
	LD	(_DRD+1),HL
	LD	HL,C_DWT
	LD	(_DWT+1),HL
	LD	HL,C_WTTRK
	LD	(_WTTRK+1),HL
	LD	HL,C_RDFAT
	LD	(_RDFAT+1),HL
	LD	HL,C_DRDW
	LD	(_DRDW+1),HL
	LD	HL,C_DWTW
	LD	(_DWTW+1),HL
;
	LD	HL,DWTPAT+1
	LD	(C_FDCPAT+1),HL
INIT0:
	LD	A,$1F
	IN	A,($F0)
	RRCA
	JR	C,INIT1
	CALL	_VER
	BIT	0,L
	JR	Z,INIT1
;
	LD	HL,CRTCD
	LD	DE,$DD
	LD	BC,20
	LDIR
	LD	A,3
	CALL	_S1FD0
;
INIT1:
	CALL	$09A1

	LD	A,80
	CALL	_WIDCH
;
	CALL	_MPRNT
	DB "L-os Angeles for X1",0
	CALL	_VER
	BIT	0,L
	LD	A,"/"
	CALL	Z,_PRINT
	CALL	_MPRNT
	DB	"turbo version ",0
	LD	A,(_LVER)
	ADD	A,"0"
	CALL	_PRINT
	LD	A,"."
	CALL	_PRINT
	LD	A,(_LVER+1)
	CALL	_PRTHX
	CALL	_MPRNT
	DB	" Gaku (Lovers/Tablacus)",13
	DB	"Ok",$0D,0
;
	BIT	0,L
	JR	Z,INITN1
	LD	A,($FF8C)
	LD	HL,(_M2HD)
	CP	2
	JR	Z,INIT2
INITN1:
	LD	HL,(_M2D)
INIT2:
	LD	(INIPAT+1),HL
;
	LD	A,"A"
INIT3:
	PUSH	AF
	CALL	_DEVADX
	PUSH	IY
	POP	DE
INIPAT:	LD	HL,M2D
	LD	BC,18
	LDIR
;
	LD	(IY+DPB_DEVNO),$87
;
	LD	HL,(_MTOFFA)
	LD	(IY+DPB_MOTOFF),L
	LD	(IY+DPB_MOTOFF+1),H
;
	LD	HL,(_GNCLA)
	LD	(IY+DPB_GNCL),L
	LD	(IY+DPB_GNCL+1),H
;
	LD	HL,(_SNCLA)
	LD	(IY+DPB_SNCL),L
	LD	(IY+DPB_SNCL+1),H
;
	LD	(IY+DPB_SDIR),0
	LD	(IY+DPB_SDIR+1),0
;
	LD	(IY+DPB_NAME),"F"
	LD	(IY+DPB_NAME+1),"D"
	LD	(IY+DPB_NAME+2),"D"
	POP	AF
	ADD	A,"0"-"A"
	LD	(IY+DPB_NAME+3),A
	SUB	"0"
	LD	(IY+DPB_UNITNO),A
	ADD	A,"A"
	INC	A
	CP	MAXDRV+$41
	JR	C,INIT3
;
	LD	A,"E"
;
INIT4:
	PUSH	AF
	CALL	_DEVADX
	LD	(IY+0),0
	LD	(IY+DPB_UNITNO),0
	LD	(IY+DPB_DEVNO),7
	LD	(IY+DPB_NAME),$20
	LD	(IY+DPB_NAME+1),$20
	LD	(IY+DPB_NAME+2),$20
	POP	AF
	INC	A
	CP	MAXDRV+$41
	JR	C,INIT4
;
	LD	A,($FF87)
	AND	3
	ADD	A,"A"
	LD	(AUTODV),A
	CALL	_SDVSW
	CALL	_CHGDRV
	CALL	FDMOFF
;
	LD	HL,_HOT
	PUSH	HL
	CALL	_KYBFC
	LD	A,$E6
	CALL	AT_COMOUT
	CALL	AT_IN49SB
	CALL	AT_IN49SB
	CP	3
	RET	Z
	CP	$1B
	RET	Z
;
	LD	HL,AUTOD
	LD	DE,(_KBFAD)
	LD	BC,AUTOE-AUTOD
	LDIR
	LD	DE,(_KBFAD)
	JP	_CEXE
;
AUTOD:	DB	"COMMAND "
AUTODV:	DB	"A:",0
AUTOE:
;
TEXTDT:	DB	$03,$0C,$0F,$30,$33,$3C,$3F
;
CRTCD:	DB	$35,$28,$2D,$84,$1B,$00,$19,$1A,$00,$0F
	DB	$00,$00,$00,$00,$00,$00,$6B,$50,$59,$88
;
INITE:
