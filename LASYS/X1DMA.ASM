;
;	L-os Angeles DMA
;
;	DISK I/O (DMA)
;
DMA:
;
VER:
	LD	HL,$2081
	RET
;
RDFAT0:
	CALL	RDFATS
	RET	C
	LD	HL,(_FATBF)
	LD	A,(HL)
	CP	(IY+DPB_FATID)
	JP	NZ,RDDERR
	RET
;
RDFATS:
	CALL	_DEVADR
	CALL	RDFATS1
	RET	NC
	CP	$FF
	SCF
	RET	Z
	LD	A,(IY+DPB_DEVNO)
	CP	$87
	SCF
	RET	NZ
	BIT	0,(IY+DPB_FDMODE)
	LD	HL,(_M2D)
	JR	Z,CHG2D
	LD	HL,(_M2HD)
CHG2D:
	PUSH	IY
	POP	DE
	LD	BC,18
	LDIR
	LD	A,(_DSK)
	CALL	_CHGDRV
RDFATS1:
	CALL	FATS
RDFATL:
	PUSH	BC
	CALL	_DRDC
	POP	BC
	RET	C
	DJNZ	RDFATL
	RET
;
WTTRK:
	LD	A,$F0
	JR	WTTRK1
;
DWTW:
	CALL	DWT	;FOR 512/sector
	RET	C
;
DWT:
	LD	A,$A0
WTTRK1:
	LD	(FDCPAT+1),A
	LD	A,16
	DI
	JR	DISK
;
RDID:
	LD	A,$C0
	JR	RDID1
;
DRDW:
	CALL	DRD	;FOR2D
	RET	C
;
DRD:
	LD	A,$80
;
RDID1:
	LD	(FDCPAT+1),A
	LD	A,14
;
DISK:
	LD	(DMAPAT+1),A
;
	LD	A,5		;リトライカウント
	LD	(RETRY),A
;
	LD	(DMAD+11),HL
DISK2:
	PUSH	DE
	CALL	SEEK
	JR	C,ERRF
;
DMAPAT:	LD	A,14
	LD	HL,DMAD
	LD	BC,$1F87
SDMA:
	INC	B
	OUTI
	DEC	A
	JR	NZ,SDMA
	OUT	(C),C		;OUT $1F87,$87
FDCPAT:	LD	A,$80		;FDC COMMAND 書き換え
	PUSH	BC
	CALL	SECSET
	LD	HL,(DMAD+11)
	INC	HL
	LD	DE,$BB06	;READ DMA
;
	LD	A,3
	CALL	READY
	IN	A,(C)
;
	POP	BC
	OUT	(C),D
	OUT	(C),E
	IN	E,(C)
	IN	D,(C)
	ADD	HL,DE
	POP	DE
	INC	DE
	EI
	CALL	FDMOFF
	OR	A
	RET	Z
	DEC	DE
	BIT	4,A
	JR	NZ,RNF
DISKE2:
	LD	HL,RETRY
	DEC	(HL)
	JR	NZ,DISK2
	PUSH	DE
ERRF:
	POP	DE
	LD	A,$FF
ERR:
	CP	A
	SCF
	JP	FDMOFF
;
RNF:
	LD	HL,(_DRIVE)
	LD	H,$1F
	LD	A,(HL)
	LD	(HL),$FF
	OR	A
	JR	Z,ERR
;
	LD	A,2
	LD	(_SEEKSP),A
	JR	DISKE2
;
BIOSE:
;
