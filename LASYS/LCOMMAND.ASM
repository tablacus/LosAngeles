;
;	L-os Angeles COMMAND
;
	ORG	$2100
;
HOT:
	LD	SP,(_STKAD)
	CALL	_CINIT
	LD	A,$00
	CALL	_PRINT
	LD	DE,(_KBFAD)
	CALL	_GETL
	CALL	_CMDANL
	CALL	C,_ERROR
	JR	HOT
;
CMDANL:				;$211B
	LD	A,(DE)
	CP	"*"
	RET	Z
	CP	"#"
	INC	DE
	JR	Z,CMDSOS
	DEC	DE
	CALL	_SPSK
	INC	DE
	LD	A,(DE)
	DEC	DE
	CP	$21
	JP	NC,_CEXE
CMDSOS:
	LD	A,(DE)
	PUSH	AF
	CALL	SS1
	POP	AF
	CALL	_CAP
	JP	_EXCMD
;
EXCMD:
	CP	$20
	JP	Z,_CEXE
	CP	"!"
	JP	Z,BOOT
	CP	"D"
	JP	Z,_CDIR
	CP	"M"
	JP	Z,_MON
	CP	"P"
	JP	Z,C_PATH
	CP	"L"
	JR	Z,C_LOAD
	CP	"N"
	JR	Z,C_NAME
	CP	"K"
	JR	Z,C_KILL
	CP	"S"
	JR	Z,C_SAVE
	CP	"J"
	JR	Z,C_JUMP
	CP	"W"
	JR	Z,C_WITH
	CP	"V"
	JR	Z,C_DVSW
;
	AND	A
	RET
;
C_WITH:
	LD	A,(_WIDTH)
	SUB	41
	JP	_WIDCH
;
C_JUMP:
	CALL	_HLHEX
	RET	C
	POP	BC
	LD	BC,_HOT
	PUSH	BC
	JP	(HL)
;
C_LOAD:
	CALL	_FILE
	RET	C
	PUSH	DE
	CALL	_ROPEN
	POP	DE
	RET	C
	CALL	CCHEX
	JR	C,CLD1
	LD	(_DTADR),HL
CLD1:
	CALL	FPRNTW
	CALL	_RDD
	JR	CLOADX
;
C_NAME:
	CALL	_FILE
	RET	C
	LD	A,(DE)
	CP	$20
	JP	Z,_NAME
	SCF
	RET
;
C_KILL:
	CALL	_FILE
	RET	C
	JP	_KILL
;
C_SAVE:
	LD	A,1
	CALL	_FILE
	RET	C
;
	CALL	CCHEX
	RET	C
	LD	(_DTADR),HL
;
	CALL	CCHEX
	RET	C
	LD	BC,(_DTADR)
	SBC	HL,BC
	INC	HL
	LD	(_SIZE),HL
	LD	L,C		;_DTADR
	LD	H,B
	CALL	CCHEX
	LD	(_EXADR),HL
;
	PUSH	DE
	CALL	_WOPEN
	POP	DE
	RET	C
;
	CALL	CCHEX
	JR	C,CSV1
	LD	(_DTADR),HL
;
CSV1:
	CALL	_WRD
CLOADX:
	RET	C
;
	CALL	_MPRNT
	DB	"Ok",$0D,0
	RET
;
C_DVSW:
	LD	A,(DE)
	JR	CDVSW1
;
C_DIR:
	CALL	_FILE
	RET	C
	LD	A,(FNAME)
	CP	$20
	LD	DE,DIRW
	CALL	Z,_FILED
	JP	_DIRX
;
C_EXE:
	LD	HL,PATHD
CEX1:
	LD	A,(HL)
	INC	HL
	CP	"@"
	RET	C
	CALL	_CHGDRV
	JR	C,CEX1
	PUSH	HL
	PUSH	DE
	CALL	_FILEX
	JR	C,CEXX
	INC	DE
	LD	(EXPAT+1),DE
	LD	A,(FNAME)
	CP	$20
	JR	Z,SDVSWX
	CALL	CEX3
CEXX:
	POP	DE
	POP	HL
	JR	CEX1
;
SDVSWX:
	POP	DE
	POP	DE
	LD	A,(_DSK)
CDVSW1:
	JP	_SDVSW
;
CEX3:
	LD	HL,FNAME+8
	LD	A,(HL)
	CP	$20
	JR	NZ,CEX4
	LD	(HL),"B"	;B??
	INC	HL
	LD	(HL),"?"
	INC	HL
	LD	(HL),"?"
CEX4:
	CALL	_ROPEN
	RET	C
	POP	DE
	POP	DE
	POP	DE
	LD	A,(FNAME+$10)
	CP	$A0
	JP	NZ,_EXBAT
CEX5:
	CALL	_RDD
	RET	C
MSXB3:
	LD	HL,(_EXADR)
EXPAT:	LD	DE,0
	XOR	A
	JP	(HL)
;
MSXBIN:
	LD	HL,(_GCBUF)
	INC	HL
	LD	E,(HL)		;開始番地
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(_DTADR),DE
	LD	C,(HL)		;終了番地
	INC	HL
	LD	B,(HL)
	INC	HL
	PUSH	HL
	LD	HL,(_MEMAX)
	SBC	HL,BC
	POP	HL
	RET	C
	LD	C,(HL)		;実行番地
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	(_EXADR),BC
;
	LD	BC,(_SIZE)
	DEC	BC
	LD	A,B
	CP	4
	JR	NC,MSXB2
	DEC	BC		;1 ｸﾗｽﾀ ﾉ ｼｮﾘ
	DEC	BC
	DEC	BC
	DEC	BC
	DEC	BC
	DEC	BC
	LDIR
	JR	MSXB3
MSXB2:
	LD	BC,$0400-7	;2 ｸﾗｽﾀ ｲｼﾞｮｳ
	LDIR
	EX	DE,HL
	LD	B,64
	CALL	RDD1
	JR	MSXB3
;
EXBAT:
	CALL	_FGETC
	RET	C
	CP	$FE
	JR	Z,MSXBIN
;
	CALL	_KYBFC
BAT1:
	OR	A
	RET	Z
	CP	$1A
	RET	Z
	CP	$0A
	CALL	NZ,_KYBFS
	CALL	_FGETC
	JR	NC,BAT1
	RET
;
C_PATH:
	EX	DE,HL
	LD	DE,PATHD
	LD	A,(HL)
	OR	A
	JR	NZ,CPATH0
;
	CALL	_MSX
	JP	_LTNL
;
CPATH0:
	LD	B,PATHX
CPATH1:
	LD	A,(HL)
	OR	A
	JR	Z,CPATH2
	CP	"@"
	JR	NC,CPATH2
	LD	A,"@"
CPATH2:
	LD	(DE),A
	INC	DE
	INC	HL
	DJNZ	CPATH1
	AND	A
	RET
;
CCHEX:
	CALL	CCHECK
	SCF
	RET	NZ
	LD	A,(DE)
	CP	$21
	RET	C
	JP	_HLHEX
;
SPS1:
	INC	DE
SPS2:
	LD	A,(DE)
	CP	$21
	JR	NC,SPS1
	RET
;
SS1:
	INC	DE
SSKIP:
	LD	A,(DE)
	CP	$20
	JR	Z,SS1
	RET
;
PATHX	EQU	10
;
PATHD:	DB	"@"
	DS	PATHX-1
	DB	0
;
CMDE:
