;
;	L-os Angeles IO
;
;	MSX-DOS
;
CSR:
	LD	HL,(XYADR)
	RET
SCRN:
	LD	A,$20
	RET
;
SYSTEM:
	EXX
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	PUSH	IY
	EXX
	CALL	$0005
	EXX
	POP	IY
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	EXX
	RET
;
PRINT:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	AF
	LD	E,A
	CP	$1B
	JR	C,CTRL
PRINT1:
	LD	HL,(XYADR)
	LD	H,0
	LD	A,L
	SUB	80
	JR	C,PR1
	LD	L,0
PR1:
	INC	L
	LD	(XYADR),HL
	DEC	L
	LD	BC,PRBF
	ADD	HL,BC
	LD	(HL),E
;
PRINT2:
	LD	C,$06		;INPOUT
	CALL	SYSTEM
PRINT3:
	POP	AF
	POP	HL
	POP	DE
	POP	BC
	AND	A
	RET
;
PCLR:
	XOR	A
	LD	(XYADR),A
	RET
;
CTRL:
	CP	$0D
	JR	Z,CTRL_M
	CP	7
	JR	Z,PRINT2
	CP	$0C
	JR	Z,CTRL_L
	CP	$1A
	JR	Z,CTRL_Z
	CP	$0B
	JR	Z,CTRL_K
	CP	9
	JR	Z,CTRL_I
	JR	PRINT3
CTRL_M:
	CALL	PCLR
	LD	C,$06		;INPOUT
	CALL	SYSTEM
	LD	E,$0A		;CRLF
	JR	PRINT2
CTRL_L:
	CALL	PCLR
	CALL	ZPRINT
CTLX1:	DB	$1B,"E",0
	JR	PRINT3
CTRL_Z:
	CALL	ZPRINT
CTZX1:	DB	$1B,"J",0
	JR	PRINT3
CTRL_K:
	CALL	PCLR
	CALL	ZPRINT
CTKX1:	DB	$1B,"H",0
	JR	PRINT3
CTRL_I:
	LD	A,(XYADR)
	AND	7
	LD	B,A
	LD	A,8
	SUB	B
	LD	B,A
CTRL_I1:
	LD	A,$20
	CALL	PRINT
	DJNZ	CTRL_I1
	JR	PRINT3
;
LOC:
	CALL	PCLR
	LD	A,H
	ADD	A,$20
	LD	(LOCY),A
	LD	A,L
	ADD	A,$20
	LD	(LOCX),A
	CALL	ZPRINT
	DB	$1B,"Y"
LOCY:	DB	$20
LOCX:	DB	$20
	DB	0
	AND	A		;CF=0
	RET
;
ZPRINT:
	EX	(SP),HL
ZPRNT1:
	LD	A,(HL)
	INC	HL
	OR	A
	JR	Z,ZPRNT2
	CALL	ZPRNT3
	JR	ZPRNT1
ZPRNT2:
	EX	(SP),HL
	RET
;
ZPRNT3:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	E,A
	LD	C,$06		;INPOUT
	CALL	SYSTEM
	POP	HL
	POP	DE
	POP	BC
	RET
;
BELL:
	PUSH	AF
	LD	A,7
	JR	PRNT01
;
PRNT0:
	PUSH	AF
	CP	$20
	JR	NC,PRNT01
	LD	A,$20
PRNT01:
	CALL	PRINT
	POP	AF
	RET
;
NL:
	PUSH	AF
	LD	A,(XYADR)
	OR	A
	JR	Z,NLX
	CALL	ZPRINT
	DB	$0D,$0A,0
	CALL	PCLR
NLX:
	POP	AF
	RET
;
GETL:
	PUSH	BC
	PUSH	HL
	PUSH	DE
	CALL	FGETL
	JR	NC,ZGETL2
	CALL	_KYBFC
;
	LD	DE,KBFAD0
	LD	A,80
	LD	(DE),A
	LD	C,$0A		;GETS
	CALL	SYSTEM
ZGETL2:
	POP	DE
	PUSH	DE
	LD	B,80
	LD	A,(KBFADX)	;break
	CP	$0D
	JR	Z,GETP0
	CP	9
	JR	Z,GETP0
	CP	$20
	JR	NC,GETP0
	LD	A,$1B
	LD	(DE),A
	INC	DE
	DEC	B
	JR	GETL2
GETP0:
	LD	A,(XYADR)
	OR	A
	JR	Z,GETL0
	LD	C,A
	LD	HL,PRBF
GETP1:
	LD	A,(HL)
	INC	HL
	LD	(DE),A
	INC	DE
	DEC	B
	JR	Z,GETL3
	DEC	C
	JR	NZ,GETP1
GETL0:
	LD	HL,KBFADX
GETL1:
	LD	A,(HL)
	INC	HL
	CP	9
	JR	NZ,GETLT
	LD	A,$20
	JR	GETLT2
GETLT:
	CP	$20
	JR	C,GETL2
GETLT2:
	LD	(DE),A
	INC	DE
	DJNZ	GETL1
	JR	GETL3
GETL2:
	XOR	A
	LD	(DE),A
	INC	DE
	DJNZ	GETL2
GETL3:
	POP	DE
	POP	HL
	POP	BC
	LD	A,$0D
	JP	PRINT
;
GETKY:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	E,$FF
	LD	C,$06		;INPOUT
	JR	GETKY1
;
FLGET:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	C,$07		;INKEY
GETKY1:
	CALL	SYSTEM
	POP	HL
	POP	DE
	POP	BC
XFLGET:	CP	4
	JR	NZ,FL1
	LD	A,$1C
	RET
FL1:
	CP	5
	JR	NZ,FL2
	LD	A,$1E
	RET
FL2:
	CP	$13
	JR	NZ,FL3
	LD	A,$1D
	RET
FL3:
	CP	$18
	JR	NZ,FL4
	LD	A,$1F
	RET
FL4:
	AND	A
	RET
;
LPRNT:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	E,A
	LD	C,$05		;PRNOUT
	CALL	SYSTEM
	POP	HL
	POP	DE
	POP	BC
	ADD	A,$01
	RET
;
WIDCH:
	CP	41
	JR	NC,WIDCH1
	CALL	_MPRNT
	DB	$0C,"40",$0D,0
	LD	A,40
	JR	WIDCH2
;
WIDCH1:
	CALL	_MPRNT
	DB	$0C,"80",$0D,0
	LD	A,80
WIDCH2:
	LD	(_WIDTH),A
	XOR	A
	RET
;
CHGDRV:
	PUSH	BC
	PUSH	HL
	PUSH	IY
	CALL	CHGDRV0
	LD	A,(_DSK)
	POP	IY
	POP	HL
	POP	BC
	RET
;
TAB:
	PUSH	HL
	CALL	_CSR
	LD	A,L
	POP	HL
	SUB	B
	CCF
	RET	C
;
TAB1:
	CALL	_PRNTS
	INC	A
	JR	NZ,TAB1
	RET
;
CHGDRV0:
	CP	$40
	RET	C
	CALL	Z,_RDVSW
;
	LD	C,A
	CALL	_DEVADX
	RET	C
;
	LD	A,C
	CALL	_CAP
	LD	(_DSK),A
	CALL	_MOTOFF
;
	LD	L,(IY+DPB_DRDC)
	LD	H,(IY+DPB_DRDC+1)
	LD	(_DRDC+1),HL
;
	LD	L,(IY+DPB_DWTC)
	LD	H,(IY+DPB_DWTC+1)
	LD	(_DWTC+1),HL
;
	LD	L,(IY+DPB_ADDCL)
	LD	H,(IY+DPB_ADDCL+1)
	LD	(CLSPAT+1),HL
;
	LD	A,H
	LD	HL,$2424
	OR	A
	JR	Z,CHGDRV1
	LD	HL,0
CHGDRV1:
	LD	(INCDW),HL
;
	LD	A,(IY+DPB_MAXCL)
	LD	(CLPAT2+1),A
	LD	A,(IY+DPB_MAXCL+1)
	LD	(CLPAT1+1),A
;
	LD	A,(IY+DPB_MAXDIR)
	LD	(RTEPAT+1),A
;
	LD	A,(IY+DPB_MAXCYL)
	LD	(_MXTRK),A
	LD	A,(IY+DPB_MAXSEC)
	LD	(_MSEC),A
;
	LD	H,0
	LD	L,(IY+DPB_FATPS)
	LD	(_FATPS),HL
;
	LD	A,(IY+DPB_INCSEC)
	LD	(SECPAT+1),A
	LD	H,0
	LD	L,(IY+DPB_DIRPS)
	LD	(_DIRPS),HL
;
	LD	A,(IY+DPB_UNITNO)
	LD	(_DRIVE),A
;
	LD	L,(IY+DPB_MOTOFF)
	LD	H,(IY+DPB_MOTOFF+1)
	LD	(_MOTOFF+1),HL
;
	LD	L,(IY+DPB_GNCL)
	LD	H,(IY+DPB_GNCL+1)
	LD	(_GNCL+1),HL
;
	LD	L,(IY+DPB_SNCL)
	LD	H,(IY+DPB_SNCL+1)
	LD	(_SNCL+1),HL
;
	AND	A
	RET
;
BOOT:
	LD	HL,0
	LD	($0001),HL
	JP	0
;
POKE:
	PUSH	BC
	LD	B,$40		;write
	CALL	ADR
	OUT	(PORT0),A
	JR	POKE1
;
PEEK:
	PUSH	BC
	LD	B,0		;read
	CALL	ADR
	IN	A,(PORT0)
POKE1:
	POP	BC
	PUSH	AF
	LD	A,($FFED)
	OUT	(PORT1),A
	LD	A,$80+14
	OUT	(PORT1),A
	POP	AF
	AND	A
	EI
	RET
;
ADR:			; read B=$40 : write B=$00
	PUSH	AF
	LD	A,H
	ADD	A,$40		;use $4000-
	LD	C,A
	RLCA
	RLCA
	AND	$03		;bit16-14
	DI
	OUT	(PORT1),A
	LD	A,$80+14	;R_14
	OUT	(PORT1),A
	NOP			;wait
	NOP
	NOP
	NOP
	LD	A,L		;bit7-0
	OUT	(PORT1),A
	LD	A,C
	AND	$3F
	OR	B
	OUT	(PORT1),A	;bit13-8 and flag
	POP	AF
	RET
;
POKEAT_:
	EX	DE,HL
POKEAT_1:
	LD	A,(DE)
	INC	DE
	CALL	_POKE
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,POKEAT_1
	RET
;
PEEKAT_:
	EX	DE,HL
PEEKAT_1:
	CALL	_PEEK
	INC	HL
	LD	(DE),A
	INC	DE
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,PEEKAT_1
	RET
;
;
;	SUBMIT
;
;
KYBFC:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,0
	LD	(SUBPS),HL
KEYBC1:
	LD	E,$FF
	LD	C,$06
	CALL	SYSTEM
	OR	A
	JR	NZ,KEYBC1
;
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET
;
KYBFS:
	OR	A
	RET	Z
	PUSH	HL
	PUSH	AF
	LD	HL,(SUBPS)
	INC	L
	LD	A,L
	CP	H
	JR	Z,KEYBE
	LD	(SUBPS),A
	LD	H,SUBBF/256
	POP	AF
	LD	(HL),A
	POP	HL
	AND	A
	RET
;
KEYBE:
	POP	AF
	POP	HL
	SCF
	RET
;
FGETL:
	CALL	FGETL1
	RET	C
;
	LD	HL,$0D00
	LD	(KBFAD1),HL
	RET
;
FGETL1:
	CALL	_BRKEY
	SCF
	RET	Z
	CALL	INKBF
;
	CP	$0D
	RET	Z
	CP	$0A
	JR	Z,FGETL1
;
	OR	A
	SCF
	RET	Z
	CP	$1A
	SCF
	RET	Z
;
	CP	$09
	JR	NZ,FGETL2
	LD	A,$20
FGETL2:
	CALL	PRINT
	JR	FGETL1
;
INKBF:
	PUSH	HL
	LD	HL,(SUBPS)
	LD	A,H
	XOR	L
	JR	Z,INKBF1
	LD	A,H
	INC	A
	LD	(SUBPS+1),A
	LD	L,A
	LD	H,SUBBF/256
	LD	A,(HL)
	SCF
INKBF1:
	POP	HL
	RET
;
;
;	TIME
;
PRTTMS:
;
	LD	A,(_WIDTH)
	CP	80
	RET	C
;
	CALL	_PRNTS
	CALL	_PRNTS
;
	LD	A,(IY+24)
	OR	A
	RET	Z
	LD	L,A
;
	LD	H,(IY+25)
	SRL	H
	RLA
	RLA
	RLA
	RLA
	AND	$0F
	LD	D,A
	LD	A,H
	ADD	A,80
	CALL	PRTA
	LD	A,"-"
	CALL	_PRINT
	LD	A,D
	CALL	PRTA
	LD	A,"-"
	CALL	_PRINT
	LD	A,L
	AND	$1F
	CALL	PRTA
;
	LD	L,(IY+22)
	LD	H,(IY+23)
;
	CALL	_PRNTS
	LD	A,H
	LD	H,L
	RRA
	RR	L
	RRA
	RR	L
	RRA
	RR	L
	AND	$1F
	CALL	PRTA
	LD	A,":"
	CALL	_PRINT
	LD	A,L
	RRCA
	RRCA
	AND	$3F
	CALL	PRTA
	LD	A,":"
	CALL	_PRINT
	LD	A,H
	AND	$1F
	ADD	A,A
;
;	PRINT10
;
PRTA:
	SUB	100
	JR	NC,PRTA
	ADD	A,100
	LD	B,"0"-1
PRTA1:
	SUB	10
	INC	B
	JR	NC,PRTA1
	ADD	A,10
	LD	C,A
	LD	A,B
	CALL	_PRINT
	LD	A,C
	ADD	A,"0"
	JP	_PRINT
;
SETTMS:
	PUSH	IX
	PUSH	IY
	LD	C,$2C
	CALL	SYSTEM
;
	SLA	L
	SLA	L
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	A,D
	RRCA
	AND	$1F
	OR	L
	LD	L,A
	LD	(FNAME+$16),HL
;
	LD	C,$2A
	CALL	SYSTEM
;
	LD	BC,-1980
	ADD	HL,BC
	LD	H,L
;
	LD	A,D
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	L,A
	ADD	HL,HL
	LD	A,L
	OR	E
	LD	L,A
	LD	(FNAME+$18),HL
	POP	IY
	POP	IX
	RET
;
;	L-os Angeles MDX1
;
XPOKE:
	PUSH	BC
	PUSH	AF
	LD	C,L
	LD	A,H
	ADD	A,$40
	LD	B,A
	POP	AF
	OUT	(C),A
	POP	BC
	RET
;
XPEEK:
	PUSH	BC
	LD	C,L
	LD	A,H
	ADD	A,$40
	LD	B,A
	IN	A,(C)
	POP	BC
	RET
;
XPRINT:
	PUSH	AF
	CP	$0D
	JR	NZ,XPRNT2
	LD	A,3
XPRNT2:
	CP	8
	JR	NZ,XPRNT3
	LD	A,2
XPRNT3:
	CP	$1A
	JR	NZ,XPRNT4
	LD	A,5
XPRNT4:
	PUSH	DE
	LD	E,A
BASE1:	CALL	$CD		;_PRINT
	POP	DE
	POP	AF
	AND	A
	RET
;
XNL:
	PUSH	AF
	LD	A,4
	CALL	XPRINT
	POP	AF
	RET
;
XSCRN:
	PUSH	BC
	PUSH	HL
	CALL	LOC1
BASE2:	CALL	$D0		;_SCRN
	POP	HL
	POP	BC
	RET
;
XCSR:
	PUSH	BC
BASE3:	CALL	$D3		;_POS
	POP	BC
	RET
;
XLOC:
	PUSH	BC
	PUSH	HL
BASE3X:	CALL	$D6		;_LOC
	POP	HL
	POP	BC
	RET
;
LOC1:
	LD	C,L
	LD	B,0
	LD	A,H
	ADD	A,A
	ADD	A,A
	ADD	A,H
	ADD	A,A	;*10
	LD	L,A
	LD	H,B
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,BC
	LD	A,H
	OR	$30
	LD	B,A
	LD	C,L
	RET
;
XGETKY:
	CALL	GETKY
	OR	A
	RET	NZ
BASE4:	LD	A,($92)		;_KEYD
	RET
;
XGETL:
	PUSH	BC
	PUSH	HL
	PUSH	DE
XGETL1:
	CALL	INKBF
	CALL	NC,_FLGET
	CP	$1B
	JR	NZ,XGETL1Z
	CALL	_FLGET
	CALL	ZPRINT
	DB	$0D,$05,0
	POP	DE
	PUSH	DE
XGETL1L:
	LD	A,(DE)
	INC	DE
	CP	$20
	JR	C,XGETL1Y
	CALL	XPRINT
	JR	XGETL1L
XGETL1Y:
	XOR	A
XGETL1Z:
	CP	$0D
	JR	Z,XGETL1X
	CALL	XPRINT
	CP	3
	JR	Z,XGETBK
	JR	XGETL1
XGETL1X:
	CALL	_CSR
	LD	L,0
	POP	DE
	PUSH	DE
	XOR	A
	LD	B,80
XGETL0:
	LD	(DE),A
	INC	DE
	DJNZ	XGETL0
	POP	DE
	PUSH	DE
	LD	B,80
XGETL2:
	CALL	_SCRN
	INC	L
	LD	(DE),A
	INC	DE
	DJNZ	XGETL2
;
	LD	B,80
XGETL3:
	DEC	DE
	LD	A,(DE)
	CP	$21
	JR	NC,XGETLE
	XOR	A
	LD	(DE),A
	DJNZ	XGETL3
XGETLE:
	LD	A,$0D
	CALL	XPRINT
	XOR	A
	POP	DE
	POP	HL
	POP	BC
	RET
;
XGETBK:
	POP	DE
	LD	A,$1B
	LD	(DE),A
	INC	DE
	XOR	A
	LD	(DE),A
	DEC	DE
	POP	HL
	POP	BC
	RET
;
XCOLOR:
BASE5:	LD	($96),A		;_COLORF
	RET
;
XS1FD0:
	PUSH	BC
	LD	BC,$1FD0
	OUT	(C),A
	LD	(_WK1FD0),A
BASE9:	LD	($BF),A		;WK1FD0
	POP	BC
	RET
;
