;
;	L-os Angeles INIT
;
INIT:
	LD	SP,(_STKAD)
;
	LD	HL,($0001)
	LD	(BOOT+1),HL
;
	LD	HL,_HOT
	LD	(_USR),HL
	LD	($0001),HL
	LD	HL,($0006)
	LD	(_MEMAX),HL
	LD	(_MEMAX1),HL
	LD	HL,EXBAT2
	LD	(_EXBAT+1),HL
;
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	JP	NZ,INIT1
	LD	A,L
	ADD	A,A
	JP	NZ,INIT1
;				;for X1 LSX-Dodgers
	LD	A,($000B)
	LD	(BASE1+2),A
	LD	(BASE2+2),A
	LD	(BASE3+2),A
	LD	(BASE3X+2),A
	LD	(BASE4+2),A
	LD	(BASE5+2),A
	LD	(BASE6+2),A
	LD	(BASE7+2),A
	LD	(BASE8+2),A
	LD	(BASE9+2),A
;
	LD	HL,XPOKE
	LD	(_POKE+1),HL
	LD	HL,XPEEK
	LD	(_PEEK+1),HL
	LD	HL,XPRINT
	LD	(_PRINT+1),HL
	LD	HL,XLOC
	LD	(_LOC+1),HL
	LD	HL,XCSR
	LD	(_CSR+1),HL
	LD	HL,XSCRN
	LD	(_SCRN+1),HL
	LD	HL,XGETL
	LD	(_GETL+1),HL
	LD	HL,XNL
	LD	(_NL+1),HL
	LD	HL,XGETKY
	LD	(_GETKY+1),HL
	LD	HL,XCOLOR
	LD	(_COLOR+1),HL
	LD	HL,XCINIT
	LD	(_CINIT+1),HL
	LD	HL,XS1FD0
	LD	(_S1FD0+1),HL
	LD	HL,$C9A7
	LD	(XFLGET),HL
	LD	HL,$A281
	LD	(VER+1),HL
BASE6:	LD	A,($97)		;_LINE
	LD	(_MAXLN),A
BASE7:	LD	A,($BF)		;WK1FD0
	LD	(_WK1FD0),A
	JR	INIT2
INIT1:
	LD	A,($F3B0)	;MSX LINLEN
	LD	(_WIDTH),A
	CALL	ZPRINT
	DB	$1B,"x5",0
INIT2:
	CALL	_MPRNT
	DB	$0C,"L-os Angeles for "
	DB	"MSX-DOS version ",0
	LD	A,(_LVER)
	ADD	A,"0"
	CALL	_PRINT
	LD	A,"."
	CALL	_PRINT
	LD	A,(_LVER+1)
	CALL	_PRTHX
	CALL	_MPRNT
	DB	$A5,"Gaku (Lovers/Tablacus)",13
	DB	"Ok",$0D,0
;
	CALL	INIDPB
	LD	C,$19
	CALL	SYSTEM
	PUSH	AF
	LD	C,$0D
	CALL	SYSTEM
	POP	AF
	LD	E,A
	PUSH	AF
	LD	C,$0E
	CALL	SYSTEM
	POP	AF
	ADD	A,"A"
	LD	(AUTODV),A
	CALL	_SDVSW
	CALL	_CHGDRV
;
	LD	HL,_HOT
	PUSH	HL
	CALL	_BRKEY
	RET	Z
	LD	HL,$0080
	LD	A,(HL)
	OR	A
	JR	Z,EXEAUTO
	INC	HL
	LD	C,A
	LD	B,0
	LD	DE,AUTOP
	LDIR
EXEAUTO:
	LD	DE,AUTOD
	CALL	_CEXE
	LD	DE,AUTOP1
	JP	_CEXE
;
INIDPB:
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	JR	NZ,INIDPB1
	LD	A,L
;	ADD	A,A
	AND	A
	JR	C,INIDPBX
;
INIDPB1:
	LD	A,"A"
INIT3:
	PUSH	AF
	CALL	_DEVADX
	PUSH	IY
	POP	DE
	LD	HL,M2D
	LD	BC,18
	LDIR
;
	LD	(IY+DPB_DEVNO),$87
;
	CALL	INIDPBS
	LD	(IY+DPB_SDIR),0
	LD	(IY+DPB_SDIR+1),0
;
	LD	(IY+DPB_NAME),"D"
	LD	(IY+DPB_NAME+1),"O"
	LD	(IY+DPB_NAME+2),"S"
INIT4:
	POP	AF
	LD	(IY+DPB_NAME+3),A
	SUB	"A"
	LD	(IY+DPB_UNITNO),A
	ADD	A,"A"+1
	CP	"H"+1
	JR	C,INIT3
	RET
;
INIDPBX:
	LD	HL,($000B-1)
	LD	L,$DC
	XOR	A
	CALL	$000F
	PUSH	IX
	POP	HL
	LD	DE,DEVICE
	LD	BC,256
	LDIR
;
INIDPBS:
	LD	HL,(_MTOFFA)
	LD	(IY+DPB_MOTOFF),L
	LD	(IY+DPB_MOTOFF+1),H
;
	LD	HL,(_GNCLA)
	LD	(IY+DPB_GNCL),L
	LD	(IY+DPB_GNCL+1),H
;
	LD	HL,(_SNCLA)
	LD	(IY+DPB_SNCL),L
	LD	(IY+DPB_SNCL+1),H
	RET
;
AUTOD:	DB	"COMMANDE "
AUTODV:	DB	"A:"
AUTOP:	DB	0
AUTOP1:	DB	0
;
PE_:
INITE:
