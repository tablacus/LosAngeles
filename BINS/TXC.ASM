;
;	TXC
;
	$INCLUDE	LA.DEF
;
BUFFA	EQU	$34F8
DATA	EQU	$3500
;
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"text file converter v1.06"
	DB	" Lovers",$0D
	DB	0
	POP	DE
SW1:
	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,MAIN
SWI:
	INC	DE
	LD	A,(DE)
SWX:	CALL	#CAP
	INC	DE
	CP	"A"
	JR	NZ,SWA
	XOR	A
	LD	(SUREF),A
	JR	SWE
SWA:
	CP	"C"
	JR	NZ,SW2
	LD	(SW_C),A
	JR	SWE
SW2:
	CP	"L"
	JR	NZ,SW3
	LD	(SW_L),A
	JR	SWE
SW3:
	CP	"N"
	JR	NZ,SW4
	LD	(SW_N),A
	JR	SWE
SW4:
	CP	"Z"
	JR	NZ,SW5
	LD	(SW_Z),A
	JR	SWE
SW5:
	CP	"D"
	JR	NZ,SW6
	LD	(SW_D),A
	JR	SWE
SW6:
	CP	"T"
	JR	NZ,SW7
	LD	(SW_T),A
	JR	SWE
SW7:
	CP	"S"
	JR	NZ,SW8
	LD	(SW_C),A
	LD	(SW_N),A
	JR	SWE
SW8:
	CP	"M"
	JR	NZ,SW9
	LD	(SW_L),A
	LD	(SW_Z),A
	JR	SWE
SW9:
	CP	"K"
	JP	NZ,USAGE
	LD	(SW_K),A
	PUSH	DE
	LD	DE,MS_K
	CALL	MSGL
	POP	DE
SWE:
	LD	A,(DE)
	CP	$20
	JR	Z,SW1
	JR	SWX
;
MAIN:
	PUSH	DE
	LD	A,(SW_C)
	LD	DE,MS_C
	OR	A
	CALL	NZ,MSGL
	LD	A,(SW_L)
	LD	DE,MS_L
	OR	A
	CALL	NZ,MSGL
	LD	A,(SW_N)
	LD	DE,MS_N
	OR	A
	CALL	NZ,MSGL
	LD	A,(SW_Z)
	LD	DE,MS_Z
	OR	A
	CALL	NZ,MSGL
	LD	A,(SW_D)
	LD	DE,MS_D
	OR	A
	CALL	NZ,MSGL
	LD	A,(SW_T)
	LD	DE,MS_T
	OR	A
	CALL	NZ,MSGL
	POP	DE
;
	XOR	A
	LD	(DEBUFFA),DE
	LD	(D2BUFFA),DE
	CALL	#FILE
	JP	C,USAGE
	LD	IX,(#IBFAD)
	LD	A,(IX+1)
	CP	$20
	JP	Z,USAGE
	LD	A,(DE)
	INC	DE
	CP	$20
	JR	Z,COK
	CP	","
	JR	NZ,FLOAD
COK:
	LD	(D3BUFFA),DE
	CALL	#FILE
	JR	C,FLOAD
	LD	A,(IX+1)
	CP	$20
	JR	NZ,CCK2
	LD	A,(#DSK)
	LD	HL,DFILE
	LD	(HL),A
	JR	CCK3
CCK2:
	LD	HL,(D3BUFFA)
CCK3:
	LD	(D2BUFFA),HL
FLOAD:
	LD	DE,(DEBUFFA)
	CALL	#FILE
;				;File Load
	CALL	#ROPEN
	RET	C
FLOAD2:
	LD	HL,#FBPS
	LD	DE,FBUFF
	LD	BC,8
	LDIR
	LD	IX,(#IBFAD)
	LD	A,(IX+1+$10)
	CP	$A0
	JP	Z,FLOAD3
;
	CALL	#MPRNT
	DB	"Source",9,0
	CALL	FPRNT
	LD	HL,(#MEMAX)
	DEC	HL
	LD	(HL),$1A
	LD	DE,(#SIZE)
	SBC	HL,DE		;CF=0
	RET	C
	LD	A,H
	CP	$35
	RET	C
	LD	(#DTADR),HL
	CALL	#RDD
	RET	C
;
	LD	A,(SW_K)
	OR	A
	JR	Z,MAIN1
;
	LD	HL,(#DTADR)
KANJI1:
	LD	A,(HL)
	CP	$1A
	JR	Z,MAIN1
	OR	A
	JR	Z,MAIN1
	JP	P,KANJI2
	AND	$E0
	JP	PE,KANJI2
	LD	A,(HL)
	INC	HL
	CP	$81
	JR	NZ,KANJI2
	LD	A,(HL)
	CP	$40
	JR	NZ,KANJI2
	DEC	HL
	LD	(HL),$20
	INC	HL
	LD	(HL),$20
KANJI2:
	INC	HL
	JR	KANJI1
;
MAIN1:
	LD	HL,DATA
	LD	DE,BUFFA
	LD	BC,(#DTADR)
;
TXT1:	LD	A,(BC)		;READ TEXT
	INC	BC
;
	CP	$0D
	JR	NZ,TXT2
	CALL	TXCR
	JR	TXT1
TXT2:
	CP	$0A
	JR	NZ,TXT3
	CALL	TXLF
	JR	TXT1
TXT3:
	OR	A
	JR	Z,NULL
;
	CP	$1A
	JR	Z,EOF
;
	CALL	WRITE
	JR	TXT1
;
TXCR:
	CALL	POKEBF
	LD	A,$0D
	CALL	POKE
;	
	LD	A,(SW_L)
	OR	A
	RET	Z
;
	LD	A,$0A
	CALL	POKE
;
	LD	A,(BC)
	CP	$0A
	RET	NZ
	INC	BC
	RET
;
TXLF:
	CALL	POKEBF
	LD	A,(SW_C)
	OR	A
	RET	NZ
	LD	A,$0A
	CALL	POKE
	RET
;
NULL:
	CALL	POKEBF
	LD	A,(SW_Z)
	OR	A
	JR	Z,EOF0
	LD	A,$1A
	JR	EOF1
;
EOF:
	CALL	POKEBF
	LD	A,(SW_N)
	OR	A
	LD	A,$1A
	JR	Z,EOF1
EOF0:
	XOR	A
EOF1:
	CALL	POKE
;
	LD	DE,DATA
	AND	A
	SBC	HL,DE
;
	LD	(#SIZE),HL
;
	LD	HL,(#IBFAD)
	INC	HL
	LD	DE,FCB
	LD	BC,32
	LDIR
;
	LD	DE,(D2BUFFA)
	XOR	A
	CALL	#FILE
	RET	C
	LD	IX,(#IBFAD)
	LD	HL,FCB
	LD	B,8+3
SAVE:
	LD	A,(IX+1)
	CP	"?"
	JR	NZ,SAVE1
	LD	A,(HL)
	LD	(IX+1),A
SAVE1:
	INC	HL
	INC	IX
	DJNZ	SAVE
	XOR	A
	LD	(#WILD),A
;
	CALL	#MPRNT
	DB	"Save",9,0
	CALL	FPRNT
;
	CALL	SURE
	JR	NZ,QUIT
	CALL	#BRKEY
	JR	NZ,WOPEN
QUIT:
	CALL	#MPRNT
	DB	"Quit",$0D,0
	AND	A
	RET
WOPEN:
	LD	HL,DATA
	LD	(#DTADR),HL
	CALL	WOPENT
	RET	C
	CALL	#WRD
	RET	C
	CALL	#MPRNT
	DB	"Completed !",$0D,0
;
FLOAD3:
	LD	DE,(DEBUFFA)
	CALL	#FILE
;				;File Load
	LD	HL,FBUFF
	LD	DE,#FBPS
	LD	BC,8
	LDIR
	LD	A,(#WILD)
	OR	A
	RET	Z
	CALL	#NROPEN
	CCF
	RET	NC
	JP	FLOAD2
;
FPRNT:
	LD	A,(#DSK)
	CALL	#PRINT
	LD	A,":"
	CALL	#PRINT
	CALL	#FPRNT
	LD	A,9
	CALL	#PRINT
	LD	HL,(#SIZE)
	CALL	#PRTHL
	JP	#LTNL
;
POKEBF:
	LD	A,$0D
	CALL	WRITE
	LD	A,E
	OR	A
	RET	Z
	JP	WRITEY
;
WRITE:
	LD	(DE),A
	CP	9
	JR	NZ,WRITE0
	LD	A,(SW_D)
	OR	A
	JR	Z,WRITEW
;
	LD	A,$20
WRITED:
	LD	(DE),A
	INC	E
	JR	NZ,WRITED
	JR	WRITEY
WRITE0:
	INC	E
	RET	NZ
;
WRITEY:
	LD	A,E
	CP	BUFFA
	RET	Z
;
WRITEW:
	LD	A,(SW_T)
	OR	A
	JR	Z,WRITEZ
	LD	E,$FF
WRITE1:	LD	A,(DE)
	CP	$20
	JR	NZ,WRITEZ
	LD	A,9
	LD	(DE),A
	DEC	E
	BIT	3,E
	JR	NZ,WRITE1
;
WRITEZ:
	LD	E,BUFFA
WRITEX:
	LD	A,(DE)
	CP	$0D
	JR	Z,CLEAR
	CALL	POKE
	LD	A,(DE)
	CP	9
	JR	Z,CLEAR
	INC	E
	JR	NZ,WRITEX
CLEAR:
	LD	E,BUFFA
	RET
;
POKE:
	LD	(HL),A
;	CALL	#PAUSE
;	DW	#HOT
	INC	HL
	PUSH	HL
	DEC	HL
	AND	A
	SBC	HL,BC
	POP	HL
	RET	C
	CALL	#MPRNT
	DB	"Out of memory",$07,$0D,0
	JP	#HOT
;
USAGE:
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" TXC [½²¯Á] filename1[ filename2]",$0D,$0D
	DB	"½²¯Á:"
	DB	9,"/A "
	DB	"µ°ÄÓ°ÄÞ",$0D
	DB	9,"/C "
MS_C:	DB	"CR+LF to CR",$0D
	DB	9,"/L "
MS_L:	DB	"CR to CR+LF",$0D
	DB	9,"/N "
MS_N:	DB	"EOF to NULL",$0D
	DB	9,"/Z "
MS_Z:	DB	"NULL to EOF",$0D
	DB	9,"/D "
MS_D:	DB	"TAB to SPACE",$0D
	DB	9,"/T "
MS_T:	DB	"SPACE to TAB",$0D
	DB	9,"/S "
MS_S:	DB	"MS-DOS to S-OS (/C /N )",$0D
	DB	9,"/M "
MS_M:	DB	"S-OS to MS-DOS (/L /Z )",$0D
	DB	9,"/K "
MS_K:	DB	"KANJI SPACE to SPACEx2",$0D
	DB	0
	AND	A
	RET
;
MSGL:	CALL	#MSG
	JP	#LTNL
;
SURE:
	LD	A,(SUREF)
	OR	A
	RET	Z
	CALL	#MPRNT
	DB	"Sure? (Y/other)",0
	CALL	#FLGET
	CALL	#LTNL
	CP	"Y"
	RET	Z
	CP	"y"
	RET	Z
	CP	"Ý"
	RET
;
WOPENT:				;À²Ñ½ÀÝÌßÎ¿ÞÝ
	PUSH	IX
	LD	IX,(#IBFAD)
	LD	L,(IX+1+$16)
	LD	H,(IX+1+$17)
	PUSH	HL
	LD	L,(IX+1+$18)
	LD	H,(IX+1+$19)
	PUSH	HL
	CALL	#WOPEN
	POP	HL
	LD	(IX+1+$18),L
	LD	(IX+1+$19),H
	POP	HL
	LD	(IX+1+$16),L
	LD	(IX+1+$17),H
	POP	IX
	RET
;
SW_C:	DB	0
SW_L:	DB	0
SW_N:	DB	0
SW_Z:	DB	0
SW_D:	DB	0
SW_T:	DB	0
SW_K:	DB	0
;
SUREF:	DB	1
DEBUFFA:DW	0
D2BUFFA:DW	0
D3BUFFA:DW	0
FCB:	DS	32
FBUFF:	DS	8
DFILE:	DB	"A:*.*",0
