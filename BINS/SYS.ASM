;
;	Systemcopy FDD
;
IPL2D	EQU	$31F0
IPL2HD	EQU	$33F0
DATA	EQU	$3800
;
	$INCLUDE	LA.DEF
;
	OFFSET	$C000-$3000
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"X1 systemcopy(FDD) v1.07"
	DB	" Lovers",$0D,0
	POP	DE
	CALL	#SPSK
	LD	A,(DE)
	INC	DE
	LD	(DEVICE),A
	CALL	#DEVAD2
	LD	A,(IY+!FATLN)
	OR	A
	JP	Z,USAGE
	LD	A,(IY+!DEVNO)
	CP	$87
	JP	NZ,USAGE
;
	LD	A,(DE)
	CP	":"
	JP	NZ,USAGE
;
	LD	DE,FILELA
	CALL	COPY
;
	CALL	#MPRNT
	DB	"make IPL ",0
;
	LD	A,(DEVICE)
	CALL	#CHGDRV
	RET	C
	CALL	#RDFAT
	CALL	#MOTOFF
	RET	C
	LD	A,(IY+!FDMODE)
	INC	A
	JR	NZ,WT2HD
	CALL	#MPRNT
	DB	" 2D ",0
	LD	HL,IPL2D
	JR	WTIPL
WT2HD:
	LD	A,(IY+!MAXSEC)
	CP	8
	JP	NZ,USAGE
;
	CALL	#MPRNT
	DB	"2HD ",0
	LD	HL,IPL2HD
WTIPL:
	LD	A,(DEVICE)
	CALL	#CHGDRV
;
	LD	DE,0
	CALL	#DWT
	CALL	#MOTOFF
	RET	C
	CALL	#MPRNT
	DB	"Ok",$0D
	DB	"completed !",$0D,0
	AND	A
	RET
;
COPY:
	CALL	COPY2
	JR	C,ERR
	CALL	#MPRNT
	DB	"Ok",$0D,0
	RET
ERR:
	CALL	#MPRNT
	DB	"Error",$0D,0
	RET
;
COPY2:
	XOR	A
	CALL	#FILE
	RET	C
;
	CALL	#FPRNT
	CALL	#PRNTS
;
	CALL	#ROPEN
	RET	C
	LD	HL,(#DTADR)
	PUSH	HL
	LD      HL,DATA
	LD	(#DTADR),HL
	CALL	#RDD
	POP	HL
	LD	(#DTADR),HL
	RET	C
;
	LD	A,(DEVICE)
	CALL	#CHGDRV
	RET	C
;
A020:
	LD	IX,(#IBFAD)
	LD	B,8
A0201:
	LD	A,(IX+1)
	CP	$A0
	JR	NZ,A0202
	LD	(IX+1),$20
A0202:
	INC	IX
	DJNZ	A0201
;
	LD	IX,(#IBFAD)
;
	LD	L,(IX+1+$16)
	LD	H,(IX+1+$17)
	PUSH	HL
	LD	L,(IX+1+$18)
	LD	H,(IX+1+$19)
	PUSH	HL
	CALL	#WOPEN
	POP	HL
	LD	(IX+1+$18),L
	LD	(IX+1+$19),H
	POP	HL
	LD	(IX+1+$16),L
	LD	(IX+1+$17),H
	RET	C
;
	CALL	FSIZE
	LD	DE,2
WRDF1:
	CALL	SPACE
	JR	Z,WRDF3
WRDF2:
	INC	DE
	CALL	#ENDCL
	JR	C,WRDF1
FULL:
	CALL	#MPRNT
	DB	"Device Full ",0
	SCF
	RET
;
WRDF3:
	LD	(IX+1+$1A),E
	LD	(IX+1+$1B),D
WRDF4:
	CALL	SPACE
	JR	NZ,WRDF2
	DEC	B
	JR	Z,WRDF5
;
	INC	DE
	CALL	#ENDCL
	JR	C,WRDF4
	JR	FULL
WRDF5:
	CALL	FSIZE
	LD	E,(IX+1+$1A)
	LD	D,(IX+1+$1B)
;
WRDF6:
	LD	A,B
	DEC	A
	JR	NZ,WRDF7
	LD	HL,$FFFF
	JR	WRDF8
WRDF7:
	LD	H,D
	LD	L,E
	INC	HL
WRDF8:
	PUSH	BC
	PUSH	DE
	CALL	#SNCL
	POP	DE
	POP	BC
	INC	DE
	DJNZ	WRDF6
;
	CALL	#WTFAT
	RET	C
	CALL	#WTDIRX
	RET	C
;
	LD	HL,DATA
	LD	(#DTADR),HL
	LD	E,(IX+1+$1A)
	LD	D,(IX+1+$1B)
	LD	B,0
	JP	#WRDD
;
FSIZE:
	LD	HL,(#SIZE)
	DEC	HL
	SRL	H
	SRL	H
	INC	H
	LD	B,H
	RET
;
SPACE:
	PUSH	BC
	PUSH	DE
	CALL	#GNCL
	LD	A,D
	OR	E
	POP	DE
	POP	BC
	RET
;
USAGE:
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" SYS <drv>:",$0D
	DB	0
	AND	A
	RET
;
FILELA:	DB	"\LA.SYS",0
;
DEVICE:	DB	0
