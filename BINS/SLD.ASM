;
;	SLOAD	by Larn M.R.
;
	$INCLUDE	LA.DEF

BASE	EQU	$000B
@FATBF	EQU	$4000
@DIRBF	EQU	@FATBF+$1000
@DTBUF	EQU	@DIRBF+$1000

	ORG	$3000

	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JP	Z,USAGE
	CP	"-"
	JP	Z,USAGE
	CP	$21
	JP	C,USAGE

	CALL	#FILE
	RET	C
	LD	(DEBUF),DE
	LD	IX,(#IBFAD)
	LD	A,(IX+1)
	LD	DE,WILD
	CP	$20
	CALL	Z,#FILED

	LD	A,(#DSK)
	LD	(DSK1),A
	LD	HL,(#IBFAD)
	INC	HL
	LD	DE,SFILE
	LD	BC,16
	LDIR

	CALL	#RDFAT
	CALL	#MOTOFF
	JP	NC,DIRX
	CALL	#DEVADR
	RET	C

	LD	A,(IY+!DEVNO)
	AND	$7F
	CP	7
	JR	NZ,CHKDSK

	CALL	#VER
	LD	A,H
	CP	$A2
	JR	NZ,FDMODE
				;for LAE on LSX-Dodgers
	LD	HL,(BASE-1)
	LD	L,$89
	LD	(HL),$FF

	LD	A,(IY+!UNITNO)
	LD	L,$DC		;GETDPB
	CALL	$000F
	PUSH	IX
	POP	IY

FDMODE:
	BIT	0,(IY+!FDMODE)
	JR	Z,HIGH
	LD	B,16
	JR	LOW
HIGH:
	LD	B,26
LOW:
;	CHANGE DPB(for S-OS)

	LD	A,(IY+!MAXSEC)
	LD	(DPBX),A
	LD	(IY+!MAXSEC),B
;
;	CHECK DISK
;
CHKDSK:
	LD	HL,$0E00
	LD	DE,$0000	;FAT
	CALL	@RDFAT
	JR	NC,CHKDSK1
	JP	Z,ERROR
	LD	HL,$0C00
	LD	DE,$0001
	CALL	@RDFAT
	JP	C,ERROR
CHKDSK1:
					;READ DIR
	LD	DE,(@FATPS)
	INC	DE
	LD	(@DIRPS),DE
	LD	HL,@DIRBF
	CALL	@DRDSB		;FILE SEARCH
	JP	C,ERROR

	LD	DE,(DEBUF)
	CALL	#FILE
	JP	C,ERROR
	CALL	#RDFAT
	JP	C,ERROR
	LD	A,(#DSK)
	LD	(DSK2),A

	CALL	@SOPEN
	LD	A,(HL)
	AND	7
	JP	Z,NEXT
	BIT	7,(HL)
	JP	NZ,NEXT
DIR1:
	JP	C,OK
	PUSH	HL
	POP	IX
	LD	DE,(#IBFAD)

	LDI			;ATR
	LD	BC,8
	LDIR			;Ì§²ÙÈ°Ñ

	INC	HL
	INC	HL
	INC	HL
	INC	HL
	INC	HL

	LD	BC,3
	LDIR			;¶¸Á®³¼

	LD	L,(IX+20)
	LD	H,(IX+21)
	LD	(#DTADR),HL

	LD	L,(IX+22)
	LD	H,(IX+23)
	LD	(#EXADR),HL

	LD	E,(IX+30)
	LD	D,(IX+31)
	SLA	E
	SRL	D
	RR	E

	XOR	A
	OR	E
	JP	Z,NEXT
	LD	(CLPS),DE
	LD	HL,-1
SIZE1:
	INC	HL
	CALL	@GNCL
	XOR	A
	OR	D
	JR	Z,SIZE1

	LD	A,E
	AND	$0F

	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	OR	L
	LD	L,A
	LD	A,(IX+18)
	OR	A
	JR	NZ,SIZE2
	INC	HL
SIZE2:
	LD	D,0
	LD	E,H
	LD	(#SIZEH),DE

	LD	H,L
	LD	L,(IX+18)
	LD	(#SIZE),HL

	CALL	#FPRNT

	LD	A,(DSK2)
	CALL	#CHGDRV
	CALL	#WOPENH
	JP	C,ERROR

	LD	HL,@DTBUF
	LD	(#DTADR),HL

COPY:
	LD	DE,(CLPS)
	LD	HL,@DTBUF
	CALL	@DRDSB
	JP	C,ERROR

	LD	A,(DSK2)
	CALL	#CHGDRV

	LD	DE,(CLPS)
	CALL	@GNCL
	LD	(CLPS),DE
	LD	A,D
	OR	A
	JR	NZ,LAST

	LD	B,4
	CALL	#WRDX
	JP	C,ERROR
	JR	COPY
LAST:
	LD	A,(CLPS)
	AND	$0F
	SRL	A
	SRL	A
	INC	A
	LD	B,A
	CALL	#WRDX
	JP	C,ERROR

	CALL	#WTFAT
	JP	C,ERROR
	CALL	#WTDIRX
	JP	C,ERROR

	CALL	#MPRNT
	DB	" Ok",$0D,0
	CALL	#PAUSE
	DW	OK
NEXT:
	CALL	@NOPEN
	JP	DIR1
ERROR:
	CALL	#MPRNT
	DB	7," Error!",$0D,0
OK:
	XOR	A

	LD	A,(DPBX)
	OR	A
	JR	Z,OK2
	LD	(IY+!MAXSEC),A
OK2:
	CALL	#MOTOFF
	JP	#NL

DIRX:
	CALL	#DIRX
	JP	#HOT

@RDFAT:
	LD	(@FATPS),DE
	LD	BC,@FATBF
	ADD	HL,BC
	LD	(@FATAD),HL
	LD	HL,@FATBF
	CALL	@DRDSB
	JR	C,RDFAT1
	LD	HL,(@FATAD)
	LD	E,(HL)
	INC	L
	LD	D,(HL)

	LD	HL,$8F01
	AND	A
	SBC	HL,DE
	RET	Z
	SCF
	RET
RDFAT1:
	XOR	A
	SCF
	RET

@DRDSB:
	LD	A,(DSK1)
	CALL	#CHGDRV
	LD	A,(IY+!DEVNO)
	AND	$7F
	CP	7
	JR	NZ,DRDRAM

	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	EX	DE,HL
	LD	B,$10
DRDSB1:
	PUSH	BC
	PUSH	HL
	CALL	#DRD
	POP	HL
	INC	H
	POP	BC
	JR	C,DRDSB2
	DJNZ	DRDSB1
DRDSB2:
	POP	DE
	RET

DRDRAM:
	PUSH	DE
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	EX	DE,HL
	LD	B,4
DRDSB3:
	PUSH	BC
	CALL	#DRDC
	POP	BC
	JR	C,DRDSB4
	DJNZ	DRDSB3
DRDSB4:
	POP	DE
	RET

@DSKF:
	LD	A,(@FATPS)
	RRCA
	CPL
	AND	$80
	LD	B,A

	LD	HL,0
	LD	DE,0
DSKF1:
	PUSH	DE
	CALL	@GNCL
	LD	A,D
	OR	E
	JR	NZ,DSKF2
	INC	HL
DSKF2:
	POP	DE
	INC	E
	DJNZ	DSKF1
	RET

@GNCL:
	XOR	A
	OR	D
	RET	NZ
	PUSH	HL
	SLA	E
	RL	D
	SRL	E
	LD	A,(@FATAD+1)
	ADD	A,D
	LD	D,A
	EX	DE,HL
	LD	A,(HL)
	ADD	A,A
	JR	C,GNCL1
	SET	7,L
	LD	D,(HL)
	SRL	D
	RRA
	LD	E,A
	POP	HL
	RET
GNCL1:
	LD	D,$FF
	AND	A
	RRA
	LD	E,A
	POP	HL
	RET

@SOPEN:
	LD	HL,FILESE
SOPEN0:
	LD	(OPENPAT+1),HL

SOPEN1:
	LD	C,128
	LD	HL,@DIRBF
SOPEN2:
OPENPAT:CALL	FILESE		;¶·¶´
	CCF
	RET	C
SOPENE:
	LD	(FBAD),HL
	LD	A,C
	LD	(FBCNT),A
	RET			;Ok	CF=0

@NOPEN:
	LD	HL,FILESE
	LD	(OPENPAT+1),HL

	LD	A,(FBCNT)
	DEC	A
	SCF
	RET	Z
NOPEN2:
	LD	HL,(FBAD)
	LD	BC,$20
	ADD	HL,BC
	LD	C,A
	JR	SOPEN2

FILESE:
	LD	A,(HL)
	CP	$FF
	RET	Z		;END:ZF=1 CF=0
	OR	A
	JR	Z,FILESE1
	LD	DE,SFILE
	LD	B,8
	PUSH	HL
	INC	HL
	CALL	CPSTR
	JR	NZ,FILESE0
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	LD	B,3
	CALL	CPSTR
FILESE0:
	POP	HL
	SCF
	RET	Z		;!!!:(ZF=1) CF=1
FILESE1:
	LD	DE,$20
	ADD	HL,DE
	DEC	C
	JR	NZ,FILESE
ZFCF0:
	OR	$FF		;ZF=0 CF=0
	RET

NEXTSE:
	LD	A,(HL)
	OR	A
	SCF
	RET	Z		;!!!:ZF=1 CF=1
	CP	$FF
	SCF
	RET	Z		;!!!:(ZF=1) CF=1
	LD	DE,$20
	ADD	HL,DE
	DEC	C
	JR	NZ,NEXTSE
	JR	ZFCF0

CPSTR:
	LD	A,(DE)
	CP	"?"		;Ü²ÙÄÞ¶°ÄÞ
	JR	Z,CPSTR2
	LD	A,(HL)
	CALL	CAP2
	PUSH	HL
	LD	H,A
	LD	A,(DE)
	CALL	CAP2
	CP	H		;CP (HL),(DE)
	POP	HL
	RET	NZ
CPSTR2:
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET

CAP2:
	CALL	#CAP
CAP3:
	CP	$21
	RET	NC
	LD	A,$A0
	RET

USAGE:
	CALL	#MPRNT
	DB	"S-OS file loader v1.00 Lovers",$0D
	DB	"usage:",$0D
	DB	" SLD S-OSÌ§²ÙÈ°Ñ Ì§²ÙÈ°Ñ",$0D
	DB	$0D,0
	XOR	A
	RET

WILD:
	DB	"*.*",0

DSK1:	DB	0
DSK2:	DB	0
DEBUF:	DW	0
DPBX:	DB	0
CLPS:	DW	0
FBAD:	DW	0
FBCNT:	DB	0
@FATPS:	DW	0
@DIRPS:	DW	0
@FATAD:	DW	$0E00
SFILE:	DS	16
