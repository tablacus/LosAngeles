;
;	RSTEX by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	INC	DE
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
MAIN:
	CALL	PRO
	RET	C
;
	CALL	TITLE
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	LD	BC,LAST-START
	AND	A
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	LD	HL,($0039)
	LD	(_RST),HL
;
	LD	HL,RST-START
	ADD	HL,DE
	LD	($0039),HL
;
	LD	HL,HLPAT+1-START
	ADD	HL,DE
	LD	(_HLPAT+1),HL
;
	LD	HL,START
	LDIR
	XOR	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Å²Ü!",7,$0D,0
	AND	A
	RET
;
REM2:
	CALL	TITLE
	CALL	#MPRNT
	DB	'release',$0D,'MEMAX:',0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	($0039),DE
	XOR	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
TITLE:
	CALL	#MPRNT
	DB	$1A,"rstex v1.00 Lovers"
	DB	$0D,0
	RET
;
USAGE:
	CALL	TITLE
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" RSTEX     ¼Þ®³Á­³",$0D
	DB	" RSTEX /R   ¶²¼Þ®",$0D
	DB	0
	XOR	A
	RET
;
;		 012345  6 7
START:	DB	"[RSTEX",0,0
;
_RST:	DW	#HOT
;
RST:
_HLPAT:	LD	(0),HL
	LD	HL,0
	PUSH	AF
	ADD	HL,SP
	POP	AF
	PUSH	HL
	PUSH	IY
	PUSH	IX
	EXX
	EX	AF,AF'
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	EXX
	EX 	AF,AF'
HLPAT:	LD	HL,0
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
;
	CALL	#MPRNT
	DB	$0D
	DB	" AF   BC   DE   HL",$0D,$0D
	DB	" AF'  BC'  DE'  HL'",$0D,$0D
	DB	" IX   IY   SP   PC",$0D
	DB	$1E,$1E,$1E,$1E,0
;
	LD	C,3
LOOP1:
	LD	B,4
LOOP2:
	POP	HL
	CALL	#PRTHL
	CALL	#PRNTS
	DJNZ	LOOP2
;
	CALL	#LTNL
	CALL	#LTNL
	DEC	C
	JR	NZ,LOOP1
	JP	#HOT
;
LAST:
