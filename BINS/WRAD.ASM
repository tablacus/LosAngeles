;
;	work ram disk driver  by Larn M.R.
;
	$INCLUDE	LA.DEF
;
DEID	EQU	$0C
;
	ORG	$3000
;
;	INIT
;
	XOR	A
	LD	(FORMATS),A
	LD	(AUTOS),A
	LD	A,32
	LD	(SIZE),A
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"work ram disk driver v1.00",$0D
	DB	" (C) 1995 Lovers",$0D,0
	POP	DE
GRAD1:	CALL	#SPSK
	LD	A,(DE)
	INC	DE
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,GRAD2
SWI:
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	'F'
	JR	NZ,GRADS1
	LD	(FORMATS),A
GRADS1:	CP	'A'
	JR	NZ,GRADS2
	LD	(AUTOS),A
	JR	GRAD1
GRADS2:	CP	'R'
	JP	Z,REMOVE
	CP	"M"
	JR	NZ,GRAD1
	CALL	#2HEX
	JR	C,GRADM
	LD	B,A
	AND	$0F
	LD	C,A
	LD	A,B
	AND	$F0
	RRCA
	LD	B,A
	RRCA
	RRCA
	ADD	A,B
	ADD	A,C
	CP	3
	JR	C,GRADM
	CP	64
	JR	NC,GRADM
	LD	(SIZE),A
	JR	GRAD1
;
GRAD2:	LD	A,(DE)
	CP	':'
	JR	Z,GRAD3
GRADM:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" WRAD [½²¯Á] drv:",$0D,$0D
	DB	"½²¯Á:"
	DB	9,"/A  µ°ÄÓ°ÄÞ",$0D
	DB	9,"/F  Ì«°Ï¯Ä",$0D
	DB	9,"/Mn »²½Þ(n:3-63)",$0D
	DB	9,"/R  ¶²¼Þ®",$0D,0
	AND	A
	RET
;
GRAD3:	LD	A,(#MAXDRV)
	LD	B,A
	DEC	DE
	LD	A,(DE)
	CP	'0'
	JR	C,GRADM
	LD	(DRIVE),A
;
GRAD4:	LD	A,B
	CALL	#DEVAD2
	JR	C,GRAD42
	LD	A,(IY+$12)
	CP	DEID
	JR	NZ,GRAD42
	LD	A,(IY+$13)
	CP	0
	JP	Z,GRADZ
GRAD42:	DJNZ	GRAD4
;
	CALL	GRADP
	RET	NC
;
	LD	A,(SIZE)
	ADD	A,A
	ADD	A,A
	LD	D,A
	LD	E,0
	LD	HL,(#WKSIZ)
	LD	(WKSIZ),HL
	AND	A
	SBC	HL,DE
	JR	NC,WKOK
	CALL	#MPRNT
	DB	"Ä¸¼­Ü°¸ ¶Þ ÀØÅ²Ü!",7,$0D,0
	AND	A
	RET
WKOK:
	LD	(#WKSIZ),HL
	LD	A,H
	LD	(PATCH1+1),A
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	AND	A
	LD	BC,LAST-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	PUSH	HL
;
	CALL	PRTMEM
;
	LD	A,(SIZE)
	SUB	2
	LD	H,0
	LD	L,A
	CALL	#PRTHLX
	CALL	#MPRNT
	DB	" K Bytes",$0D,0
;
;	SOFT RELOCATE
;
	POP	DE
	LD	HL,ADR-START
	ADD	HL,DE
	LD	(DRDCX+1),HL
	LD	(DWTCX+1),HL
;
	LD	HL,START
	PUSH	DE
	LDIR
	POP	DE
;
;	WRITE DEVICE TABLE
;
	LD	(IY+$00),1	;FATLN
	LD	(IY+$01),$F8	;FATID
	LD	HL,DRDC-START
	ADD	HL,DE
	LD	(IY+$02),L	;DRDC
	LD	(IY+$03),H
	LD	HL,DWTC-START
	ADD	HL,DE
	LD	(IY+$04),L	;DWTC
	LD	(IY+$05),H
	LD	(IY+$06),0	;ADDCL
	LD	(IY+$07),0
	LD	A,(SIZE)
	LD	(IY+$08),A	;CLEND
	LD	(IY+$09),0
	LD	(IY+$0A),0	;FDMODE
	LD	(IY+$0B),2	;MAXDIR
	LD	(IY+$0C),0	;MAXCYL
	LD	(IY+$0D),1	;MAXSEC
	LD	(IY+$0E),0	;FATPS
	LD	(IY+$0F),4	;INCSEC(1024=4)
	LD	(IY+$10),1	;DIRPS
	LD	(IY+$11),1	;ADDSEC
	LD	(IY+$12),DEID	;DEVNO
	LD	(IY+$13),0	;UNITNO
	LD	HL,MOTOFF-START
	ADD	HL,DE
	LD	(IY+$14),L	;MOTOFF
	LD	(IY+$15),H
	LD	HL,(#GNCLA)
	LD	(IY+$16),L	;GNCL
	LD	(IY+$17),H
	LD	HL,(#SNCLA)
	LD	(IY+$18),L	;SNCL
	LD	(IY+$19),H
	LD	(IY+$1A),0	;SDIR
	LD	(IY+$1B),0
	LD	(IY+$1C),'W'
	LD	(IY+$1D),'O'
	LD	(IY+$1E),'R'
	LD	(IY+$1F),'K'
;
;	Ì«°Ï¯Ä
;
GRADF:	LD	A,(FORMATS)
	OR	A
	RET	Z
;
	CALL	#MPRNT
	DB	"format",$0D,0
;
	LD	A,(DRIVE)
	CALL	#CHGDRV
	RET	C
	CALL	#DEVADR
	LD	HL,(#DTBUF)
	LD	E,L
	LD	D,H
	INC	DE
	XOR	A
	LD	(HL),A
	LD	BC,$03FF
	LDIR
;
	LD	HL,(#DTBUF)
	LD	A,(IY+$01)
	LD	(HL),A	;FATID
	INC	HL
	DEC	(HL)	;LD (HL),$FF
	INC	HL
	DEC	(HL)
	LD	E,(IY+$0E)
	LD	D,0
	LD	B,(IY)		;FATLN
FORMAT0:PUSH	BC
	LD	HL,(#DTBUF)
	CALL	#DWTC
	POP	BC
	RET	C
	LD	HL,(#DTBUF)
	XOR	A
	LD	(HL),A
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	DJNZ	FORMAT0
;
	LD	E,(IY+$10)
	LD	D,0
FORMAT2:LD	HL,(#DTBUF)
	CALL	#DWTC
	RET	C
	LD	A,E
	CP	(IY+$0B)
	JR	C,FORMAT2
	RET
;
GRADZ:	PUSH	IY
	POP	HL
	CALL	GRADP
	RET	NC
	PUSH	IY
	POP	DE
	LD	BC,$0020
	LDIR
	JP	GRADF
;
GRADP:	LD	A,(DE)
	CALL	#DEVAD2
	RET	C
	LD	A,(IY+$12)
	CP	DEID
	SCF
	RET	Z
	LD	A,(AUTOS)
	OR	A
	SCF
	RET	NZ
;
	CALL	#MPRNT
	DB	'Now Drive "',0
	LD	A,(DE)
	CALL	#PRINT
	CALL	#MPRNT
	DB	":",$22," is ",$22,0
	LD	A,(IY+$1C)
	CALL	#PRINT
	LD	A,(IY+$1D)
	CALL	#PRINT
	LD	A,(IY+$1E)
	CALL	#PRINT
	LD	A,(IY+$1F)
	CALL	#PRINT
	CALL	#MPRNT
	DB	'"',$0D
	DB	'Do you want to change to "WRAD"? (Y/other):',0
	CALL	#FLGET
	CALL	#CAP
	CP	'Y'
	JR	Z,SURE1
	CP	'Ý'
	JR	Z,SURE1
;
	CALL	#MPRNT
	DB	"No",$0D,0
	AND	A
	RET
;
SURE1:	CALL	#MPRNT
	DB	"Yes",$0D,0
	SCF
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	INC	H
	JR	Z,REME
	DEC	H
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM1
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Å²Ü!",7,$0D,0
	AND	A
	RET
REM1:	CALL	#MPRNT
	DB	"release",$0D,0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
;
	EX	DE,HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	(#WKSIZ),DE
;
	CALL	PRTMEM
;
	LD	A,(#MAXDRV)
	LD	B,A
REM2:	LD	A,B
	CALL	#DEVAD2
	JR	C,REM3
	LD	A,(IY+$12)
	CP	DEID
	JR	NZ,REM3
	LD	(IY),0
REM3:	DJNZ	REM2
	CALL	#RDVSW
	CALL	#DEVAD2
	RET	NC
	LD	A,(#MAXDRV)
	LD	B,A
	LD	C,1
REM4:	LD	A,C
	CALL	#DEVAD2
	JR	NC,REM5
	INC	C
	DJNZ	REM4
	SCF
	RET
;
REM5:	LD	A,C
	ADD	A,$40
	JP	#SDVSW
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
PRTMEM:
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	CALL	#PRTHL
	CALL	#MPRNT
	DB	$0D
	DB	"WKSIZ:",0
	LD	HL,(#WKSIZ)
	CALL	#PRTHL
	JP	#LTNL
;
START:	DB	"[WRAD ",0,0
;
WKSIZ:	DW	0
;
MOTOFF:	RET
;
DRDC:
	PUSH	DE
	PUSH	HL
DRDCX:	CALL	ADR
	CALL	#PEEK@
DWTC1:
	POP	HL
	POP	DE
	INC	H
	INC	H
	INC	H
	INC	H
	INC	DE
	RET
DWTC:
	PUSH	DE
	PUSH	HL
DWTCX:	CALL	ADR
	CALL	#POKE@
	JR	DWTC1
ADR:
	LD	A,E
	ADD	A,A
	ADD	A,A
PATCH1:	ADD	A,$40
	LD	D,A
	LD	BC,$400
	LD	E,C
	RET
;
LAST
;
FORMATS:DB	0
AUTOS:	DB	0
DRIVE:	DB	0
;
SIZE:	DB	32
