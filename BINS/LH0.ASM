;
;	LH0	by Larn M.R.
;
	ORG	$C000
;
	$INCLUDE	LA.DEF
;
_FILESW	EQU	$3000
	$INCLUDE	FILES.ASM
;
FGBUF	EQU	$6000
DATA	EQU	$8000
DTEND	EQU	$A0
LENGTH	EQU	8
;
;
SW1:
	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,SWEND
SWI:
	INC	DE
SWX:
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	"L"
	JR	NZ,SW2
	LD	(SW_L),A
;	JR	SWE
SW2:
SWE:
	LD	A,(DE)
	CP	$21
	JR	C,SW1
	JR	SWX
;
SWEND:
	LD	A,(DE)
	CP	$21
	JP	C,USAGE
;
	CALL	_FILESI
	XOR	A
	CALL	#FILE
	JP	C,END
	LD	IY,(#IBFAD)
	LD	A,(IY+1+8)
	CP	$20
	JR	NZ,MAIN1
	LD	(IY+1+8),"L"
	LD	(IY+1+9),"Z"
	LD	(IY+1+10),"H"
MAIN1:
	PUSH	DE
	CALL	FSET
	LDIR
	CALL	ROPEN
	POP	DE
	JP	C,END
;
	LD	A,1
	CALL	_CHGFILE
	XOR	A
	CALL	#FILE
	LD	IY,(#IBFAD)
	LD	A,(IY+1)
	CP	$20
	LD	DE,DIRW
	CALL	Z,#FILED
	CALL	FSET
	LD	DE,WILD2
	LDIR
MAIN:
	XOR	A
	CALL	_CHGFILE
	CALL	#MPRNT
	DB	$1A,"archive: ",0
	LD	A,(#DSK)
	CALL	#PRINT
	LD	A,":"
	CALL	#PRINT
	CALL	#FPRNT
	CALL	#MPRNT
	DB	$0D
	DB	"mothod  packed  filesize    date     time     filename",$0D
REM:	DB	"====== ======== ========  ======== ======== ============",$0D
	DB	0
HEADER:
	CALL	FGETCN
	CP	"-"
	JR	NZ,HEADER
	CALL	FGETCN
	CP	"l"
	JR	NZ,HEADER
	CALL	FGETCN
	CP	"h"
	JR	NZ,HEADER
	CALL	FGETCN
	LD	(METHOD),A
	CALL	FGETCN
	CP	"-"
	JR	NZ,HEADER
;
	LD	HL,PACKL
	LD	B,12	;PACKED SIZE TIME DATE
	CALL	FGETCT
;
	CALL	FGETC0
	LD	(ATTR),A
	CALL	FGETC0
	LD	(HLEVEL),A
;
	CALL	FGETC0
	LD	(FNAMEX),A
	LD	HL,FNAME
	LD	B,A
	CALL	FGETCT
	LD	(HL),0
;
	LD	HL,CRC
	LD	B,2
	CALL	FGETCT
;
	LD	A,(HLEVEL)
	OR	A
	JR	Z,DISPLAY
;
	CALL	FGETC0	;?
;
	CALL	FGETC0
	PUSH	AF
	CALL	FGETC0
	POP	AF
	OR	A
	JR	Z,DISPLAY
;
	LD	HL,DIR
HEAD2:
	PUSH	HL
	CALL	FGETC0
	POP	HL
	CP	$20
	JR	C,HEAD4
	CP	$FF
	JR	NZ,HEAD3
	LD	A,"\"
HEAD3:
	LD	(HL),A
	INC	HL
	JR	HEAD2
HEAD4:
	XOR	A
	LD	(HL),A
	LD	B,6
HEAD5:
	PUSH	BC
	CALL	FGETC0
	POP	BC
	DJNZ	HEAD5
;
DISPLAY:
;
;	method
;
	CALL	#MPRNT
	DB	"-lh",0
	LD	A,(METHOD)
	CALL	#PRINT
	CALL	#MPRNT
	DB	"-  ",0
;
;	packed
;
	LD	HL,(PACKH)
	CALL	#PRTHL
	LD	HL,(PACKL)
	CALL	#PRTHL
	CALL	#PRNTS
;
;	filesize
;
	LD	HL,(SIZEH)
	CALL	#PRTHL
	LD	HL,(SIZEL)
	CALL	#PRTHL
;
;	date & time
;
	LD	IY,TIME-22
	CALL	#PRTTMS
	CALL	#PRNTS
	CALL	#CSR
	LD	A,L
	CP	25
	JR	NZ,DIS2
	LD	B,19
DIS1:
	CALL	#PRNTS
	DJNZ	DIS1
DIS2:
;
;	filename
;
	LD	DE,FNAME
	CALL	#MSX
	CALL	#PRNTS
;
	LD	A,(FNAMEX)
	SUB	12
	JR	NC,DIS4
DIS3:
	CALL	#PRNTS
	INC	A
	JR	NZ,DIS3
DIS4:
	LD	A,(SW_L)
	OR	A
	JR	NZ,SKIP
;
	LD	A,(METHOD)
	CP	"0"
	JR	Z,LH0
;
SKIP0:
	CALL	#MPRNT
	DB	"Skip",0
;
SKIP:
	LD	HL,(PACKL)
	LD	DE,$FFFF
	ADD	HL,DE
	LD	(PACKL),HL
	JR	C,SKIP1
	LD	HL,(PACKH)
	LD	A,H
	OR	L
	JR	Z,NEXT
	DEC	HL
	LD	(PACKH),HL
SKIP1:
PATCH:	CALL	FGETC0
	JR	SKIP
NEXT:
	LD	A,(WRITE)
	OR	A
	JR	Z,NEXT1
	CALL	LIST
	CALL	WCLOSE
	LD	DE,OK
	JR	NC,MSX
	LD	DE,ERROR
MSX:
	CALL	#MSX
NEXT1:
	CALL	#LTNL
	CALL	#BRKEY
	JP	NZ,HEADER
;
	CALL	#MPRNT
	DB	"Quit",$0D,0
	JP	END2
;
WERR:
	LD	DE,ERROR
	CALL	#MSX
	JP	SKIP
;
LH0:
	LD	A,1
	CALL	_CHGFILE
	LD	DE,FNAME
	CALL	#FILED
	CALL	CHWILD
	LD	HL,WILD2
	LD	C,1
	LD	DE,(#FSEA)
	CALL	CALLDE
	CALL	CHWILD
	JR	NC,SKIP0
	CALL	#MPRNT
	DB	"Extract ",0
	LD	A,1
	LD	(WRITE),A
	CALL	WOPEN
	JP	C,WERR
	LD	HL,EXLH0
	LD	(PATCH+1),HL
	JP	SKIP
;
EXLH0:
	CALL	FGETC0
	CALL	FPUTC
	RET	NC
WERR2:
	LD	DE,ERROR
	CALL	#MSX
LIST:
	XOR	A
	LD	(WRITE),A
	LD	HL,FGETC0
	LD	(PATCH+1),HL
	RET
;
CHWILD:
	LD	HL,(#IBFAD)
	LD	DE,WILD2
	LD	B,22
CW1:
	INC	HL
	LD	C,(HL)
	LD	A,(DE)
	LD	(HL),A
	LD	A,C
	LD	(DE),A
	INC	DE
	DJNZ	CW1
	RET
;
FGETCN:
	CALL	FGETC
	RET	NC
	POP	AF
	XOR	A
	CALL	_CHGFILE
	CALL	FSET
	EX	DE,HL
	LDIR
	LD	A,$C9		;RET
	LD	(FGPAT),A
	CALL	#NL
	LD	DE,REM
	CALL	#MSX
	CALL	_NROPEN
	EXX
	LD	HL,$8000
	EXX
	JP	NC,MAIN
	JR	END2
;
END:
	CALL	#NL
	LD	DE,REM
	CALL	#MSX
END2:
	CALL	_FILESR
	XOR	A
	JP	#NL
;
FSET:
	LD	HL,(#IBFAD)
	INC	HL
	LD	DE,WILD
	LD	BC,22
	RET
;
CALLDE:
	PUSH	DE
	RET
;
ROPEN:
	XOR	A
	CALL	_CHGFILE
	CALL	#ROPEN
	RET	C
	LD	HL,$8000
	EXX
	RET
;
FGETCT:
	PUSH	BC
	CALL	FGETCX
	POP	BC
	DJNZ	FGETCT
	RET
;
FGETCX:
	PUSH	HL
	CALL	FGETC
	POP	HL
	JR	C,FGETCXE
	LD	(HL),A
	INC	HL
	RET
;
FGETC0:
	CALL	FGETC
	RET	NC
FGETCXE:
	CALL	_FILESR
	CALL	#NL
	JP	#HOT
;
FGETC:
	EXX
	BIT	7,H
	JR	NZ,FGETB
FGETC1:
	LD	A,(HL)
	INC	HL
	EXX
	AND	A
FGPAT:	RET		;/EXX
;
FAPAT:	LD	DE,0
	EX	DE,HL
	ADD	HL,DE
	EX	DE,HL
	EXX
	RET	NC
	SBC	A,A
	DEC	HL
	RET
;
FGETB:
	EXX
	XOR	A
	CALL	_CHGFILE
	LD	DE,(#CLPS)
	CALL	#ENDCL
	JR	NC,FGETE2
	LD	HL,FGBUF
	LD	(#DTADR),HL
	LD	B,8
	CALL	#RDDX
	JR	C,FGETE1
	LD	A,B
	OR	A
	JR	Z,FGETB2
;
	LD	HL,(#SIZE)
	DEC	HL
	LD	A,H
	AND	$1F
	LD	H,A
	LD	DE,FGBUF
	ADD	HL,DE
	EX	DE,HL
	LD	HL,-2
	SBC	HL,DE
	LD	(FAPAT+1),HL
	LD	A,$D9		;EXX
	LD	(FGPAT),A
FGETB2:
	EXX
	LD	HL,FGBUF
	JR	FGETC1
;
FGETE1:
	XOR	A	;ERROR
	SCF
	RET
;
FGETE2:
	EXX
	LD	HL,$8000
	EXX
	SCF		;EOF
	SBC	A,A
	RET
;
WOPEN:
	LD	HL,(SIZEL)
	LD	(#SIZE),HL
	LD	HL,(SIZEH)
	LD	(#SIZEH),HL
;
	CALL	#WOPENH
	RET	C
;
	LD	HL,(#IBFAD)
	LD	DE,1+$0B	;ATTR
	ADD	HL,DE
	LD	A,(ATTR)
	LD	(HL),A
;
	LD	DE,$16-$0B	;TIME
	ADD	HL,DE
	EX	DE,HL
	LD	HL,TIME
	LD	BC,4
	LDIR
	EXX
	LD	BC,DATA
	EXX
	XOR	A
	RET
;
FPUTC:
	EXX
	LD	(BC),A
	INC	BC
	LD	A,B
	CP	DTEND
	EXX
	JR	NC,FPUTC2
	AND	A
	RET
FPUTC2:
	LD	A,1
	CALL	_CHGFILE
	EXX
	LD	BC,DATA
	LD	(#DTADR),BC
	EXX
	LD	B,LENGTH
	JP	#WRDX
;
WCLOSE:
	LD	A,1
	CALL	_CHGFILE
	LD	HL,(#IBFAD)
	LD	DE,1+$1C
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	OR	(HL)
	INC	HL
	OR	(HL)
	INC	HL
	OR	(HL)
	RET	Z
;
	EXX
	PUSH	BC
	EXX
	POP	HL
	LD	DE,-DATA-1
	ADD	HL,DE
	LD	B,H
	SRA	B
	SRA	B
	INC	B
	JR	Z,WCLOSE2
;
	LD	HL,DATA
	LD	(#DTADR),HL
	CALL	#WRDX
WCLOSE2:
	CALL	NC,#WTFAT
	CALL	NC,#WTDIRX
	JP	#MOTOFF
;
USAGE:
	CALL	#MPRNT
	DB	$1A,"lh0 v1.01"
	DB	" Lovers",$0D
	DB	"usage:",$0D
	DB	" LH0 [½²¯Á] lzhÌ§²Ù[ extÌ§²Ù]",$0D
	DB	$0D
	DB	9,"/L list",$0D
	DB	0
	RET
;
DIRW:	DB	"*.*",0
OK:	DB	"Ok",0
ERROR:	DB	"Error",0
;
SW_L:	DB	0
WRITE:	DB	0
;
METHOD:	DB	0
PACKL:	DW	0
PACKH:	DW	0
SIZEL:	DW	0
SIZEH:	DW	0
TIME:	DW	0
DATE:	DW	0
ATTR:	DB	0
HLEVEL:	DB	0
;
FNAMEX:	DB	0
CRC:	DW	0
FNAME:				;DS	32
DIR	EQU	FNAME+32	;DS	64
;
WILD:	EQU	DIR+64		;DS	22
WILD2:	EQU	WILD+22		;DS	22
