;
;	PATH by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
MAIN:
	LD	(DEBUF+1),DE
	PUSH	DE
	CALL	PRO
	POP	DE
	LD	A,(DE)
	JR	NC,TSR
;
	CP	$20
	JP	NC,SET
;
	LD	DE,10
	ADD	HL,DE
	CALL	#MPRNT
	DB	$1A,"PATH=",0
PRINT1:
	LD	A,(HL)
	INC	HL
	CP	1
	JR	NZ,PRINT2
	LD	A,";"
PRINT2:
	CP	$20
	CCF
	JP	NC,#LTNL
	CALL	#PRINT
	JR	PRINT1
TSR:
	CP	$20
	JP	C,USAGE
;
	CALL	TITLE
	LD	HL,(#FILEX+1)
	LD	DE,$3C
	ADD	HL,DE
	LD	A,(HL)
	LD	(\FILE+1),HL
	CP	$D5
	JP	NZ,ERROR
;
	LD	HL,(#CEXE+1)
	LD	DE,$2F
	ADD	HL,DE
	LD	A,(HL)
	LD	(\CEX3+1),HL
	CP	$21
	JP	NZ,ERROR
;
	LD	DE,$55-$2F
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	(\EXPAT+2),HL
	CP	$11
	JP	NZ,ERROR
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	LD	BC,LAST-START
	AND	A
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	LD	HL,(#CEXE+1)
	LD	(_CEXE+1),HL
;
	LD	HL,CEXE-START
	ADD	HL,DE
	LD	(#CEXE+1),HL
;
	LD	HL,PATH-START
	ADD	HL,DE
	LD	(\PATH+1),HL
;
	LD	HL,DEPAT+1-START
	ADD	HL,DE
	LD	(\DEPAT+2),HL
	LD	(\DEPAT2+2),HL
;
	LD	HL,(#IBFAD)
	INC	HL
	LD	(\FNAME+1),HL
;
	LD	HL,START
	LDIR
SET:
	CALL	PRO
	LD	DE,10
	ADD	HL,DE
	LD	B,64
DEBUF:	LD	DE,0
SET1:
	LD	A,(DE)
	INC	DE
	CP	"%"
	JR	NZ,SETX1
	LD	A,(DE)
	DEC	DE
	CP	":"
	LD	A,"%"
	JR	NZ,SET2
	CALL	#RDVSW
	INC	DE
SETX1:
	CP	";"
	JR	NZ,SET2
	LD	A,1
	JR	SET3
SET2:
	CP	$20
	JR	C,SETE
SET3:
	LD	(HL),A
	INC	HL
	DJNZ	SET1
SETE:
	DEC	HL
	LD	A,(HL)
	CP	1
	JR	NZ,SETE1
	DEC	(HL)
SETE1:
	XOR	A
	INC	B
SETE2:
	DEC	B
	JR	Z,OK
	INC	HL
	LD	(HL),A
	JR	SETE2
OK:
	CALL	#MPRNT
	DB	$1A,"Ok",$0D,0
	XOR	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Å²Ü!",7,$0D,0
	AND	A
	RET
;
REM2:
	CALL	TITLE
	CALL	#MPRNT
	DB	'release',$0D,'MEMAX:',0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	(#CEXE+1),DE
	XOR	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
TITLE:
	CALL	#MPRNT
	DB	$1A,"path v1.01 Lovers"
	DB	$0D,0
	RET
;
USAGE:
	CALL	TITLE
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" PATH [[drv:]pathname][[drv:]pathname]¥¥¥",$0D
	DB	" PATH      Êß½ É Ë®³¼Þ",$0D
	DB	" PATH ;    Êß½ É ¾¯Ã² ¶²¼Þ®",$0D
	DB	" PATH /R   ¼Þ®³Á­³ ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
ERROR:
	CALL	#MPRNT
	DB	"ºÉ ¼½ÃÑ ÆÊ À²µ³ ¼Ã ²Å²É!",7,$0D,0
	RET
;
;
;		 012345	 6 7
START:	DB	"[PATH ",0,0
;
_CEXE:	JP	#HOT
;
PATH:	DS	65,0
:
_CEXE2:
\DEPAT2:LD	DE,(0)		;DEPAT
	JR	_CEXE
CEXE:
	LD	A,(DE)
	CP	"\"
	JR	Z,_CEXE
	INC	DE
	LD	A,(DE)
	DEC	DE
	CP	":"
	JR	Z,_CEXE
\DEPAT:	LD	(0),DE		;DEPAT
\PATH:	LD	DE,0		;PATH
;
CEX1:
	LD	A,(DE)
CEXE1:
	CP	$20
	JR	C,_CEXE2
	CALL	#FILE
	JR	C,CEXX1
	PUSH	DE
	PUSH	DE
\FNAME:	LD	A,(0)		;#FNAME
	CP	$20
	JR	Z,CEX2
	CALL	#SOPEN
	JR	C,CEXX
	LD	BC,$000B
	ADD	HL,BC
	BIT	4,(HL)
	JR	Z,CEXX
	LD	BC,$001A-$000B
	ADD	HL,BC
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	LD	(#NDIRP),BC
CEX2:
DEPAT:	LD	DE,0
\FILE:	CALL	#FILED		;FILE1+3
	JR	C,CEXX
	INC	DE
\EXPAT:	LD	(0),DE		;EXPAT
\CEX3:	CALL	#HOT		;CEX3
CEXX:
	POP	DE
	POP	DE
CEXX1:
	LD	A,(DE)
	INC	DE
	CP	2
	JR	NC,CEXX1
	OR	A
	JR	NZ,CEX1
	JR	CEXE1
;
LAST:
