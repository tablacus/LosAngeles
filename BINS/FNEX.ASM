;
;	FNEX	by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"fnex v1.02 Lovers"
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
MAIN:
	CALL	PRO
	RET	C
	CALL	WOCALC
	LD	B,3
	CALL	CPSTR
	SCF
	JP	NZ,ERROR
	CALL	NMCALC
	LD	A,(IY)
	CP	$CD
	SCF
	JP	NZ,ERROR
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	LD	BC,LAST-START
	AND	A
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	PUSH	DE
;
	CALL	WOCALC
	POP	DE
	PUSH	DE
	PUSH	HL
	POP	IY
	LD	HL,WOPENP-START
	ADD	HL,DE
	EX	DE,HL
	LD	(IY),$CD
	LD	(IY+1),E
	LD	(IY+2),D
;
	CALL	NMCALC
	POP	DE
	PUSH	DE
	LD	HL,NAMEP-START
	ADD	HL,DE
	EX	DE,HL
	LD	L,(IY+1)
	LD	H,(IY+2)
	LD	(_WTDX2+1),HL
	LD	(IY+1),E
	LD	(IY+2),D
;
	POP	DE
;
	LD	HL,(#NFPRT+1)
	LD	(_NFPRT),HL
	LD	HL,(#FILED+1)
	LD	(_FILED),HL
;
	LD	HL,NFPRT-START
	ADD	HL,DE
	LD	(#NFPRT+1),HL
	LD	HL,MSN-START
	ADD	HL,DE
	LD	(\MSN1+1),HL
	LD	(\MSN2+1),HL
;
	LD	HL,FILED-START
	ADD	HL,DE
	LD	(#FILED+1),HL
	LD	HL,FILE7-START
	ADD	HL,DE
	LD	(\F71+1),HL
	LD	(\F72+1),HL
	LD	HL,FILE10-START
	ADD	HL,DE
	LD	(\F101+1),HL
	LD	(\F102+1),HL
	LD	HL,FILE11-START
	ADD	HL,DE
	LD	(\F111+1),HL
	LD	HL,CCHKF-START
	ADD	HL,DE
	LD	(\CC1+1),HL
	LD	(\CC2+1),HL
	LD	(\CC3+1),HL
	LD	(\CC4+1),HL
	LD	HL,CCHK2-START
	ADD	HL,DE
	LD	(\CCHK2+1),HL
	LD	HL,(#IBFAD)
	INC	HL
	LD	(\FN1+1),HL
	LD	(\FN2+1),HL
	INC	HL
	LD	(\FN11+1),HL
	PUSH	DE
	LD	DE,8-1
	ADD	HL,DE
	LD	(\FN81+1),HL
	LD	DE,$0C-8
	ADD	HL,DE
	LD	(\FNC1+1),HL
	LD	(\FNC2+1),HL
	LD	(\FNC3+1),HL
	LD	(\FNC4+1),HL
	POP	DE
;
	LD	HL,START
	LDIR
	XOR	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Å²Ü!",7,$0D,0
	AND	A
	RET
;
REM2:	CALL	#MPRNT
	DB	'release',$0D,'MEMAX:',0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(#NFPRT+1),DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(#FILED+1),DE
;
	CALL	NMCALC
	INC	HL
	LD	A,(HL)
	LD	(IY+1),A
	INC	HL
	LD	A,(HL)
	LD	(IY+2),A
;
	CALL	WOCALC
	EX	DE,HL
	LD	BC,3
	LDIR
	XOR	A
	RET
;
WOCALC:
	LD	HL,(#WOPENH+1)
	LD	DE,12
	ADD	HL,DE
	LD	DE,WOPOLD
	RET
;
NMCALC:
	LD	IY,(#NAME+1)
	LD	DE,21
	ADD	IY,DE
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" FNEX    ¼Þ®³Á­³",$0D
	DB	" FNEX /R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
ERROR:
	CALL	#MPRNT
	DB	"ºÉ ¼½ÃÑ ÆÊ À²µ³ ¼Ã ²Å²É!",7,$0D,0
	RET
;
WOPOLD:	DB	$77,$10,$FC
;
;		 012345	 6 7
START:	DB	"[FNEX ",0,0
;
_NFPRT:	DW	#HOT
_FILED:	DW	#HOT
_WTDX2:	JP	#HOT
;
NAMEP:
	LD	HL,(#FBAD)
	LD	DE,$10
	ADD	HL,DE
	LD	A,(HL)
	CP	$A0
	JR	Z,_WTDX2
	PUSH	BC
	LD	DE,$0C-$10
	ADD	HL,DE
	EX	DE,HL
\FNC3:	LD	HL,$0C		;#FNAME+$0C
	LD	BC,10
	LDIR
	POP	BC
	JR	_WTDX2
NFPRT:
	LD	B,8		;FILENAME
\MSN1:	CALL	MSN
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	LD	B,10
\MSN2:	CALL	MSN
	LD	B,$16-8
NFPRT1:
	DEC	HL
	DJNZ	NFPRT1
	LD	A,"."
	CALL	#PRINT
	LD	B,3
MSN:
	LD	A,(HL)
	CP	5
	JR	NZ,MSN3
	LD	A,$E5
MSN3:
	CP	$21
	JR	NC,MSN2
MSN1:
	LD	A,$A0
MSN2:
	CALL	#PRINT
	INC	HL
	DJNZ	MSN
	RET
;
FILED:
	XOR	A
	LD	(#WILD),A
\FNC1:	LD	HL,$0C		;#FNAME+$0C
	LD	B,10
\F71:	CALL	FILE7
	LD	A,$20
	LD	B,8+3
\FN1:	LD	HL,0		;#FNAME
	PUSH	HL
\F72:	CALL	FILE7
	POP	HL
	LD	B,8
;
	LD	A,(DE)		;$E5É¼®Ø
	CP	$E5
	JR	NZ,FILE3
	LD	A,5
	INC	DE
	JR	FILE2
;
FILE3:
\CC1:	CALL	CCHKF
	RET	Z
	CP	"*"
	JR	Z,FILE9
	CP	"."
	JR	Z,FILE4
FILE2:
	LD	(HL),A
	INC	HL
	DJNZ	FILE3
;
EFILE1:
\FNC2:	LD	HL,$0C		;#FNAME+$0C
	LD	B,10
;
	LD	A,(DE)
;
EFILE3:
\CC2:	CALL	CCHKF
	RET	Z
	CP	$20
	JR	NZ,EFNS
	XOR	A
EFNS:
	CP	"*"
	JR	Z,EFILE9
	CP	"."
	JR	Z,FILE4
	LD	(HL),A
	INC	HL
	DJNZ	EFILE3
;
FILE3L:
\CC3:	CALL	CCHKF
	RET	Z
	CP	"."
	JR	NZ,FILE3L
;
FILE4:
\FN81:	LD	HL,8		;#FNAME+8
	LD	B,3
;
FILE4L:
\CC4:	CALL	CCHKF
	RET	Z
	CP	"."
	JR	NZ,FILE12
\FN2:	LD	(0),A		;(#FNAME),A	;..É¼®Ø
\FN11:	LD	(1),A		;(#FNAME+1),A
	LD	A,$20
FILE12:
	CP	"*"
	JR	Z,FILE10
	LD	(HL),A
	INC	HL
	DJNZ	FILE4L
FILENE:
	AND	A
	RET
;
FILE9:
\F102:	CALL	FILE10
\FNC4:	LD	HL,$0C		;#FNAME+$0C
	LD	B,10
EFILE9:
\F101:	CALL	FILE10
	JR	FILE3L
;
FILE10:
	LD	A,"?"
\F111:	CALL	FILE11
;
FILE7:
	LD	(HL),A
	INC	HL
	DJNZ	FILE7
	RET
;
CCHKF:
	LD	A,(DE)
\CCHK2:	CALL	CCHK2
	RET	Z
	CP	$A0
	JR	NZ,CCHKF2
	LD	A,$20
CCHKF2:
	INC	DE
	CP	"?"
	RET	NZ
;
FILE11:
	LD	(#WILD),A
	AND	A
	RET
;
CCHK2:
	CP	"\"
	RET	Z
	CP	":"
	RET	Z
	CP	$20
	RET	NC
	CP	A	;Z=1
	RET
;
WOPENP:
	LD	BC,10
	ADD	HL,BC
	LD	B,C	;B=10
WOPL:
	LD	(HL),A	;A=0
	INC	HL
	DJNZ	WOPL
	RET
;
LAST:
