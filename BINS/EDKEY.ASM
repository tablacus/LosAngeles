;
;	EDKEY
;
	$INCLUDE	LA.DEF
;
	OFFSET	$C000-$3000
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
 	DB	$1A,"X1 edkey v1.01"
	DB	" (C) 1994 by Larn M.R."
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	EDKEY
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
EDKEY:
	CALL	PRO
	RET	C
;
	CALL	#VER
	LD	A,H
	CP	$20
	JR	Z,EDKEY1
	LD	HL,$FAEE
	LD	(PATCH2+1),HL
EDKEY1:
	LD	HL,(#FLGET+1)
	LD	(PATCH1+1),HL
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	AND	A
	LD	BC,LAST-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
;
	PUSH	HL
	EX	DE,HL
	LD	HL,START
	LDIR
	POP	HL
	LD	DE,PATCH1-START
	ADD	HL,DE
	LD	(#FLGET+1),HL
;
	AND	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Ï¾Ý!",7,$0D,0
	AND	A
	RET
;
REM2:	CALL	#MPRNT
	DB	'realese',$0D,'MEMAX:',0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	LD	H,A
	LD	(#FLGET+1),HL
	AND	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" EDKEY    ¼Þ®³Á­³",$0D
	DB	" EDKEY /R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
;
;		 012345	 6 7
START:	DB	"[EDKEY",0,0
;
PATCH1:	CALL	0
;
	CP	$1C
	RET	C
	CP	$20
	RET	NC
;
	PUSH	AF
PATCH2:	LD	A,($002F)
	AND	$10
	JR	NZ,KEYE
	POP	AF
	SUB	$1C
	JR	NZ,KEY1
	LD	A,6
	RET
KEY1:	DEC	A
	JR	NZ,KEY2
	INC	A	;A=1
	RET
KEY2:	DEC	A
	JR	NZ,KEY3
	LD	A,$12
	RET
KEY3:	LD	A,3
	RET
KEYE:
	POP	AF
	RET
LAST:
