;
;	X-DOS CG¥EMULATER
;
	$INCLUDE	LA.DEF
;
DATA	EQU	$C800
DTEND	EQU	$CC
LENGTH	EQU	1
;
	ORG	$BD00
;
	PUSH	DE
	CALL	INIT
	POP	DE
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SAVE1
	CP	"-"
	JR	Z,SAVE1
;
LOAD:
	CALL	$C000
	JP	#HOT
;
SAVE1:
	INC	DE
	LD	A,(DE)
	INC	DE
	CALL	#CAP
	CP	"S"
	JR	NZ,LOAD
;
SAVE2:
	PUSH	DE
	CALL	#MPRNT
	DB	"200 line...0",$0D
	DB	"400 line...1",0
	CALL	#FLGET
	CALL	#LTNL
	POP	DE
	CP	3
	RET	Z
	CP	$1B
	RET	Z
	CP	"0"
	JR	Z,SAVE3
	CP	"1"
	JR	NZ,SAVE2
SAVE3:
	SUB	"0"
	ADD	A,A
	ADD	A,A
	LD	(#FBF7),A
	OR	$20
	LD	(#FBF8),A
	AND	4
	RRC	A
	ADD	A,"2"
	CALL	#PRINT
	CALL	#MPRNT
	DB	"00 line mode",$0D,0
;
	CALL	$C003
	JP	#HOT
;
INIT:
	LD	HL,@HOT
	LD	DE,$ED30
	LD	BC,FIN-@HOT
	LDIR
	LD	IX,$C000
	LD	BC,$7FE
INIT1:
	LD	A,(IX+1)
	CP	$D6
	JR	NZ,INIT2
	LD	A,(IX+2)
	CP	$F8
	JR	NZ,INIT2
;
	LD	(IX+1),#WK1FD0
	LD	(IX+2),#WK1FD0/256
INIT2:
	LD	A,(IX+2)
	CP	$FB
	JR	NZ,INIT3
	LD	A,(IX+1)
	CP	$F7
	JR	Z,INIT4
	CP	$F8
	JR	NZ,INIT3
;
	LD	(IX+1),#FBF8
	LD	(IX+2),#FBF8/256
	JR	INIT3
INIT4:
	LD	(IX+1),#FBF7
	LD	(IX+2),#FBF7/256
INIT3:
	INC	IX
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,INIT1
	RET
;
AWOPEN:
	LD	HL,0
	PUSH	IX
	LD	IX,(#IBFAD)
	LD	(IX),L
	POP	IX
	LD	(#SIZE),HL
	LD	(#SIZEH),HL
;
	LD	HL,DATA
	LD	(ADDR),HL
;
	CALL	#WOPEN
	RET	C
	XOR	A
	RET
;
AWRD:
	PUSH	BC
	PUSH	HL
	PUSH	AF
	LD	HL,(#IBFAD)
	LD	BC,1+$1C
	ADD	HL,BC
	INC	(HL)
	JR	NZ,AWRD1
	INC	HL
	INC	(HL)
	JR	NZ,AWRD1
	INC	HL
	INC	(HL)
	JR	NZ,AWRD1
	INC	HL
	INC	(HL)
AWRD1:
	LD	HL,(ADDR)
	LD	(HL),A
	INC	HL
	LD	(ADDR),HL
	LD	A,H
	CP	DTEND
	JR	NC,AWRD2
	POP	AF
	POP	HL
	POP	BC
	XOR	A
	RET
AWRD2:
	PUSH	DE
	LD	HL,DATA
	LD	(ADDR),HL
	LD	(#DTADR),HL
	LD	B,LENGTH
	CALL	#WRDX
	POP	DE
	POP	BC
	LD	A,B
	POP	HL
	POP	BC
	RET	C
	XOR	A
	RET
;
AWCLSE:
	LD	HL,(#IBFAD)
	LD	DE,1+$1C
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	OR	(HL)
	INC	HL
	OR	(HL)
	INC	HL
	OR	(HL)
	RET	Z
;
	LD	HL,(ADDR)
	LD	DE,DATA
	AND	A
	SBC	HL,DE
;
	JR	Z,AWC1
;				;WRITE BUFFA
	LD	HL,DATA
	LD	(#DTADR),HL
	LD	B,LENGTH
	CALL	#WRDX
AWC1:
	CALL	NC,#WTFAT
	CALL	NC,#WTDIRX
	JP	#MOTOFF
ERROR:
	SCF
	RET
;
ARCLSE:
	AND	A
	RET
;
;	ORG	$ED30
;
@HOT	JP	#HOT
@PRINT	JP	#PRINT
@PRNT	JP	#PRNT0
@LTNL	JP	#LTNL
@NL	JP	#NL
@MSG	JP	#MSG
@MSX	JP	#MSX
@MPRINT	JP	#MPRNT
@TAB	JP	#TAB
@LPRNT	JP	#LPRNT
@LPTON	JP	#LPTON
@LPTOF	JP	#LPTOF
@GETL	JP	#GETL
@GETKY	JP	#GETKY
@BRKEY	JP	#BRKEY
@INKEY	JP	#INKEY
@PAUSE	JP	#PAUSE
@BELL	JP	#BELL
@PRTHX	JP	#PRTHX
@PRTHL	JP	#PRTHL
@ASC	JP	#ASC
@HEX	JP	#HEX
@AHEX	JP	#2HEX
@HLHEX	JP	#HLHEX
@WOPEN	JP	#WOPEN
@WRD	JP	#WRD
	JP	ERROR
@RDD	JP	#RDD
@FILE	JP	#FILE
@FPRINT	JP	#FPRNT
@MON	JP	#MON
@DSKRED	JP	ERROR
@DSKWRT	JP	ERROR
@DIR	JP	#CDIR
@ROPEN	JP	#ROPEN
@FSET	JP	#SET
@FRESET	JP	#RESET
@RNAME	JP	#NAME
@KILL	JP	#KILL
@CSR	JP	#CSR
@SCRN	JP	#SCRN
@LOC	JP	#LOC
@FLGET	JP	#FLGET
@RDVSW	JP	#RDVSW
@SDVSW	JP	#SDVSW
@ERROR	JP	#ERROR
@CLS	JP	$0A8F
@PALET	JP	ERROR
@LMODUL	JP	ERROR
@SETDMA	JP	ERROR
@WIDTH	JP	#WIDTH
@AROPEN	JP	#ROPEN
@ARCLSE	JP	ARCLSE
@ARDD	JP	#FGETC
@AWOPEN	JP	AWOPEN
@AWCLSE	JP	AWCLSE
@AWRD	JP	AWRD
@GETREC	JP	ERROR
@COPY	JP	ERROR
@BIOSJP	JP	$0018
@FTYCH	JP	ERROR
@BOOT	JP	#COLD
@DSKRD2	JP	ERROR
@DSKWT2	JP	ERROR
@CALL	JP	#CMDANL
@PRT10	JP	#PRTHLX
@SYSCAL	JP	ERROR
@PTIME	JP	ERROR
FIN:
;
;	WORK
;
ADDR	DW	DATA
#FBF7	DB	0
#FBF8	DB	$20
