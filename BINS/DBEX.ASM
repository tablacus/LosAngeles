;
;	DBEX by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	OFFSET	$C000-$3000
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"dbex v1.05 Lovers"
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	DBEX
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
;
DBEX:	CALL	PRO
	RET	C
;
	LD	A,(#GCEH)
	LD	(GCEH),A
	LD	A,(#CLLN)
	LD	(CLLN),A
	LD	HL,(#DTBUF)
	LD	(DTBUF),HL
	LD	HL,(#DIRBF)
	LD	(DIRBF),HL
	LD	HL,(#GCBUF)
	LD	(GCBUF),HL
;
	CALL	#MPRNT
	DB	'MEMAX:',0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	DEC	H
	LD	A,H
	LD	(#GCEH),A
	LD	A,1
	LD	(#CLLN),A
	DEC	H
	DEC	H
	DEC	H
	LD	L,0
	LD	(#DTBUF),HL
	LD	(#DIRBF),HL
	LD	(#GCBUF),HL
	DEC	H
	LD	L,START-DATA
	LD	(#MEMAX),HL
;
	CALL	#PRTHL
	CALL	#LTNL
;
	EX	DE,HL
	LD	HL,START
	LD	BC,DATA-START
	LDIR
;
	AND	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	INC	H
	JR	Z,REME
	DEC	H
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM1
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Ï¾Ý!",7,$0D,0
	AND	A
	RET
REM1:	LD	A,(#MAXDRV)
	LD	B,A
	LD	C,'A'
RLOOP1:	LD	A,C
	CALL	#DEVAD2
	LD	A,(IY)
	CP	3
	JR	NC,REME
	INC	C
	DJNZ	RLOOP1
;
REM2:	CALL	#MPRNT
	DB	'release',$0D,'MEMAX:',0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(#MEMAX),DE
	CALL	#PRTHL
	CALL	#LTNL
;
	LD	A,(HL)
	INC	HL
	LD	(#GCEH),A
;
	LD	A,(HL)
	INC	HL
	LD	(#CLLN),A
;
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(#DTBUF),DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(#DIRBF),DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	(#GCBUF),DE
	AND	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" DBEX    ¼Þ®³Á­³",$0D
	DB	" DBEX /R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
;
;		 012345	 6 7
START:	DB	'[DBEX ',0,0
;
GCEH:	DB	0
CLLN:	DB	0
DTBUF:	DW	0
DIRBF:	DW	0
GCBUF:	DW	0
DATA:
