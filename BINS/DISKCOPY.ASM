;
;	DISKCOPY
;
	$INCLUDE	LA.DEF
;
DATA	EQU	$3400
;
	ORG	$3000
;
	CALL	START
MAINX:	CALL	MAIN1
	CALL	#MOTOFF
	RET	C
	CALL	#MPRNT
	DB	$0D,"completed !",$0D
	DB	"Ó³²ÁÄÞ?(Y/other)",0
	CALL	#FLGET
	CALL	#LTNL
	CP	"Y"
	JR	Z,MAINX
	CP	"y"
	JR	Z,MAINX
	CP	"Ý"
	JR	Z,MAINX
	AND	A
	RET
;
START:	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"diskcopy v1.06"
	DB	" (C) 1994 by Larn M.R.",$0D,0
	POP	DE
;
SW1:	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,MAIN
SWI:
	INC	DE
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	"V"
	JR	NZ,SW2
	CALL	#MPRNT
	DB	"verify on",$0D,0
	XOR	A
	LD	(VERSW+1),A
	JR	SW1
SW2:
	JR	SW1
;
MAIN:	CALL	DRIVE
	JP	C,USAGE
	LD	A,(#DSK)
	LD	(DSK1),A
	CALL	#DEVADR
	PUSH	IY
	POP	IX
	CALL	#SPSK
	CALL	DRIVE
	JP	C,USAGE
	LD	A,(#DSK)
	LD	(DSK2),A
	RET
;
MAIN1:	CALL	#MPRNT
	DB	"diskcopy ",0
	LD	A,(DSK1)
	CALL	#DEVADX
	CALL	DPRINT
	CALL	#MPRNT
	DB	" to ",0
	CALL	#DEVADR
	CALL	DPRINT
	CALL	#LTNL
;
	CALL	SURE
	CALL	#LTNL
	RET	C
;
	LD	A,(DSK1)
	CALL	#CHGDRV
	CALL	#RDFAT
	RET	C
	LD	A,(DSK2)
	CALL	#CHGDRV
	CALL	#RDFAT
	RET	C
;
	CALL	DEVICE
	SCF
	RET	NZ
;
INIT:
	LD	A,(#MEMAX+1)
	LD	HL,(#MEMAX1)
	CP	H
	JR	C,MEMAX
	LD	A,H
MEMAX:
	SUB	DATA/256
	RET	C
	SRL	A
	SRL	A
	LD	(BCBF+1),A
;
	LD	E,(IX+!ADDCL)
	LD	A,(IX+!DBLCL)
	OR	A
	JR	Z,INIT1
	SRL	E
INIT1:	LD	D,0
	LD	L,(IX+!MAXCL)
	LD	H,(IX+!MAXCL+1)
	ADD	HL,DE
	LD	E,0
;
DC:
BCBF:	LD	BC,32
	AND	A
	SBC	HL,BC
	JR	C,DCEND
	JR	Z,COPY
;
	CALL	COPY
	JR	NC,DC
	JP	#LTNL
;
DCEND:	ADD	HL,BC
	LD	C,L
;
COPY:	PUSH	DE
	PUSH	HL
	CALL	READ
	POP	HL
	POP	DE
	RET	C
	PUSH	HL
	CALL	WRITE
	POP	HL
	RET
;
READ:	LD	A,(DSK1)
	CALL	#CHGDRV
	LD	A,"r"
	CALL	#PRINT
READX:	LD	HL,DATA
	LD	B,C
READ1:	PUSH	BC
	CALL	#DRDC
	POP	BC
	RET	C
	CALL	#BRKEY
	SCF
	RET	Z
	CCF
	DJNZ	READ1
	RET
;
WRITE:	LD	A,(DSK2)
	CALL	#CHGDRV
	LD	A,"w"
	CALL	#PRINT
	LD	(DEBF+1),DE
	CALL	WRITEX
VERSW:	LD	A,$01
	OR	A
	RET	NZ
DEBF:	LD	DE,0
	LD	A,"v"
	CALL	#PRINT
	JR	READX
;
WRITEX:	LD	HL,DATA
	LD	B,C
WRITX1:	PUSH	BC
	CALL	#DWTC
	POP	BC
	RET	C
	CALL	#BRKEY
	SCF
	RET	Z
	CCF
	DJNZ	WRITX1
	RET
;
;				DEVICE CHECK
DEVICE:	LD	A,(IX+!FATLN)
	CP	(IY+!FATLN)
	RET	NZ
	LD	A,(IX+!FATID)
	CP	(IX+!FATID)
	RET	NZ
	LD	A,(IX+!ADDCL)
	CP	(IY+!ADDCL)
	RET	NZ
	LD	A,(IX+!DBLCL)
	CP	(IY+!DBLCL)
	RET	NZ
	LD	A,(IX+!MAXCL)
	CP	(IY+!MAXCL)
	RET	NZ
	LD	A,(IX+!MAXCL+1)
	CP	(IY+!MAXCL+1)
	RET	NZ
	LD	A,(IX+!MAXDIR)
	CP	(IY+!MAXDIR)
	RET	NZ
	LD	A,(IX+!FATPS)
	CP	(IY+!FATPS)
	RET	NZ
	LD	A,(IX+!DIRPS)
	CP	(IY+!DIRPS)
	RET	NZ
	LD	A,(IX+!DEVNO)
	CP	(IY+!DEVNO)
	JR	Z,DEV1
DEV0:	CP	A
	RET
DEV1:	LD	A,(IX+!UNITNO)
	SUB	(IY+!UNITNO)
	JR	NZ,DEV0
	INC	A
	RET
;
DRIVE:	LD	A,(DE)
	INC	DE
	CALL	#CHGDRV
	RET	C
	LD	A,(DE)
	INC	DE
	CP	":"
	SCF
	RET	NZ
	CCF
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" DISKCOPY [½²¯Á] <drv1>: <drv2>:",$0D,$0D
	DB	"½²¯Á:"
	DB	9,"/V  verify on",$0D
	DB	0
	POP	AF
	AND	A
	RET
;
SURE:	CALL	#MPRNT
	DB	"hit [CR] to start",0
SURE1:	CALL	#FLGET
	CALL	BREAK
	SCF
	RET	Z
	CP	$0D
	SCF
	CCF
	RET	Z
	JR	SURE1
;
BREAK:	CP	3
	RET	Z
	CP	$1B
	RET
;
DPRINT:	LD	A,(IY+$1C)
	CALL	#PRINT
	LD	A,(IY+$1D)
	CALL	#PRINT
	LD	A,(IY+$1E)
	CALL	#PRINT
	LD	A,(IY+$1F)
	JP	#PRINT
;
DSK1:	DB	0
DSK2:	DB	0
;
