;
;	BANK RAM DISK DRIVER
;
	$INCLUDE	LA.DEF
;
DEID	EQU	$0B
;
;
	OFFSET	$C000-$8000
	ORG	$8000
;
;	INIT
;
	XOR	A
	LD	(GRADF+1),A
	LD	(AUTOS+1),A
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"X1 bank ram disk driver v1.14"
	DB	" Lovers",$0D,0
	POP	DE
GRAD1:	CALL	#SPSK
	LD	A,(DE)
	INC	DE
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,GRAD2
SWI:
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	'F'
	JR	NZ,GRADS1
	LD	(GRADF+1),A
GRADS1:	CP	'A'
	JR	NZ,GRADS2
	LD	(AUTOS+1),A
	JR	GRAD1
GRADS2:	CP	'R'
	JR	NZ,GRAD1
	JP	REMOVE
;
GRAD2:	LD	A,(DE)
	CP	':'
	JR	Z,GRAD3
GRADM:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" BRAD [½²¯Á] drv:",$0D,$0D
	DB	9,"/A µ°ÄÓ°ÄÞ",$0D
	DB	9,"/F Ì«°Ï¯Ä",$0D
	DB	9,"/R ¶²¼Þ®",$0D,0
	RET
;
GRAD3:	LD	A,(#MAXDRV)
	LD	B,A
	DEC	DE
	LD	A,(DE)
	CP	'0'
	JR	C,GRADM
	LD	(DRIVE),A
;
GRAD4:	LD	A,B
	CALL	#DEVAD2
	JR	C,GRAD42
	LD	A,(IY+$12)
	CP	DEID
	JR	NZ,GRAD42
	LD	A,(IY+$13)
	OR	A
	JP	Z,GRADZ
GRAD42:	DJNZ	GRAD4
;
	CALL	GRADP
	RET	NC
;
	LD	BC,$37D0
	OUT	(C),C
	LD	A,$3F
	OUT	($D0),A
	IN	A,(C)
	CP	C
	RET	NZ
;
	LD	BC,$0B00
	LD	HL,$7F00
	DI
;
BANK1:	OUT	(C),L
	LD	E,(HL)
	LD	A,E
	ADD	A,$A5
	LD	(HL),A
	CP	(HL)
	LD	(HL),E
	JR	NZ,BANK2
	INC	L
	BIT	4,L
	JR	Z,BANK1
;
BANK2:	LD	A,$10
	OUT	(C),A
	EI
	LD	A,L
	SUB	$01
	RET	C
	CALL	#MPRNT
	DB	"BANK:",0
	LD	A,L
	CALL	#PRTHX
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	DEC	HL
	DEC	HL
	DEC	HL
	LD	(CLPAT+1),HL
	CALL	#MPRNT
	DB	$0D,"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	AND	A
	LD	BC,LAST-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
;
;	SOFT RELOCATE
;
	EX	DE,HL
	LD	HL,ADR-START
	ADD	HL,DE
	LD	(PATCH1+1),HL
	LD	(PATCH2+1),HL
;
	LD	HL,TFR-START
	ADD	HL,DE
	LD	(PATCH3+1),HL
	LD	(PATCH4+1),HL
;
	LD	HL,PATCH5+1-START
	ADD	HL,DE
	LD	(DRDC+2),HL
	LD	(DWTC+2),HL
;
	LD	HL,START
	PUSH	DE
	LDIR
	POP	DE
;
;	WRITE DEVICE TABLE
;
	LD	(IY+$00),1	;FATLN
	LD	(IY+$01),$F9	;FATID
	LD	HL,DRDC-START
	ADD	HL,DE
	LD	(IY+$02),L	;DRDC
	LD	(IY+$03),H
	LD	HL,DWTC-START
	ADD	HL,DE
	LD	(IY+$04),L	;DWTC
	LD	(IY+$05),H
	LD	(IY+$06),3	;ADDCL
	LD	(IY+$07),0
CLPAT:	LD	HL,256
	LD	(IY+$08),L	;CLEND
	LD	(IY+$09),H
	LD	(IY+$0A),0	;FDMODE
	LD	(IY+$0B),5	;MAXDIR
	LD	(IY+$0C),0	;MAXTRK
	LD	(IY+$0D),1	;MAXSEC
	LD	(IY+$0E),0	;FATPS
	LD	(IY+$0F),4	;INCSEC(1024=4)
	LD	(IY+$10),1	;DIRPS
	LD	(IY+$11),1	;ADDSEC
	LD	(IY+$12),DEID	;DEVNO
	LD	(IY+$13),0	;UNITNO
	LD	HL,MOTOFF-START
	ADD	HL,DE
	LD	(IY+$14),L	;MOTOFF
	LD	(IY+$15),H
	LD	HL,(#GNCLA)
	LD	(IY+$16),L	;GNCL
	LD	(IY+$17),H
	LD	HL,(#SNCLA)
	LD	(IY+$18),L	;SNCL
	LD	(IY+$19),H
	LD	(IY+$1A),0	;SDIR
	LD	(IY+$1B),0
	LD	(IY+$1C),'B'
	LD	(IY+$1D),'R'
	LD	(IY+$1E),'A'
	LD	(IY+$1F),'M'
;
;	Ì«°Ï¯Ä
;
GRADF:	LD	A,$00
	OR	A
	RET	Z
;
	CALL	#MPRNT
	DB	"format",$0D,0
;
	LD	A,(DRIVE)
	CALL	#CHGDRV
	RET	C
	CALL	#DEVADR
	LD	HL,(#DTBUF)
	LD	E,L
	LD	D,H
	INC	DE
	XOR	A
	LD	(HL),A
	LD	BC,$03FF
	LDIR
;
	LD	HL,(#DTBUF)
	LD	A,(IY+$01)
	LD	(HL),A	;FATID
	INC	HL
	DEC	(HL)	;LD (HL),$FF
	INC	HL
	DEC	(HL)
	LD	E,(IY+$0E)
	LD	D,0
	LD	B,(IY)		;FATLN
FORMAT0:PUSH	BC
	LD	HL,(#DTBUF)
	CALL	#DWTC
	POP	BC
	RET	C
	LD	HL,(#DTBUF)
	XOR	A
	LD	(HL),A
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	DJNZ	FORMAT0
;
	LD	E,(IY+$10)
	LD	D,0
FORMAT2:LD	HL,(#DTBUF)
	CALL	#DWTC
	RET	C
	LD	A,E
	CP	(IY+$0B)
	JR	C,FORMAT2
	RET
;
GRADZ:	PUSH	IY
	POP	HL
	CALL	GRADP
	RET	NC
	PUSH	IY
	POP	DE
	LD	BC,$0020
	LDIR
	JP	GRADF
;
GRADP:	LD	A,(DE)
	CALL	#DEVAD2
	RET	C
	LD	A,(IY+$12)
	CP	DEID
	SCF
	RET	Z
AUTOS:	LD	A,$00
	OR	A
	SCF
	RET	NZ
;
	CALL	#MPRNT
	DB	'Now Drive "',0
	LD	A,(DE)
	CALL	#PRINT
	CALL	#MPRNT
	DB	":",$22," is ",$22,0
	LD	B,4
NAME:	LD	A,(IY+$1C)
	INC	IY
	CALL	#PRINT
	DJNZ	NAME
	LD	B,4
NAME1:	DEC	IY
	DJNZ	NAME1
	CALL	#MPRNT
	DB	'"',$0D
	DB	'Do you want to change to "BRAD"? (Y/other):',0
	CALL	#FLGET
	CALL	#CAP
	CP	'Y'
	JR	Z,SURE1
	CP	'Ý'
	JR	Z,SURE1
;
	CALL	#MPRNT
	DB	'No',$0D,0
	AND	A
	RET
;
SURE1:	CALL	#MPRNT
	DB	"Yes",$0D,0
	SCF
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	INC	H
	JR	Z,REME
	DEC	H
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM1
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Å²Ü!",7,$0D,0
	RET
REM1:	CALL	#MPRNT
	DB	'release',$0D,'MEMAX:',0
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	LD	A,(#MAXDRV)
	LD	B,A
REM2:	LD	A,B
	CALL	#DEVAD2
	JR	C,REM3
	LD	A,(IY+$12)
	CP	DEID
	JR	NZ,REM3
	LD	(IY),0
REM3:	DJNZ	REM2
	CALL	#RDVSW
	CALL	#DEVAD2
	RET	NC
	LD	A,(#MAXDRV)
	LD	B,A
	LD	C,1
REM4:	LD	A,C
	CALL	#DEVAD2
	JR	NC,REM5
	INC	C
	DJNZ	REM4
	SCF
	RET
;
REM5:	LD	A,C
	ADD	A,$40
	JP	#SDVSW
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
START:	DB	"[BRAD ",0,0
;
DRDC:	LD	(PATCH5+1),SP
	LD	SP,$FF60
	PUSH	DE
	EXX
	 PUSH	 BC
	 PUSH	 DE
PATCH1:	 CALL	 ADR
PATCH3:	CALL	TFR
	EXX
DWTCE:	 POP	 DE
	 POP	 BC
	 EXX
	POP	DE
	INC	DE
	EI
PATCH5:	LD	SP,0
	XOR	A
MOTOFF:	RET
;
DWTC:	LD	(PATCH5+1),SP
	LD	SP,$FF60
	PUSH	DE
	EXX
	 PUSH	 BC
	 PUSH	 DE
PATCH2:	 CALL	 ADR
	EXX
	 LD	D,E
	 LD	E,A
	 EXX
	EX	DE,HL
PATCH4:	CALL	TFR
	EX	DE,HL
	EXX
	 OUT	 (C),D	 ;MAIN
	 JR	 DWTCE
;
ADR:	 EXX
	LD	A,E
	ADD	A,A
	RL	D
	ADD	A,A
	RL	D
	PUSH	AF
	ADD	A,A
	LD	A,D
	ADC	A,A
	AND	$0F
	EXX
	 LD	 BC,$0B00
	 LD	 D,A	  ;BANK
	 LD	 E,$10	  ;MAIN
	 EXX
	POP	DE
	LD	B,0
	LD	E,B
	RES	7,D
	RET
;
TFR:	PUSH	DE
	DI
;
TFR1:	EXX
	 OUT	 (C),D	 ;BANK(MAIN)
	 EXX
	EX	(SP),HL
	LD	A,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	EX	(SP),HL
	EXX
	 OUT	 (C),E	 ;MAIN(BANK)
	 EXX
	LD	(HL),A
	INC	HL
	LD	(HL),C
	INC	HL
	LD	(HL),D
	INC	HL
	LD	(HL),E
	INC	HL
	DJNZ	TFR1
	POP	DE
	RET
;
LAST
;
DRIVE:	DB	0
;
