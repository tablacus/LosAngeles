;
;	TIMEX
;
	$INCLUDE	LA.DEF
;
S49REB	EQU	$0B26
IN49SB	EQU	$0B49
OT49SB	EQU	$0B54
COMOUT	EQU	$0DFE
#FNAME	EQU	$1481
;
	OFFSET	$C000-$3000
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"time stamp suppot, X1 timex v1.02",$0D
	DB	" (C) 1994 by Larn M.R."
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	TIMEX
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
TIMEX:
	CALL	PRO
	RET	C
;
	LD	HL,(#PRTTMS+1)
	LD	(PRTTMSX),HL
	LD	HL,(#SETTMS+1)
	LD	(SETTMSX),HL
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	AND	A
	LD	BC,LAST-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
;
	EX	DE,HL
	LD	HL,PRTTMS-START
	ADD	HL,DE
	LD	(#PRTTMS+1),HL
;
	LD	HL,SETTMS-START
	ADD	HL,DE
	LD	(#SETTMS+1),HL
;
	LD	HL,PRTA-START
	ADD	HL,DE
	LD	(PATCH1+1),HL
	LD	(PATCH2+1),HL
	LD	(PATCH3+1),HL
	LD	(PATCH4+1),HL
	LD	(PATCH5+1),HL
;
	LD	HL,BCD-START
	ADD	HL,DE
	LD	(BCD1+1),HL
	LD	(BCD2+1),HL
	LD	(BCD3+1),HL
	LD	(BCD4+1),HL
	LD	(BCD5+1),HL
;
	LD	HL,START
	LDIR
	AND	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Ï¾Ý!",7,$0D,0
	AND	A
	RET
;
REM2:	CALL	#MPRNT
	DB	"release",$0D,"MEMAX:",0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	INC	DE
	LD	H,A
	LD	(#PRTTMS+1),HL
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	LD	H,A
	LD	(#SETTMS+1),HL
	AND	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" TIMEX    ¼Þ®³Á­³",$0D
	DB	" TIMEX /R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
;
;		 012345	 6 7
START:	DB	"[TIMEX",0,0
;
PRTTMSX:DW	0
SETTMSX:DW	0
;
PRTTMS:
;
	LD	A,(#WIDTH)
	CP	80
	RET	C
;
	CALL	#PRNTS
	CALL	#PRNTS
;
	LD	A,(IY+24)
	OR	A
	RET	Z
	LD	L,A
;
	LD	H,(IY+25)
	SRL	H
	RLA
	RLA
	RLA
	RLA
	AND	$0F
	LD	D,A
	LD	A,H
	ADD	A,80
PATCH1:	CALL	PRTA
	LD	A,"-"
	CALL	#PRINT
	LD	A,D
PATCH2:	CALL	PRTA
	LD	A,"-"
	CALL	#PRINT
	LD	A,L
	AND	$1F
PATCH3:	CALL	PRTA
;
	LD	L,(IY+22)
	LD	H,(IY+23)
;	LD	A,H
;	OR	L
;	RET	Z
;
	CALL	#PRNTS
	LD	A,H
	LD	H,L
	RRA
	RR	L
	RRA
	RR	L
	RRA
	RR	L
	AND	$1F
PATCH4:	CALL	PRTA
	LD	A,":"
	CALL	#PRINT
	LD	A,L
	RRCA
	RRCA
	AND	$3F
PATCH5:	CALL	PRTA
	LD	A,":"
	CALL	#PRINT
	LD	A,H
	AND	$1F
	ADD	A,A
;
;	PRINT10
;
PRTA:
	SUB	100
	JR	NC,PRTA
	ADD	A,100
	LD	B,"0"-1
PRTA1:
	SUB	10
	INC	B
	JR	NC,PRTA1
	ADD	A,10
	LD	C,A
	LD	A,B
	CALL	#PRINT
	LD	A,C
	ADD	A,"0"
	JP	#PRINT
;
SETTMS:
	LD	A,$EF	;Ä¹²ÖÐÀÞ¼
	CALL	COMOUT
	CALL	IN49SB
BCD1:	CALL	BCD
	LD	H,A
	CALL	IN49SB
BCD2:	CALL	BCD
	ADD	A,A
	ADD	A,A
	LD	L,A
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	CALL	IN49SB
BCD3:	CALL	BCD
	RRCA
	AND	$1F
	OR	L
	LD	L,A
	LD	(#FNAME+$16),HL
;
	LD	A,$ED	;¶ÚÝÀÞ°ÖÐÀÞ¼
	CALL	COMOUT
	CALL	IN49SB
BCD4:	CALL	BCD
	SUB	80
	JR	NC,SETTMS1
	ADD	A,100
SETTMS1:
	LD	H,A
	CALL	IN49SB
	AND	$F0
	LD	L,A
	ADD	HL,HL
	CALL	IN49SB
BCD5:	CALL	BCD
	OR	L
	LD	L,A
	LD	(#FNAME+$18),HL
	RET
;
BCD:
	LD	C,A
	AND	$F0
	RRCA
	LD	B,A
	RRCA
	RRCA
	ADD	A,B
	LD	B,A
	LD	A,C
	AND	$0F
	ADD	A,B
	RET
LAST:
