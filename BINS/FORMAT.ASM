;
;	FORMAT
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"X1 format v1.12"
	DB	" Lovers",$0D,0
	POP	DE
;
SW1:	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,MAIN
SWI:
	INC	DE
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	"V"
	JR	Z,SW1
SW2:	CP	"C"
	JP	NZ,USAGE
	PUSH 	DE
	LD	DE,PRLOG
	CALL	#MSG
	CALL	#LTNL
	POP	DE
	XOR	A
	LD	(LOGSW+1),A
	JR	SW1
;
MAIN:	LD	A,(DE)
	INC	DE
	CALL	#CHGDRV
	JP	C,USAGE
	LD	A,(DE)
	CP	":"
	JP	NZ,USAGE
;
MAIN1:	CALL	#DEVADR
	CALL	#MPRNT
	DB	"format ",0
	LD	B,4
PRNAME:	LD	A,(IY+$1C)
	CALL	#PRINT
	INC	IY
	DJNZ	PRNAME
	DEC	IY
	DEC	IY
	DEC	IY
	DEC	IY
	CALL	#LTNL
	LD	A,(IY+!DEVNO)
	CP	$87
	JP	NZ,LOGIC
LOGSW:	LD	A,1
	OR	A
	JP	Z,LOGICY
;
	CALL	#VER		;Normal
	LD	A,H
	AND	$F0
	CP	$20
	JP	NZ,LOGIC
	LD	A,L
	RRA
	LD	A,$00
	JR	NC,TYPE0
;
	CALL	#MPRNT
	DB	"format type (2D=0,2HD=2)? ",0
TYPE:	CALL	#FLGET
	CP	"0"
	JR	Z,TYPE0
	CP	"2"
	JR	Z,TYPE2
	CALL	BREAK
	RET	Z
	JR	TYPE
;
TYPE0:	LD	BC,LGAP1
	LD	DE,LSEC
	LD	IX,LGAP4
	LD	HL,(#M2D)
	JR	TYPEX
;
TYPE2:	LD	BC,HGAP1
	LD	DE,HSEC
	LD	IX,HGAP4
	LD	HL,(#M2HD)
;
TYPEX:	CALL	#PRINT
	CALL	#NL
	LD	(GAP1+1),BC
	LD	(SEC+1),DE
	LD	(GAP4+1),IX
	PUSH	IY
	POP	DE
	LD	BC,18
	LDIR
	LD	A,(#DSK)
	CALL	#CHGDRV
	LD	A,(IY+!UNITNO)
	AND	3
	LD	L,A
	LD	H,$1F
	LD	(HL),$FF
	CALL	SURE
	CALL	#LTNL
	RET	C
;
	CALL	#MPRNT
	DB	"physical format",$0D,0
	CALL	PHYSIC
	CALL	#LTNL
	JP	C,#MOTOFF
	CALL	VERIFY
	CALL	#LTNL
	JP	C,#MOTOFF
;
	LD	HL,FDATA
	LD	DE,FDATA+1
	LD	(HL),0
	LD	BC,$400
	LDIR
	LD	A,(IY+!FDMODE)
	INC	A
	LD	HL,LIPL
	JR	Z,LOW
	LD	HL,HIPL
LOW:	LD	DE,FDATA
	LD	C,$20
	LDIR
	LD	HL,IPLPRG
	LD	C,IPLPEND-IPLPRG
	LDIR
	LD	HL,FDATA+$1E0
	LD	B,$10
LOW1:	LD	(HL),$20
	INC	HL
	LD	(HL),$FE
	INC	HL
	DJNZ	LOW1
;
	LD	DE,0
	LD	HL,FDATA
	CALL	#DWT
	JP	C,#MOTOFF
;
	JR	LOGICX
;
LOGICY:	CALL	#RDFAT
	CALL	#MOTOFF
	RET	C
LOGIC:	CALL	SURE
	CALL	#LTNL
	RET	C
LOGICX:	CALL	#MPRNT
	DB	"logical  format",$0D,0
	LD	HL,(#DTBUF)
	LD	E,L
	LD	D,H
	INC	DE
	XOR	A
	LD	(HL),A
	LD	BC,$03FF
	LDIR
;
	LD	HL,(#DTBUF)
	LD	A,(IY+$01)
	LD	(HL),A		;FATID
	INC	HL
	DEC	(HL)		;LD (HL),$FF
	INC	HL
	DEC	(HL)
	LD	E,(IY+!FATPS)
	LD	D,0
	LD	B,(IY+!FATLN)
LOGIC1:	PUSH	BC
	LD	HL,(#DTBUF)
	CALL	#DWTC
	POP	BC
	RET	C
	LD	HL,(#DTBUF)
	XOR	A
	LD	(HL),A
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	DJNZ	LOGIC1
;
	LD	E,(IY+!DIRPS)
	LD	D,0
LOGIC2:	LD	HL,(#DTBUF)
	CALL	#DWTC
	RET	C
	LD	A,E
	CP	(IY+!MAXDIR)
	JR	C,LOGIC2
	CALL	#MOTOFF
	CALL	#MPRNT
	DB	"completed !",$0D
	DB	"Ó³²ÁÄÞ?(Y/other)",0
	CALL	#FLGET
	CALL	#LTNL
	CP	"Y"
	JR	Z,MAINX
	CP	"y"
	JR	Z,MAINX
	CP	"Ý"
MAINX:	JP	Z,MAIN1
	AND	A
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" FORMAT [½²¯Á] <drv>:",$0D,$0D
	DB	9,"/C  "
PRLOG:	DB	"logical only",$0D
	DB	0
	RET
;
SURE:	CALL	#MPRNT
	DB	"hit [CR] to start",0
SURE1:	CALL	#FLGET
	CALL	BREAK
	SCF
	RET	Z
	CP	$0D
	SCF
	CCF
	RET	Z
	JR	SURE1
;
BREAK:	CP	3
	RET	Z
	CP	$1B
	RET
;
PHYSIC:
	LD	DE,0
	EXX
	LD	L,0	;CYLINDER
PH1:
	LD	A,"w"
	CALL	#PRINT
PH2:
	EXX
	CALL	MAKE
	RET	C
	EXX
	CALL	#BRKEY
	SCF
	RET	Z
	INC	L
	LD	A,L
	CP	(IY+!MAXCYL)
	JR	C,PH1
	RET
;
MAKE:	LD	IX,FDATA
	EXX
	LD	C,0		;SIDE0
	CALL	MKDATA
	LD	(GDATA+1),IX
	EXX
	LD	C,1		;SIDE1
	CALL	MKDATA
;
	LD	HL,FDATA
	CALL	WTTRK
	RET	C
GDATA:	LD	HL,FDATA
WTTRK:
	PUSH	DE
	CALL	#WTTRK
	POP	DE
	RET	C
;
	LD	L,(IY+!MAXSEC)
	LD	H,0
	ADD	HL,DE
	EX	DE,HL
	AND	A
	RET
;
MKDATA:
	LD	H,0
	EXX
GAP1:	LD	HL,HGAP1
	CALL	SETD
MAKE1:
SEC:	LD	HL,HSEC
	CALL	SETD
	EXX
	LD	(IX),L	;CYLINDER
	INC	IX
	LD	(IX),C	;SIDE
	INC	IX
	INC	H
	LD	(IX),H	;SECTOR
	PUSH	HL
	INC	IX
	LD	A,(IY+!INCSEC)
	CP	2
	JR	Z,SIZE
	DEC	A
SIZE:	LD	(IX),A	;SIZE
	INC	IX
	EXX
	CALL	SETD
	POP	AF		;AF=HL
	CP	(IY+!MAXSEC)
	JR	C,MAKE1
GAP4:	LD	HL,HGAP4
;
SETD:	LD	A,(HL)
	INC	HL
	LD	B,A
	INC	A
	RET	Z
	LD	A,(HL)
	INC	HL
SETD1:	LD	(IX),A
	INC	IX
	DJNZ	SETD1
	JR	SETD
;
VERIFY:
	LD	A,(#SEEKSP)
	PUSH	AF
	LD	A,(IY+!MAXSEC)
	ADD	A,A
	LD	C,A
;
	EXX
	LD	DE,0
	EXX
	LD	L,(IY+!MAXCYL)
VE2:
	LD	A,"v"
	CALL	#PRINT
	LD	B,C
VER2:
	EXX
	LD	HL,FDATA
	CALL	#DRD
	EXX
	JR	C,VERE
	DJNZ	VER2
;
	DEC	L
	JR	NZ,VE2
VERE:
	POP	BC
	LD	A,B
	LD	(#SEEKSP),A
	RET
;
;	Format Data 2D
;
LGAP1:	DB	32,$4E,255
;
LSEC:	DB	12,$00,3,$F5,1,$FE,255
;
	DB	1,$F7,22,$4E,12,$00,3,$F5,1,$FB
	DB	0,$E5,0,$E5
	DB	1,$F7,84,$4E,255
;
LGAP4:	DB	0,$4E,0,$4E,255
;
;	Format Data 2HD
;
HGAP1:	DB	80,$4E,12,$00,3,$F6,1,$FC,50,$4E,255
;
HSEC:	DB	12,$00,3,$F5,1,$FE,255
;
	DB	1,$F7,22,$4E,12,$00,3,$F5,1,$FB
	DB	0,$E5,0,$E5,0,$E5,0,$E5
	DB	1,$F7,116,$4E,255
;
HGAP4:	DB	0,$4E,0,$4E,0,$4E,255
;
;	BOOT-DATA
;
LIPL:	DB	$EB,$FE,$90,$20,"L","O","V","E"
	DB	"R","S",$20,$00,$02,$02,$01,$00
	DB	$02,$70,$00,$D0,$02,$FD,$02,$00
	DB	$09,$00,$02,$00,$00,$00,$18,$FE
;
HIPL:	DB	$EB,$FE,$90,$20,"L","O","V","E"
	DB	"R","S",$20,$00,$04,$01,$01,$00
	DB	$02,$C0,$00,$D0,$04,$FE,$02,$00
	DB	$08,$00,$02,$00,$00,$00,$18,$FE
;
IPLPRG:
	LD	BC,$FFC
	DB	$ED,$71
	LD	A,7
	LD	($FF86),A
	JP	$F5
IPLPEND:
FDATA:
;
