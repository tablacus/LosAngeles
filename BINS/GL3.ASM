;
;	GL3 LOADER
;
	$INCLUDE	LA.DEF
;
DATA	EQU $5000
;
	ORG $C000
;
	LD	IX,(#IBFAD)
;
	PUSH	DE
	CALL	#MPRNT
	DB	"X68000's GL3 loader/saver,X1 GL3 v1.03",$0D
	DB	" (C) 1994 by Larn M.R.",$0D,0
	POP	DE
;
SW1:	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,MAIN
SWI:
	INC	DE
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	"D"
	JR	NZ,SW2
	LD	A,1
	LD	(MAGIC+1),A
	JR	SW1
SW2:	CP	"S"
	JR	NZ,USAGE
	JP	SAVE
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" GL3 [½²¯Á] <filename>",$0D,$0D
	DB	"½²¯Á:"
	DB	9,"/D  ÃÞ¨»Þ",$0D
	DB	9,"/S  ¾°ÌÞ",$0D
	DB	0
	AND	A
	RET
;
MAIN:	CALL	#FILE
	JR	C,USAGE
	LD	A,(IX+1)
	CP	$20
	JR	Z,USAGE
	CALL	#ROPEN
	JP	C,END
;
	CALL	SETCRT
	LD	HL,DATA
	LD	(#DTADR),HL
	LD	HL,$4000
	LD	(GRAM),HL
;
	LD	A,25	;25line
LOOP1:	PUSH	AF
	LD	B,8
	CALL	#RDDX
	JP	C,END2
	LD	IX,DATA
	LD	D,8	     ;Y=8dot
LOOP2:	LD	E,40	     ;40chr
LOOP3:	PUSH	DE
	LD	C,8	     ;X=8dot
LOOP4:	LD	H,(IX)
	INC	IX
	LD	L,(IX)
	INC	IX
;
MAGIC:	LD	A,2
	XOR	1
	LD	(MAGIC+1),A
	JP	NZ,MAGICE
	LD	A,H
	SUB	A,8
	JR	C,MAGIC1
	LD	H,A
MAGIC1:	RES	3,H
	LD	DE,-$40
	ADD	HL,DE
	BIT	3,H
	JR	Z,MAGIC2
	AND	A
	SBC	HL,DE
MAGIC2:	RES	6,L
	DEC	L
	DEC	L
	BIT	6,L
	JR	Z,MAGICE
	INC	L
	INC	L
;
MAGICE:	LD	IY,G3
	LD	B,12+2	     ;4x3+2
LOOP5:	ADD	HL,HL
	RL	(IY)
	DEC	IY
	DJNZ	LOOP5
	DEC	C
	JR	NZ,LOOP4
;
	LD	BC,$1FD0
	LD	A,(#WK1FD0)
	LD	E,A
	RES	4,E
	OUT	(C),E	    ;BANK0
	EXX
	LD	BC,(GRAM)
	LD	IY,B3
	CALL	PSET1
	SET	2,B
	LD	IY,B2
	CALL	PSET1
	EXX
	SET	4,E
	OUT	(C),E
	EXX
	LD	IY,B0
	CALL	PSET1
	RES	2,B
	LD	IY,B1
	CALL	PSET1
;
	LD	HL,(GRAM)
	INC	HL
	LD	(GRAM),HL
	POP	DE
	DEC	E
	JP	NZ,LOOP3
	LD	A,(MAGIC+1)
	XOR	1
	LD	(MAGIC+1),A
	LD	BC,$800-40
	ADD	HL,BC
	LD	(GRAM),HL
	LD	BC,1024-640
	ADD	IX,BC
	DEC	D
	JP	NZ,LOOP2
	LD	BC,$C000+40
	ADD	HL,BC
	LD	(GRAM),HL
	POP	AF
	DEC	A
	JP	NZ,LOOP1
	JR	END
;
END2:	POP	AF
;
END:	JP	#MOTOFF
;
;
PSET1:	RES	7,B
	LD	A,(IY)	     ;B
	OUT	(C),A
;
	SET	7,B
	RES	6,B
	LD	A,(IY+5)     ;R
	OUT	(C),A
;
	SET	6,B
	LD	A,(IY+10)    ;G
	OUT	(C),A
	RET
;
SAVE:	LD	HL,(#LVER)
	LD	BC,$0117
	AND	A
	SBC	HL,BC
	JR	NC,SAVEX
	CALL	#MPRNT
	DB	"Version ",0
	SCF
	RET
;
SAVEX:	XOR	A
	CALL	#FILE
	JP	C,USAGE
	LD	A,(IX+1)
	CP	$20
	JP	Z,USAGE
	LD	A,(#WILD)
	OR	A
	JP	NZ,USAGE
;
	LD	HL,$2000
	LD	(#SIZE),HL
	LD	HL,3
	LD	(#SIZEH),HL
	CALL	#WOPENH
	JP	C,END
	CALL	#DSKF
	LD	BC,200
	AND	A
	SBC	HL,BC
	JP	C,END
;
	LD	A,"?"
	CALL	SAVMES
	CALL	#KYBFC
	CALL	#FLGET
	CALL	#CAP
	CP	$0D
	JR	Z,OK
	CP	"Y"
	JR	Z,OK
	CP	"y"
	JR	Z,OK
	SCF
	RET
;
OK:	LD	A," "
	CALL	SAVMES
	CALL	#LTNL
;
	LD	HL,DATA
	LD	(#DTADR),HL
	LD	HL,$4000
	LD	(GRAM),HL
	LD	HL,DATA
	LD	BC,1024*8
	LD	E,0
;
SAVE0:	LD	(HL),E
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JP	NZ,SAVE0
;
	LD	A,25		;25line
SAVE1:	PUSH	AF
;
	LD	IX,DATA
	LD	D,8		;Y=8dot
SAVE2:	LD	E,40		;40chr
SAVE3:	PUSH	DE
;
	LD	BC,$1FD0
	LD	A,(#WK1FD0)
	LD	E,A
	RES	4,E		;BANK0
	OUT	(C),E
	EXX
	LD	BC,(GRAM)
	LD	IY,B3
	CALL	POINT
	SET	2,B
	LD	IY,B2
	CALL	POINT
	EXX
	SET	4,E
	OUT	(C),E		;BANK1
	EXX
	LD	IY,B0
	CALL	POINT
	RES	2,B
	LD	IY,B1
	CALL	POINT
;
	LD	C,8		;X=8dot
;
SAVE4:	LD	IY,G3
	LD	B,15
	LD	HL,0
;
SAVE5:	RL	(IY)
	ADC	HL,HL
	DEC	IY
	DJNZ	SAVE5
;
	ADD	HL,HL
	LD	(IX),H
	INC	IX
	LD	(IX),L
	INC	IX
;
	DEC	C
	JR	NZ,SAVE4
;
	LD	HL,(GRAM)
	INC	HL
	LD	(GRAM),HL
;
	POP	DE
	DEC	E
	JP	NZ,SAVE3
;
	LD	BC,$800-40
	ADD	HL,BC
	LD	(GRAM),HL
	LD	BC,1024-640
	ADD	IX,BC
	DEC	D
	JP	NZ,SAVE2
;
	LD	BC,$C000+40
	ADD	HL,BC
	LD	(GRAM),HL
;
	LD	A,"¥"
	CALL	#PRINT
	LD	B,8
	CALL	#WRDX
	JP	C,END2
	POP	AF
	DEC	A
	JP	NZ,SAVE1
;
	CALL	#WTFAT
	JP	C,#MOTOFF
	CALL	#WTDIRX
	JP	C,#MOTOFF
	CALL	#MPRNT
	DB	"Completed !",$0D,0
	JP	#MOTOFF
;
POINT:	RES	7,B
	IN	A,(C)	     ;B
	LD	(IY),A
;
	SET	7,B
	RES	6,B
	IN	A,(C)	     ;R
	LD	(IY+5),A
;
	SET	6,B
	IN	A,(C)	     ;G
	LD	(IY+10),A
	RET
;
SAVMES:	PUSH	AF
	CALL	#MPRNT
	DB	$0D,"SAVE",0
	POP	AF
	CALL	#PRINT
	CALL	#PRNTS
	LD	A,(#DSK)
	CALL	#PRINT
	LD	A,":"
	CALL	#PRINT
	JP	#FPRNT
;
SETCRT:	LD	BC,$1FB0
	LD	A,$80
	OUT	(C),A
;
	LD	HL,RESOL
	LD	DE,$00DD
	LD	BC,20
	LDIR
	LD	A,(HL)
	CALL	#S1FD0
	CALL	$09A1
	LD	A,40
	CALL	#WIDCH
	AND	A
	RET
;
RESOL:	DB	$37,$28,$2D,$34,$1F,$02,$19,$1C
	DB	$00,$07,$00,$00,$00,$00,$00,$00
	DB	$6F,$50,$59,$38,$00
;
;
;			WORKS
;
GRAM:	DS	2
;
BASE:	DB	0	; 1
B0:	DB	0	; 2
B1:	DB	0	; 3
B2:	DB	0	; 4
B3:	DB	0	; 5
	DB	0	; 6
R0:	DB	0	; 7
R1:	DB	0	; 8
R2:	DB	0	; 9
R3:	DB	0	;10
	DB	0	;11
G0:	DB	0	;12
G1:	DB	0	;13
G2:	DB	0	;14
G3:	DB	0	;15
;
