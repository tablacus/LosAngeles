;
;	LNDRV by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"symbolic link driver v1.00 Lovers"
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
MAIN:
	CALL	PRO
	RET	C
	LD	IY,(#KILL+1)
	LD	HL,#SOPEN
	LD	A,(IY)
	CP	$CD
	SCF
	RET	NZ
	LD	A,(IY+1)
	CP	L
	SCF
	RET	NZ
	LD	A,(IY+2)
	CP	H
	SCF
	RET	NZ
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	LD	BC,-65
	ADD	HL,BC
	LD	(WORK1+1),HL
	LD	BC,LAST-START
	AND	A
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	LD	HL,(#SOPEN+1)
	LD	(SOPEN+1),HL
	LD	IY,(#KILL+1)
	LD	(IY+1),L
	LD	(IY+2),H
	LD	HL,(#NOPEN+1)
	LD	(NOPEN+1),HL
;
	LD	HL,SOPEN-START
	ADD	HL,DE
	LD	(#SOPEN+1),HL
	LD	HL,NOPEN-START
	ADD	HL,DE
	LD	(#NOPEN+1),HL
;
	LD	HL,START
	LDIR
	AND	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Å²Ü!",7,$0D,0
	AND	A
	RET
;
REM2:	CALL	#MPRNT
	DB	'release',$0D,'MEMAX:',0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	EX	DE,HL
;
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(#SOPEN+1),DE
	INC	HL
	INC	HL
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	(#NOPEN+1),DE
	LD	IY,(#KILL+1)
	LD	HL,#SOPEN
	LD	(IY+1),L
	LD	(IY+2),H
	XOR	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" LNDRV    ¼Þ®³Á­³",$0D
	DB	" LNDRV -R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
;
;		 012345	 6 7
START:	DB	"[LNDRV",0,0
;
SOPEN:	CALL	#HOT
	JR	LNDRV
NOPEN:	CALL	#HOT
;
LNDRV:
	RET	C
	LD	BC,$000B
	ADD	HL,BC
	BIT	6,(HL)
	LD	HL,(#FBAD)
	RET	Z
	CALL	#BRKEY
	SCF
	RET	Z
;
	LD	BC,$001A
	ADD	HL,BC
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	A,(HL)		;#SIZE
	DEC	A
	AND	$3F
	INC	A
	LD	B,A
	LD	HL,(#DTBUF)
	PUSH	BC
	PUSH	HL
	CALL	#CLUST
	CALL	#DRDC
	POP	HL
	POP	BC
	JR	C,ERR
;
WORK1:	LD	DE,0
	PUSH	DE
LOOP:
	LD	A,(HL)
	INC	HL
	CP	"/"
	JR	NZ,SKIP
	LD	A,"\"
SKIP
	LD	(DE),A
	INC	DE
	DJNZ	LOOP
	XOR	A
	LD	(DE),A
;
	POP	DE
	CALL	#FILEX
	JP	NC,#SOPEN
ERR:
	XOR	A
	SCF
	RET
;
LAST:
