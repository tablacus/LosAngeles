;
;	9SCDRV
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"9 sector disk driver v1.02"
	DB	" Lovers"
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
MAIN:
	CALL	PRO
	RET	C
;
	LD	A,(#DTBUF+1)
	CP	$30
	JR	NC,DBEXOK
	CALL	#MPRNT
	DB	'"dbex"¦ ¼Þ®³Á­³ ¼Ã ¸ÀÞ»²',$07,$0D,0
	RET
DBEXOK:
	LD	HL,(#RDFAT+1)
	LD	(RDFATX+1),HL
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+8),HL
	AND	A
	LD	BC,LAST-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
;
	EX	DE,HL
	LD	HL,_RDFAT-START
	ADD	HL,DE
	LD	(#RDFAT+1),HL
;
	LD	HL,RDFAT-START
	ADD	HL,DE
	LD	(\RDFAT+1),HL
;
	LD	HL,DRDF-START
	ADD	HL,DE
	LD	(PATDF1+1),HL
	LD	(PATDF2+1),HL
;
	LD	HL,DRDD-START
	ADD	HL,DE
	LD	(PATDD1+1),HL
;
	LD	HL,INCDRD-START
	ADD	HL,DE
	LD	(PATID+1),HL
;
	LD	HL,DRDMO-START
	ADD	HL,DE
	LD	(PATDM+1),HL
;
	LD	HL,CHGTYPE-START
	ADD	HL,DE
	LD	(PATCH1+1),HL
	LD	(PATCH2+1),HL
;
	LD	HL,M2DD8-START
	ADD	HL,DE
	LD	(PAT2DD8+1),HL
;
	LD	HL,M2DD-START
	ADD	HL,DE
	LD	(PAT2DD+1),HL
;
	LD	HL,M2HDE-START
	ADD	HL,DE
	LD	(PAT2HDE+1),HL
;
	LD	HL,M2HS-START
	ADD	HL,DE
	LD	(PAT2HS+1),HL
;
	LD	HL,CHECK-START
	ADD	HL,DE
	LD	(PATCK1+1),HL
	LD	(PATCK2+1),HL
	LD	(PATCK3+1),HL
;
	LD	HL,(#DRDWA)
	LD	(M2DD+2),HL
	LD	(M2DD8+2),HL
	LD	HL,(#DRDA)
	LD	(M2HDE+2),HL
	LD	(M2HS+2),HL
;
	LD	HL,(#DWTWA)
	LD	(M2DD+4),HL
	LD	(M2DD8+4),HL
	LD	HL,(#DWTA)
	LD	(M2HDE+4),HL
	LD	(M2HS+4),HL
;
	LD	HL,START
	LDIR
	AND	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,8
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Ï¾Ý!",7,$0D,0
	AND	A
	RET
;
REM2:	CALL	#MPRNT
	DB	"release",$0D,"MEMAX:",0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	INC	DE
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	INC	DE
	LD	H,A
	LD	(#RDFAT+1),HL
;
	LD	A,(#MAXDRV)
	LD	B,A
	LD	C,"A"
FDDI1:
	LD	A,C
	CALL	#DEVAD2
	JR	C,FDDI2
	LD	A,(IY+!DEVNO)
	CP	$87
	JR	NZ,FDDI2
	PUSH	BC
	PUSH	IY
	POP	DE
	LD	HL,(#M2D)
	LD	BC,18
	LDIR
	POP	BC
FDDI2:
	INC	C
	DJNZ	FDDI1
	AND	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+2
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" 9SCDRV    ¼Þ®³Á­³",$0D
	DB	" 9SCDRV /R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
;
;		 0  1  234567  8 9
START:	DB	"[",6,"9SCDRV",0,0
;
RDFATX:	JP	0
;
RDFAT:
	CALL	#DEVADR
	RET	C
	LD	A,(IY+!DEVNO)
	CP	$87
	JR	NZ,RDFATX
;
	LD	A,$7F
PATCK1:	LD	(CHECK),A	;A=$87
	LD	A,(IY+!FDMODE)
	INC	A
	JR	NZ,X2HX
E2DX:
	LD	HL,(#M2D)
PATDF1:	CALL	DRDF
	JR	C,E2HX
	CP	$FD		;2D
	RET	Z
	CP	$F9
	JR	Z,E2DD9
	CP	$FB
	SCF
	RET	NZ		;NOT 2DD
PAT2DD8:LD	HL,M2DD8
CHGTYPE:
	PUSH	DE
	PUSH	IY
	POP	DE
	LD	BC,18
	LDIR
	LD	A,(#DSK)
	CALL	#CHGDRV
	LD	A,(#DRIVE)
	AND	3
	LD	L,A
	LD	H,$1F
	LD	A,$FF
	LD	(HL),A
	POP	DE
	RET
;
E2HX:
	INC	A
	RET	Z
PATCK2:	LD	HL,CHECK
	RLC	(HL)
	RET	C
X2HX:
	LD	HL,(#M2HD)
	LD	DE,0
PATDD1:	CALL	DRDD
	JR	NC,X2HX1
	INC	A
	RET	Z
PATCK3:	LD	HL,CHECK
	RLC	(HL)
	RET	C
	JR	E2DX
X2HX1:
	INC	HL
	LD	A,(HL)
	LD	B,$FE
	CP	$0C
	JR	Z,X2HDE
	LD	B,$F8
	SUB	$20		;$20
	JR	Z,X2HDE
	INC	A		;$1F
	JR	Z,X2HDE
	INC	A		;$1E
	JR	NZ,RDFATX	;2HD
X2HS:
PAT2HS:	LD	HL,M2HS
X2HDEX:
PATDF2:	CALL	DRDF
	RET	C
	CP	(IY+!FATID)
	SCF
	RET	NZ
PATID:	CALL	INCDRD
	JR	NC,DRDMO	
X2HDE:
PAT2HDE:LD	HL,M2HDE
	INC	HL
	LD	(HL),B
	DEC	HL
	JR	X2HDEX
;
E2DD9:
PAT2DD:	LD	HL,M2DD
PATCH1:	CALL	CHGTYPE
	LD	HL,(#FATBF)
INCDRD:
	INC	H
	INC	H
	INC	H
	INC	H
DRDMO:
	CALL	#DRDC
	PUSH	HL
	CALL	#MOTOFF
	POP	HL
	RET
DRDF:
	LD	DE,(#FATPS)
DRDD:
PATCH2:	CALL	CHGTYPE
	LD	HL,(#FATBF)
	PUSH	HL
PATDM:	CALL	DRDMO
	POP	HL
	RET	C
	LD	A,(HL)
	RET
;
_RDFAT:
	PUSH	IY
\RDFAT:	CALL	#HOT
	POP	IY
	RET
;
M2DD8:	DB	1,$FB
	DW	#HOT,#HOT,$2908,636
	DB	$FF,12,80,8,1,2,5,1	

M2DD:	DB	2,$F9
	DW	#HOT,#HOT,$290A,715
	DB	$FF,14,80,9,1,2,7,1
;
M2HDE:	DB	3,$FE
	DW	#HOT,#HOT,11,1429
	DB	$FE,13,80,9,1,4,7,1
;
M2HS:	DB	3,$FB
	DW	#HOT,#HOT,8,1432
	DB	$FE,10,80,9,1,4,4,10
;
CHECK:	DB	0
;
LAST:
