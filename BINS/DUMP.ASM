;
;	DUMP by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	OFFSET	$C000-$3000
	ORG	$3000
;
START:
	LD	A,$1A
	CALL	#PRINT
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:
	JP	USAGE
MAIN:
	LD	(DEBUFFA),DE
;
	CALL	#FILE
	JP	C,USAGE
	LD	IX,(#IBFAD)
	LD	A,(IX+1)
	CP	$20
	JP	Z,USAGE
;
	CALL	INIT
;
	CALL	#ROPEN
;
TYPE1:	JR	C,TYPEBK
;
	LD	A,(IX+1+$0B)
	AND	$1C
	JR	NZ,TYPE5
;
	LD	A,(#WILD)
	OR	A
	JR	Z,DUMP0
	CALL	#NL
	CALL	#FPRNT		;Ì§²ÙÒ²ÉË®³¼Þ
	CALL	#LTNL
DUMP0:
	LD	DE,0
	LD	HL,0
DUMP1:
	LD	IY,TEXT
	EX	DE,HL
	CALL	#PRTHL
	EX	DE,HL
	CALL	#PRTHL
	CALL	#PRNTS
	LD	BC,16*256
TYPE3:
	CALL	#FGETC		;1Ó¼ÞÃÞÊÞ²½¶×Æ­³Ø®¸
	JR	C,TYPE4
	CALL	#PRNTS
	LD	(IY),A
	INC	IY
	INC	C
	CALL	#PRTHX
	INC	HL
	LD	A,H
	OR	L
	JR	NZ,DUMP2
	INC	DE
DUMP2:
	DJNZ	TYPE3
	CALL	PRTTXT
	CALL	#PAUSE
	DW	TYPEBK
	CALL	#LTNL
	JR	DUMP1
;
TYPE4:
	PUSH	AF
	CALL	PRTTXT
	POP	AF
	JR	Z,TYPEBK2	;Z=1 ... ERROR
;
TYPE5:	LD	A,(#WILD)
	OR	A
	JR	Z,TYPEBK
	LD	DE,(DEBUFFA)
	CALL	#FILE
	JR	C,TYPEBK2
	CALL	#NROPEN
	JR	TYPE1
;
TYPEBK:
	LD	A,$0D
PATCH2:	CALL	#PRINT
TYPEBK2:
	CALL	#KYBFC
	LD	A,(#FILEN)
	OR	A
	CALL	Z,#ERROR
;
!GCBUF:	LD	HL,0
	LD	(#GCBUF),HL
!GCEH:	LD	A,$00
	LD	(#GCEH),A
!CLLN:	LD	A,$00
	LD	(#CLLN),A
;
	AND	A
	RET
;
PRTTXT:
	LD	A,C
	OR	A
	RET	Z
	LD	A,16
	SUB	C
	LD	B,A	
	ADD	A,A
	ADD	A,B
	ADD	A,2
PTS:
	CALL	#PRNTS
	DEC	A
	JR	NZ,PTS
;
	LD	IY,TEXT
PT1:
	LD	A,(IY)
	INC	IY
	CALL	#PRNT0
	DEC	C
	JR	NZ,PT1
	JP	#PRNTS
;
USAGE:	CALL	#MPRNT
	DB	"dump v1.00"
	DB	" Lovers",$0D
	DB	"usage:",$0D
	DB	" DUMP <filename>",$0D
	DB	0
	AND	A
	RET
;
INIT:	LD	HL,(#GCBUF)
	LD	(!GCBUF+1),HL
	LD	A,(#GCEH)
	LD	(!GCEH+1),A
	LD	A,(#CLLN)
	LD	(!CLLN+1),A
;
	LD	HL,START+$400
	LD	A,(#MEMAX+1)
	SUB	H
	CCF
	RET	NC
	SRL	A
	SRL	A
	LD	(#GCBUF),HL
	LD	(#CLLN),A
	ADD	A,A
	ADD	A,A
	ADD	A,H
	DEC	A
	LD	(#GCEH),A
	AND	A
	RET
;
DEBUFFA:DW	0
TEXT:
;
