;
;	SCEX	by Larn M.R.
;
	$INCLUDE	LA.DEF
;
IOCSV	EQU	$0052
;
BIOSV	EQU	$F845
;
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"X1 scex v1.02 Lovers"
	DB	$0D,0
	POP	DE
;
	CALL	#VER		;MACHINE CHECK
	LD	A,H
	SUB	$20
	JR	Z,X1
	DEC	A
	JP	NZ,MERROR
X1:
	RRC	L
	JP	NC,MERROR
;
	LD	A,I
	CP	$F8
	JR	NZ,SV1
;
	LD	A,$1D		;ROM
	LD	(BANK+1),A
	LD	HL,$FAED
	LD	(KEY+1),HL
	INC	HL
	LD	(KEY2+1),HL
	LD	HL,BIOSV
	JR	SV2
SV1:
	OR	A
	JP	NZ,MERROR
	LD	HL,IOCSV
SV2:
	LD	(VECT1+1),HL
	LD	(VECT2+1),HL
	LD	(VECT3+1),HL
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
SW2:
	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
MAIN:
	CALL	PRO
	RET	C
;
VECT1:	LD	HL,(IOCSV)		;KEY VECT.
	LD	(KEYVEC+1),HL
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	AND	A
	LD	BC,LAST-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
;
	EX	DE,HL
	PUSH	DE
	LD	HL,START
	LDIR
	POP	DE
	LD	HL,SCEX-START
	ADD	HL,DE
VECT2:	LD	(IOCSV),HL	;SET KEY VECT.
;
	LD	A,(#WK1FD0)
	OR	$80
	CALL	#S1FD0
	LD	HL,$CCAA
	LD	($00F6),HL
	LD	HL,$00F0
	LD	($00F8),HL
	CALL	$0A5A
	AND	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Ï¾Ý!",7,$0D,0
	AND	A
	RET
;
REM2:	CALL	#MPRNT
	DB	"release",$0D,"MEMAX:",0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	INC	DE
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	INC	DE
	LD	H,A
VECT3:	LD	(IOCSV),HL
;
	LD	A,(#WK1FD0)
	AND	$7F
	CALL	#S1FD0
	LD	HL,0
	LD	($00F6),HL
	LD	($00F8),HL
	CALL	$0A5A
	AND	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" SCEX    ¼Þ®³Á­³",$0D
	DB	" SCEX /R  ¶²¼Þ®",$0D
	DB	$0D
	DB	"[SHIFT]+[GRAPH]",9,9,"graphic on/off",$0D
	DB	"[CTRL]+[GRAPH]",9,9,"graphic 200/400",$0D
	DB	"[CTRL]+[GRAPH]+[HOME]",9,"software reset",$0D
	DB	0
	AND	A
	RET
;
MERROR:
	CALL	#MPRNT
	DB	"X1turbo¼Ø°½Þ ¾ÝÖ³ ÅÉ",$07,$0D,0
	RET
;
;	¼Þ®³Á­³ÌÞ
;
;		 012345  6 7
START:	DB	"[SCEX ",0,0
;
SCEX:
KEYVEC:	CALL	0
;
	PUSH	AF
	DI
KEY2:	LD	A,($002F)
	BIT	4,A		;[GRAPH]
	JR	NZ,SCEXE
	RRCA			;[CTRL]
	JR	NC,SCL
	RRCA			;[SHIFT]
	JR	NC,SCG
;
SCEXE:
	POP	AF
	EI
	RETI
;
SCL:
KEY:	LD	A,($002E)
	CP	$0B
	JR	Z,RESET
;
	PUSH	BC
	LD	B,$02
	JR	SCL1
;
SCG:
	PUSH	BC
	LD	B,$80
SCL1:
	LD	A,$1E		;RAM
	OUT	(0),A
	LD	A,(#WK1FD0)
	XOR	B
	POP	BC
	CALL	#S1FD0
BANK:	LD	A,$1E
	OUT	(0),A
	JR	SCEXE
;
RESET:
	LD	A,$1D
	OUT	(0),A
	RST	0
LAST:
