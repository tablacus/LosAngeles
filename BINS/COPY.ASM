;
;	COPY	by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	LD	BC,(#LVER)	;ÊÞ°¼Þ®ÝÁª¯¸
	LD	L,B
	LD	H,C
	LD	BC,$0136
	AND	A
	SBC	HL,BC
	JR	NC,VER
	LD	HL,#NOPEN+1
	LD	(NOPEN+1),HL
VER:
	LD	A,(#MEMAX+1)
	LD	HL,(#MEMAX1)
	CP	H
	JR	C,MEMAX
	LD	A,H
MEMAX:
	SUB	DATA/256
	RET	C
	SRL	A
	SRL	A
	DEC	A
	LD	(LENGTH),A
;
	LD	HL,(#FATBF)
	LD	(FATBF1),HL
;
SW1:
	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,SWEND
SWI:
	INC	DE
SWX:
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	"Q"
	JR	NZ,SW2
	LD	(SW_Q+1),A
	JR	SWE
SW2:
	CP	"V"
	JR	NZ,SW3
	LD	(SW_V+1),A
	JR	SWE
SW3:
	CP	"T"
	JR	NZ,SW5
SW4:
	LD	(SW_X+1),A
	JR	SWE
SW5:
	CP	"K"
	JR	Z,SW4
	CP	"N"
	JR	Z,SW4
	JP	MES
SWE:
	LD	A,(DE)
	CP	$21
	JR	C,SW1
	JR	SWX
;
SWEND:
	CP	$20
	JP	C,MES
	XOR	A
	CALL	FILE
	JP	C,END2
	PUSH	DE
	CALL	FSET
	LDIR
	POP	DE
	LD	A,(#WILD)
	LD	(WILD1),A
	OR	A
	JR	Z,NONC
	LD	A,$1A
	CALL	#PRINT
NONC:
	LD	A,(DE)
	CP	$21
	JR	C,NAME1
	CP	":"
	JR	Z,NAME1
	CP	","
	JP	NZ,MES
;
	INC	DE
NAME1:
	LD	(NAME+1),DE
;
	LD	HL,(#DIRBF)
	PUSH	HL
	LD	HL,DIRBF1
	LD	(#DIRBF),HL
	CALL	#ROPEN
	POP	HL
	LD	(#DIRBF),HL
	JP	C,END2
COPY0:
	CALL	PAUSE
	LD	HL,(#IBFAD)
	INC	HL
	LD	DE,FCB
	LD	BC,32
	LDIR
	LD	HL,#FBPS
	LD	DE,BUFFA
	LD	BC,8
	LDIR
;
	LD	H,$1E
	LD	A,(WILD1)
	OR	A
	JR	NZ,SYSHID
	LD	H,$18
SYSHID:
	LD	IX,(#IBFAD)
	LD	A,(IX+1+$0B)
	LD	(ATTR),A
	AND	H
	JP	NZ,SKIP
;
	LD	HL,(#CLPS)
	LD	(CLPS1),HL
	LD	A,(#DSK)
	LD	(DSK1),A
;
	LD	HL,FATBF2
	LD	(#FATBF),HL
NAME:	LD	DE,0
	LD	IX,(#IBFAD)
	LD	A,(IX+1+$10)
	SUB	$A0
	SUB	$01
	SBC	A,A	;$A0->$FF not->$00
	CALL	FILE
	JP	C,END2
;
	CALL	#RDFAT
	JP	C,END2
;
	LD	A,$C9
	LD	(#RDFAT),A
;
	LD	HL,(#IBFAD)
	INC	HL
	LD	DE,FCB
	LD	B,8+3		;filename+ext
	CALL	SAVE
	INC	HL
	INC	DE
	LD	B,10		;filename+10(fnex)
	CALL	SAVE
SAVE2:
	CALL	#DEVADR
	JP	C,END2
	PUSH	IY
	POP	IX
	LD	A,(DSK1)
	CALL	#DEVADX
;
	LD	A,(IX+$12)	;DEVNO
	CP	(IY+$12)
	JR	NZ,WOPEN
;
	LD	A,(IX+$13)	;UNITNO
	CP	(IY+$13)
	JR	NZ,WOPEN
;
	CALL	#SOPEN
	JR	C,SAME1
	LD	DE,$1A
	ADD	HL,DE
	LD	DE,(CLPS1)
	LD	A,E
	CP	(HL)
	INC	HL
	JR	NZ,WOPEN
	LD	A,D
	CP	(HL)
SAME1:
	SCF
	JP	Z,END2
;
WOPEN:
	CALL	PAUSE
;
SW_X:	LD	A,$00
	CP	"K"
	JR	NZ,WOPENT
;
	CALL	#SOPEN
	JR	C,WOPENN1
	CALL	#FPRNT
	CALL	#PRNTS
	JR	WOPENK
;
WOPENT:
	CP	"T"
	JR	NZ,WOPENN
	CALL	#SOPEN
	JR	C,WOPENN1
	CALL	TIME
	JP	NC,SKIP
	JR	WOPENQ
;
WOPENN1:
	JP	Z,END2
	JR	WOPENQ
WOPENN2:
	JP	Z,END2
	JP	SKIP
WOPENN:
	CP	"N"
	JR	NZ,WOPENQ
	CALL	#SOPEN
	JR	C,WOPENN2
	CALL	TIME
	JP	NC,SKIP
WOPENQ:
	LD	A,(WILD1)
SW_Q:	LD	L,$00
	OR	L
	JR	Z,SINGLE
	CALL	#FPRNT
	CALL	#PRNTS
	LD	A,(SW_Q+1)
	OR	A
	JR	Z,SINGLE
WOPENK:
	CALL	END4
	CALL	#MPRNT
	DB	"?<y/n>",0
	CALL	SURE
	JP	C,SKIP1
	CALL	#PRNTS
SINGLE:
	LD	HL,(#DIRBF)
	PUSH	HL
	LD	HL,DIRBF2
	LD	(#DIRBF),HL
	LD	A,$C9
	LD	(#SETTMS),A
	CALL	#WOPENX
	LD	A,$C3
	LD	(#SETTMS),A
	POP	HL
	LD	(#DIRBF),HL
	JP	C,END2
;
	LD	A,(#DSK)
	LD	(DSK2),A
	LD	HL,0
	LD	(#SIZE),HL
	LD	(CLPS2),HL
;
	LD	A,(DSK1)
	CALL	#CHGDRV
	LD	HL,(FATBF1)
	LD	(#FATBF),HL
	LD	DE,(CLPS1)
	LD	HL,0
WOPEN2:
	INC	HL
	PUSH	HL
	CALL	#GNCL
	CALL	#ENDCL
	POP	HL
	JR	C,WOPEN2
;
	LD	A,H
	OR	L
	SCF
	JP	Z,END2
;
	PUSH	HL
	LD	A,(DSK2)
	CALL	#CHGDRV
	LD	HL,FATBF2
	LD	(#FATBF),HL
	POP	HL
	LD	B,L
	INC	H
WOPEN3:
	PUSH	HL
	LD	HL,(CLPS2)
	LD	(#CLPS),HL
	CALL	#WRDF
	LD	HL,(#CLPS)
	LD	(CLPS2),HL
	POP	HL
	JP	C,END2
	LD	B,0
	DEC	H
	JR	NZ,WOPEN3
WOPEN4:
	LD	IX,(#IBFAD)
	LD	L,(IX+1+$1A)
	LD	H,(IX+2+$1A)
	LD	(CLPS2),HL
	LD	HL,DATA
	LD	(#DTADR),HL
COPY2:
	LD	A,(DSK1)
	CALL	#CHGDRV
	LD	HL,(FATBF1)
	LD	(#FATBF),HL
	LD	HL,(CLPS1)
	LD	(#CLPS),HL
	LD	A,(LENGTH)
	LD	B,A
	CALL	#RDDX
	JP	C,END2
	JP	NZ,END
	LD	HL,(#CLPS)
	LD	(CLPS1),HL
;
	LD	A,(LENGTH)
	LD	B,A
	CALL	WRITE
	JP	NC,COPY2
	JP	END2
WRITE:
	PUSH	BC
	LD	A,(DSK2)
	CALL	#CHGDRV
	LD	HL,FATBF2
	LD	(#FATBF),HL
	LD	DE,(CLPS2)
	CALL	#WRDD
	POP	BC
	RET	C
	LD	DE,(CLPS2)
	LD	HL,(#CLPS)
	LD	(CLPS2),HL
SW_V:	LD	A,$00
	OR	A
	RET	Z
	JP	#RDDX
;
END:
	LD	A,(LENGTH)
	INC	A
	SUB	B
	LD	B,A
	CALL	WRITE
	JP	C,END2
;
	LD	A,(DSK2)
	CALL	#CHGDRV
	LD	HL,FATBF2
	LD	(#FATBF),HL
	CALL	#WTFAT
	JP	C,END2
	LD	IX,(#IBFAD)
	LD	A,(ATTR)
	LD	(IX+1+$0B),A
	LD	HL,(#DIRBF)
	PUSH	HL
	LD	HL,DIRBF2
	LD	(#DIRBF),HL
	CALL	#WTDIRX
	POP	HL
	LD	(#DIRBF),HL
	JP	C,END2
;
	CALL	#MPRNT
	DB	"Ok",$0D,0
	CALL	PAUSE
END3:
	LD	A,(WILD1)
	OR	A
	JP	Z,END2
	LD	A,(DSK1)
	CALL	#CHGDRV
	LD	HL,(FATBF1)
	LD	(#FATBF),HL
	CALL	FSET
	EX	DE,HL
	LDIR
	LD	HL,BUFFA
	LD	DE,#FBPS
	LD	BC,8
	LDIR
	LD	HL,(#DIRBF)
	PUSH	HL
	LD	HL,DIRBF1
	LD	(#DIRBF),HL
	LD	HL,(#NOPEN+1)
	PUSH	HL
NOPEN:	LD	HL,(#NOPENX+1)
	LD	(#NOPEN+1),HL
	CALL	#NROPEN
	POP	HL
	LD	(#NOPEN+1),HL
	POP	HL
	LD	(#DIRBF),HL
	JP	NC,COPY0
	JR	Z,END2
	XOR	A
END2:
	LD	HL,(FATBF1)
	LD	(#FATBF),HL
END4:
	LD	A,$C3
	LD	(#RDFAT),A
	JP	#MOTOFF
;
SKIP:
	CALL	#FPRNT
SKIP1:
	CALL	#MPRNT
	DB	" Skip",$0D,0
	JP	END3
;
FSET:
	LD	HL,(#IBFAD)
	INC	HL
	LD	DE,FNAME
	LD	BC,22
	RET
;
FILE:
	CALL	#FILE
	RET	C
	LD	IX,(#IBFAD)
	LD	A,(IX+1)
	CP	$20
	CALL	Z,FILEW
	LD	A,(#WILD)
	OR	A
	RET	NZ
	PUSH	DE
	CALL	#SOPEN
	POP	DE
	JR	NC,FILE1
	RET	Z
	XOR	A
	RET
FILE1:
	PUSH	HL
	POP	IX
	BIT	4,(IX+$0B)
	RET	Z
	LD	L,(IX+$1A)
	LD	H,(IX+$1B)
	LD	(#NDIRP),HL
FILEW:
	PUSH	DE
	LD	DE,DIRW
	CALL	#FILED
	POP	DE
	RET
FILE2:
	POP	DE
	RET
;
SAVE:
	LD	A,(HL)
	CP	"?"
	JR	NZ,SAVE1
	LD	A,(DE)
	LD	(HL),A
SAVE1:
	INC	HL
	INC	DE
	DJNZ	SAVE
	RET
;
SURE:			;Yes CF=0 , No CF=1
	CALL	#KYBFC
	CALL	#FLGET
	CALL	#CAP
	CP	"A"
	JR	Z,ALL
	CP	"Á"
	JR	Z,ALL
	CP	"Y"
	JR	Z,YES
	CP	"Ý"
	JR	Z,YES
	CP	"N"
	JR	Z,NO
	CP	"Ð"
	JR	Z,NO
	CP	3
	JR	Z,BREAK
	CP	$1B
	JR	NZ,SURE
BREAK:
	CALL	END2
	CALL	#NL
	JP	#HOT
ALL:
	XOR	A
	LD	(SW_Q+1),A
	LD	A,(SW_X)
	CP	"K"
	JR	NZ,YES
	XOR	A
	LD	(SW_X+1),A
YES:
	XOR	A
	LD	A,"y"
PRTYN:
	JP	#PRINT
NO:
	LD	A,"n"
	SCF
	JR	PRTYN
;
TIME:
	LD	IX,(#IBFAD)
	PUSH	HL
	POP	IY
	LD	E,(IX+1+$18)
	LD	D,(IX+1+$19)
	LD	L,(IY+$18)
	LD	H,(IY+$19)
	AND	A
	SBC	HL,DE
	RET	NZ
	LD	E,(IX+1+$16)
	LD	D,(IX+1+$17)
	LD	L,(IY+$16)
	LD	H,(IY+$17)
	AND	A
	SBC	HL,DE
	RET
;
PAUSE:
	CALL	#BRKEY
	JR	Z,BREAK
	CALL	#GETKY
	CP	$20
	RET	NZ
	LD	(SW_Q+1),A
	RET
;
MES:
	CALL	#MPRNT
	DB	$1A,"copy v1.23"
	DB	" Lovers",$0D
	DB	"usage:",$0D
	DB	" COPY [½²¯Á] Ì§²ÙÈ°Ñ1[ Ì§²ÙÈ°Ñ2]",$0D
	DB	$0D
	DB	9,"/K Áª¯¸",$0D
	DB	9,"/N ±¯ÌßÃÞ°Ä",$0D
	DB	9,"/Q ¶¸ÆÝ",$0D
	DB	9,"/T Æ­°Ì§²Ù",$0D
	DB	9,"/V ÍÞØÌ§²",$0D
	DB	0
	JP	END2
;
DIRW:	DB	"*.*",0
DSK1:	DB	0
DSK2:	DB	0
CLPS1:	DW	0
CLPS2:	DW	0
FATBF1:	DW	0
ATTR:	DB	0
WILD1:	DB	0
LENGTH:	DB	32
;
FCB:				;32
FNAME	EQU	FCB+32		;22
BUFFA	EQU	FNAME+22	;8
FATBF2	EQU	BUFFA+8		;$0C00
DIRBF1	EQU	FATBF2+$0C00	;$0400
DIRBF2	EQU	DIRBF1+$0400	;$0400
DATA	EQU	DIRBF2+$0400
