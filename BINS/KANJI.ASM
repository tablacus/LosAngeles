;
;	KANJI
;
	$INCLUDE	LA.DEF
;
_GETL	EQU	$0003
_PRINT	EQU	$0013
;
FKEY	EQU	$0F42
FKEYT	EQU	$F800-160
EXTSP	EQU	$FEFE
;
@IPL	EQU	FKEY
@NMI	EQU	FKEY+2
@RST	EQU	FKEY+4
;
@VER	EQU	FKEY+6
@PRINT	EQU	FKEY+8
@PRNT0	EQU	FKEY+10
@NL	EQU	FKEY+12
@TAB	EQU	FKEY+14
@LPRNT	EQU	FKEY+18
@GETL	EQU	FKEY+20
@GETKY	EQU	FKEY+22
@CSR	EQU	FKEY+24
@LOC	EQU	FKEY+26
@FLGET	EQU	FKEY+28
@MON	EQU	FKEY+30
@WIDCH	EQU	FKEY+32
@KYBFS	EQU	FKEY+34
@KYBFC	EQU	FKEY+36
@COLOR	EQU	FKEY+38
@MAXLN	EQU	FKEY+40
@XYADR	EQU	FKEY+41
@WORKS	EQU	FKEY+43
;
	ORG	$8000
;
	PUSH	DE
	LD	DE,TITLE
	CALL	#MSX
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
MAIN:
	CALL	#VER
	LD	A,H
	CP	$20
	SCF
	CCF
	RET	NZ
	RRC	L
	RET	NC
;
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	INC	H
	JP	NZ,ERROR
;
	DI
	LD	A,$1D
	OUT	(0),A
	CALL	$1085	;BIOSIN
	LD	A,$1E
	OUT	(0),A
	CALL	$09A1
;
	DI
	LD	HL,FKEY		;Function Key
	LD	DE,FKEYT
	LD	($FABA),DE
	LD	BC,160
	LDIR
;
	LD	HL,($0EA6)
	LD	($FAFE),HL
;
	LD	HL,$0EA8
	LD	DE,$FB00
	LD	BC,$0040
	LDIR
;
	LD	A,1
	LD	($FAEC),A	;CLICK OFF
	LD	($FABC),A
;
	LD	HL,(#WIDCH+1)
	LD	(\WIDCH+1),HL
	LD	(@WIDCH),HL
;
	LD	HL,FKEYT
	AND	A
	LD	BC,FIN-START
	SBC	HL,BC
	LD	(#MEMAX),HL
;
	EX	DE,HL
;
	LD	HL,($0001)
	LD	(@IPL),HL
	LD	HL,($0067)
	LD	(@NMI),HL
	LD	HL,\INIT-START
	ADD	HL,DE
	LD	($0001),HL
	LD	($0067),HL
;
	LD	HL,($0019)
	LD	(@RST),HL
	LD	HL,\RST-START
	ADD	HL,DE
	LD	($0019),HL
	LD	HL,RS1-START
	ADD	HL,DE
	LD	(RS1PAT+1),HL
	LD	HL,RSTSP-START+1
	ADD	HL,DE
	LD	(RSS1+2),HL
	INC	HL
	LD	(RSS2+1),HL
;
	LD	A,$C3
	LD	($F847),A
	LD	HL,\INT-START
	ADD	HL,DE
	LD	($F848),HL
;
	LD	HL,INTSP-START+1
	ADD	HL,DE
	LD	(INTS1+2),HL
;
	LD	HL,(#VER+1)
	LD	(@VER),HL
	LD	HL,\VER-START
	ADD	HL,DE
	LD	(#VER+1),HL
;
	LD	HL,(_PRINT+1)
	LD	(@PRINT),HL
	LD	HL,\PRINT-START
	ADD	HL,DE
	LD	(_PRINT+1),HL
;
	LD	HL,(#PRNT0+1)
	LD	(@PRNT0),HL
	LD	HL,\PRNT0-START
	ADD	HL,DE
	LD	(#PRNT0+1),HL
;
	LD	HL,(#NL+1)
	LD	(@NL),HL
	LD	HL,\NL-START
	ADD	HL,DE
	LD	(#NL+1),HL
;
	LD	HL,(#TAB+1)
	LD	(@TAB),HL
	LD	HL,\TAB-START
	ADD	HL,DE
	LD	(#TAB+1),HL
;
	LD	HL,(#LPRNT+1)
	LD	(@LPRNT),HL
	LD	HL,\LPRNT-START
	ADD	HL,DE
	LD	(#LPRNT+1),HL
	LD	HL,SPPAT+1-START
	ADD	HL,DE
	LD	(SPWPAT+2),HL
	LD	HL,\LPERR-START
	ADD	HL,DE
	LD	(LPPAT+1),HL
;
	LD	HL,(_GETL+1)
	LD	(@GETL),HL
	LD	HL,\GETL-START
	ADD	HL,DE
	LD	(_GETL+1),HL
;
	LD	HL,(#GETKY+1)
	LD	(@GETKY),HL
	LD	HL,\GETKY-START
	ADD	HL,DE
	LD	(#GETKY+1),HL
;
	LD	HL,(#CSR+1)
	LD	(@CSR),HL
	LD	HL,\CSR-START
	ADD	HL,DE
	LD	(#CSR+1),HL
;
	LD	HL,(#LOC+1)
	LD	(@LOC),HL
	LD	HL,\LOC-START
	ADD	HL,DE
	LD	(#LOC+1),HL
;
	LD	HL,(#FLGET+1)
	LD	(@FLGET),HL
	LD	HL,\FLGET-START
	ADD	HL,DE
	LD	(#FLGET+1),HL
;
	LD	HL,(#MON+1)
	LD	(@MON),HL
	LD	HL,\MON-START
	ADD	HL,DE
	LD	(#MON+1),HL
;
	LD	HL,\WIDCH-START
	ADD	HL,DE
	LD	(#WIDCH+1),HL
;
	LD	HL,(#KYBFS+1)
	LD	(@KYBFS),HL
	LD	HL,\KYBFS-START
	ADD	HL,DE
	LD	(#KYBFS+1),HL
;
	LD	HL,(#KYBFC+1)
	LD	(@KYBFC),HL
	LD	HL,\KYBFC-START
	ADD	HL,DE
	LD	(#KYBFC+1),HL
;
	LD	HL,(#COLOR+1)
	LD	(@COLOR),HL
	LD	HL,\COLOR-START
	ADD	HL,DE
	LD	(#COLOR+1),HL
;
	LD	HL,START
	LDIR
;
;	WORK
;
	LD	A,(#WIDTH)
	CALL	#WIDCH
	LD	A,(#WK1FD0)
	CALL	#S1FD0
	LD	A,(#MAXLN)
	LD	(@MAXLN),A
	LD	A,24
	LD	(#MAXLN),A
;
	LD	HL,(#XYADR)
	LD	(@XYADR),HL
	LD	HL,$FADF
	LD	(#XYADR),HL
;
	LD	HL,WORKS
	LD	DE,#CONYS	;-#COLORF
	LD	BC,10
	LDIR
;
	LD	DE,TITLE
	CALL	#MSX
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	CALL	#PRTHL
	CALL	#LTNL
	AND	A
	RET
;
WORKS:	DW	$FAE3,$FAE4,$FAE5,$FAE6,$F8D0
;
TITLE:	DB	$1A,"X1 kanji v1.11"
	DB	" Lovers"
	DB	$0D,0
;
REMOVE:
	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Å²Ü!",7,$0D,0
	RET
;
ERROR:	CALL	#MPRNT
	DB	"¼Þ®³Á­³ÃÞ·Å²Ü!",7,$0D,0
	RET
;
REM2:
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	HL,$FF00
	AND	A
	SBC	HL,DE
	SCF
	RET	NZ
	EX	DE,HL
	LD	(#MEMAX),HL
;
	DI
	LD	HL,(@IPL)
	LD	($0001),HL
	LD	HL,(@NMI)
	LD	($0067),HL
	LD	HL,(@RST)
	LD	($0019),HL
;
	LD	HL,(@VER)
	LD	(#VER+1),HL
	LD	HL,(@PRINT)
	LD	(_PRINT+1),HL
	LD	HL,(@PRNT0)
	LD	(#PRNT0+1),HL
	LD	HL,(@NL)
	LD	(#NL+1),HL
	LD	HL,(@TAB)
	LD	(#TAB+1),HL
	LD	HL,(@LPRNT)
	LD	(#LPRNT+1),HL
	LD	HL,(@GETL)
	LD	(_GETL+1),HL
	LD	HL,(@GETKY)
	LD	(#GETKY+1),HL
	LD	HL,(@CSR)
	LD	(#CSR+1),HL
	LD	HL,(@LOC)
	LD	(#LOC+1),HL
	LD	HL,(@FLGET)
	LD	(#FLGET+1),HL
	LD	HL,(@MON)
	LD	(#MON+1),HL
	LD	HL,(@WIDCH)
	LD	(#WIDCH+1),HL
	LD	HL,(@KYBFS)
	LD	(#KYBFS+1),HL
	LD	HL,(@KYBFC)
	LD	(#KYBFC+1),HL
	LD	HL,(@COLOR)
	LD	(#COLOR+1),HL
;
	LD	A,(@MAXLN)
	LD	(#MAXLN),A
	LD	HL,(@XYADR)
	LD	(#XYADR),HL
	LD	HL,#CONYS	;-#COLORF
	LD	DE,WORKS
	LD	BC,10
	LDIR
;
	LD	HL,FKEYT	;Function Key
	LD	DE,FKEY
	LD	BC,160
	LDIR
;
	LD	HL,($FAFE)
	LD	($0EA6),HL
;
	LD	HL,$FB00
	LD	DE,$0EA8
	LD	BC,$0040
	LDIR
;
	LD	BC,$1FA0	;INIT CTC
	LD	A,$35
	OUT	(C),A
	XOR	A
	OUT	(C),A
	LD	D,$47
	LD	E,3
CTC:
	INC	C
	OUT	(C),D
	OUT	(C),A
	DEC	E
	JR	NZ,CTC
;
	LD	BC,$1F91	;INIT SIO
	LD	D,1
	OUT	(C),D
	OUT	(C),A
	INC	C
	INC	C
	OUT	(C),D
	OUT	(C),A
;
	LD	BC,$1F80	;INIT DMA
	LD	A,$C3
	OUT	(C),A
	OUT	(C),A
	OUT	(C),A
	OUT	(C),A
	OUT	(C),A
	OUT	(C),A
;
	LD	DE,TITLE
	CALL	SETKB
	LD	DE,RDATA
	CALL	SETKB
	JP	0
;
SETKB:
	LD	A,(DE)
	INC	DE
	OR	A
	RET	Z
	CP	$0D
	JR	NZ,SK1
	LD	A,$03
SK1:
	CALL	#KYBFS
	JR	SETKB
;
RDATA:	DB	"Release",$03,"MEMAX:FF00",$03,0
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" KANJI    ¼Þ®³Á­³",$0D
	DB	" KANJI /R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
;	¼Þ®³Á­³ÌÞ
;
START:	DB	"[KANJI",0,0
;
\RST:
RSS1:	LD	(RSTSP+1),SP
	PUSH	AF
RSS2:	LD	A,(RSTSP+2)
	ADD	A,A
	JR	C,RS2
	POP	AF
	LD	SP,EXTSP
	JR	RS3
RS2:
	POP	AF
RS3:
RS1PAT:	CALL	RS1	;PATCH
	LD	B,$1E
	OUT	(C),A
RSTSP:	LD	SP,0
	RET
RS1:
	PUSH	BC	;PATCH
	LD	B,$1D
	OUT	(C),A
	RET
;
\INT
	PUSH	AF
	PUSH	BC
	LD	(EXTSP),SP
	LD	A,(EXTSP+1)
	ADD	A,A
	JR	C,INTP2
INTS1:	LD	(0),SP
	LD	SP,EXTSP
INTP2:
	LD	A,$1A
	IN	A,($01)
	PUSH	AF
	LD	A,$1D
	OUT	($00),A
	CALL	$F875		;JP (HL)
	POP	AF
	AND	$10
	LD	A,$1D
	JR	Z,INTP1
	INC	A
INTP1:
	OUT	($00),A
	LD	HL,EXTSP
	OR	A
	SBC	HL,SP
	JR	NZ,INTPE
INTSP:	LD	SP,0
INTPE:
	POP	BC
	POP	AF
	POP	HL
	EI
	RETI
;
\INIT:
	LD	BC,$1099	;BIOSRS
	RST	$18
	CALL	$09A1
	LD	A,1
	LD	($FAEC),A	;CLICK OFF
	LD	($FABC),A
	LD	HL,FKEYT
	LD	($FABA),HL
	LD	A,(#WIDTH)
	CALL	#WIDCH
	LD	A,(#WK1FD0)
	CALL	#S1FD0
	JP	#COLD
;
\VER:	LD	HL,$21C1
	RET
;
\PRINT:
	PUSH	BC
	LD	BC,$1791	;ACCPRT
	RST	$18
	POP	BC
	RET
;
\PRNT0:
	PUSH	BC
	LD	BC,$179D	;ACCDIS
	RST	$18
	POP	BC
	RET
;
\NL:
	PUSH	AF
	PUSH	BC
	LD	BC,$1770	;CR2
	RST	$18
	POP	BC
	POP	AF
	RET
;
\TAB:
	LD	A,($FADF)
	SUB	B
	CCF
	RET	C
TAB1:
	CALL	#PRNTS
	INC	A
	JR	NZ,TAB1
	RET
;
\LPRNT:
	DI
	PUSH	BC
	PUSH	DE
	PUSH	HL
SPWPAT:	LD	(0),SP
	LD	HL,$C337
	LD	($F83C),HL
LPPAT:	LD	HL,0
	LD	($F83E),HL
	CP	$0D
	JR	NZ,LP1
	LD	BC,$37B2		;CR1LPL
	JR	LPFIN
LP1:
	CP	$09
	JR	NZ,LP2
	LD	BC,$39C1		;TABLPL
	JR	LPFIN
LP2:
	LD	BC,$3839		;ACCLPL
LPFIN:
	RST	$18
	AND	A
\LPERR:
	LD	B,$1E
	OUT	(C),A
SPPAT:	LD	SP,0
	POP	HL
	POP	DE
	POP	BC
	EI
	RET
;
\GETL:
	PUSH	BC
	LD	BC,$1DE4	;INPUTF
	RST	$18
	POP	BC
	RET	NC
	LD	A,$1B
	LD	(DE),A
	RET
;
\GETKY:
	PUSH	BC
	XOR	A
	LD	BC,$1FF0	;INKEYS
	RST	$18
	POP	BC
	OR	A
	EI
	RET	Z
	JP	#KYBFC
;
\CSR:	LD	HL,($FADF)
	RET
;
\LOC	LD	($FADF),HL
	AND	A
	RET
;
\MON:
	LD	BC,$33C5	;MON
	RST	$18
	LD	HL,$FFFF
	LD	(#CYL0),HL
	LD	(#CYL2),HL
	CALL	#RDVSW
	JP	#CHGDRV
;
\FLGET: CALL    #GETKY
	PUSH	BC
	LD	A,1
	LD	BC,$1FF0	;INKEYS
	RST	$18
	POP	BC
	RET
;
\WIDCH:	CALL	0
	LD	A,(#WIDTH)
	LD	($F87F),A
	DEC	A
	LD	($FAE6),A
	LD	A,$0C
	JP	#PRINT
;
\KYBFS:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	E,A
	LD	BC,$15D8	;@KYBFS
	RST	$18
	XOR	A
	LD	($FB6F),A
	POP	HL
	POP	DE
	POP	BC
	RET
;
\KYBFC:
	PUSH	HL
	LD	HL,0
	LD	($FAFE),HL
	POP	HL
	RET
;
\COLOR:	
	LD	($F8D0),A
	RET
FIN:
