;
;	CD by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	LD	IX,(#IBFAD)
	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JP	Z,USAGE
	CP	"/"
	JP	Z,USAGE
	CP	"\"
	JR	NZ,CDR1
	CALL	#FILE
	RET	C
	JR	CDR2
CDR1:
	CALL	#FILE
	RET	C
	LD	A,(IX+1)
	CP	$20
	JR	Z,CD1
CDR2:
	CALL	#ROPEN
	RET	C
	BIT	4,(IX+1+$0B)
	SCF
	RET	Z
	JP	#RDD
;
CD1:
	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	AND	A
	SBC	HL,DE
	ADD	HL,DE
	JR	C,MEM1
	EX	DE,HL
MEM1:
	LD	DE,BUFFA
	XOR	A
	SBC	HL,DE
	ADD	HL,HL
	JR	C,MEM2
	LD	A,H
	DEC	A
MEM2:
	LD	(RING+1),A
;
	CALL	#DEVADR
	LD	A,(#DSK)
	LD	(WILD),A
	CALL	#PRINT
	CALL	#MPRNT
	DB	$1A,":",0
	LD	L,(IY+$1A)
	LD	H,(IY+$1B)
	LD	A,H
	OR	L
	JR	NZ,CD2
	CALL	#MPRNT
	DB	"\",$0D,0
	RET
CD2:
	LD	(SDIR),HL
	XOR	A
	LD	(STACK),A
	LD	(STOUT),A
	LD	C,A
	LD	B,A
	LD	DE,ROOT
	CALL	PUSH
	JP	C,CDEND
	CALL	#RDFAT
	JP	C,CDEND
;
CDL:
	CALL	POPX
	JP	C,CDEND
	CALL	#BRKEY
	JP	Z,CDEND
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	(IY+$1A),C
	LD	(IY+$1B),B
	LD	DE,SEARCH
	LD	BC,126
	LDIR
	XOR	A
	LD	(DE),A
;
	LD	DE,WILD
	CALL	#FILE
	CALL	NC,#SOPENX
CDN:
	JR	C,CDL
	CALL	#BRKEY
	JP	Z,CDEND
;
	PUSH	HL
	POP	IX
	BIT	4,(IX+$0B)
	JR	Z,CDNE
	LD	A,(IX)
	CP	"."
	JR	Z,CDNE
;
	LD	B,8
	LD	DE,FILE
	CALL	CDF
	LD	A,(HL)
	CP	$21
	JR	C,CDF2
	LD	A,"."
	LD	(DE),A
	LD	B,3
	INC	DE
	CALL	CDF
CDF2:
	XOR	A
	LD	(DE),A
	LD	C,(IX+$1A)
	LD	B,(IX+$1B)
	LD	HL,(SDIR)
	SBC	HL,BC
	JR	NZ,CDF3
;
	LD	DE,SEARCH
	CALL	#MSX
	LD	A,"\"
	CALL	#PRINT
	LD	DE,FILE
	CALL	#MSX
	CALL	#LTNL
	JP	CDEND
;
CDF3:
	LD	DE,SEARCH
	CALL	PUSH
	JP	C,CDEND
	LD	(HL),"\"
	LD	DE,FILE
	CALL	PUSH1
CDNE:
	LD	DE,WILD
	CALL	#FILE
	CALL	NC,#NOPENX
	JR	CDN
;
CDF:
	LD	A,(HL)
	INC	HL
	CP	$21
	JR	C,CDFF
	CP	$A0
	JR	Z,CDFF
	LD	(DE),A
	INC	DE
CDFF:
	DJNZ	CDF
	RET
;
CDEND:
	LD	HL,(SDIR)
	LD	(IY+$1A),L
	LD	(IY+$1B),H
	CALL	#NL
	XOR	A
	JP	#MOTOFF
;
PUSH:
	CALL	PUSHX
	RET	C
	LD	(HL),C
	INC	HL
	LD	(HL),B
PUSH1:
	INC	HL
	LD	A,(DE)
	INC	DE
	LD	(HL),A
	OR	A
	JR	NZ,PUSH1
	RET
;
PUSHX:
	LD	A,(STACK)
	LD	HL,(STOUT)
	LD	H,A
	INC	A
	CALL	RING
	CP	L
	SCF
	RET	Z
	LD	(STACK),A
	JR	PUSHX1
POPX:
	LD	A,(STOUT)
	LD	HL,(STACK)
	CP	L
	SCF
	RET	Z
	LD	H,A
	INC	A
	CALL	RING
	LD	(STOUT),A
PUSHX1:
	LD	L,H
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	PUSH	BC
	LD	BC,BUFFA
	ADD	HL,BC
	POP	BC
	RET
;
RING:	CP	$00
	RET	NZ
	XOR	A
	RET
;
USAGE:
	CALL	#MPRNT
	DB	$1A,"cd v1.00"
	DB	" Lovers",$0D
	DB	"usage:",$0D
	DB	" CD [pathname]",$0D
	DB	0
	XOR	A
	RET
;
ROOT:	DB	0
WILD:	DB	"@:*.*",0
;
SDIR:	DW	0
STACK:	DB	0
STOUT:	DB	0
;
FILE:
SEARCH:	EQU	FILE+13
BUFFA:	EQU	SEARCH+128
