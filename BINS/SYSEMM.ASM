;
;	Systemcopy EMM
;
DATA	EQU	$3400
#FNAME	EQU	$1481
;
	$INCLUDE	LA.DEF
;
	OFFSET	$C000-$3000
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"X1 systemcopy(EMM) v1.04"
	DB	" Lovers",$0D,0
	POP	DE
	LD	A,1
	CALL	#FILE
	JP	C,USAGE
	LD	A,(#FNAME)
	CP	$20
	JP	Z,USAGE
	LD	A,(#WILD)
	OR	A
	JP	NZ,USAGE
;
	CALL	#DEVADR
	JP	C,USAGE
	LD	A,(IY+$12)
	CP	6
	JR	NZ,SEP
	LD	A,(IY+$13)
	OR	A
	JR	NZ,SEP
;
	LD	A,(#DSK)
	LD	(DEVICE),A
	JR	EMM
SEP:
	LD	A,(DE)
	INC	DE
	CP	$20
	JR	Z,OK1
	CP	","
	JP	NZ,USAGE
OK1:
	LD	A,(DE)
	INC	DE
	LD	(DEVICE),A
	CALL	#DEVAD2
	JP	C,USAGE
	LD	A,(IY+!DEVNO)
	CP	6
	JP	NZ,USAGE
;
	LD	A,(DE)
	CP	":"
	JP	NZ,USAGE
;
EMM:
	CALL	COPY
	RET	C
;
	CALL	#MPRNT
	DB	"Ok",$0D
	DB	"make IPL EMM ",0
;
	LD	HL,(#DTBUF)
	LD	BC,$0400
CLEAR:
	LD	(HL),0
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,CLEAR
;
	LD	HL,(#DTBUF)
	LD	(HL),$01
	INC	HL
;
	LD	IX,(#DTBUF)
	LD	DE,#FNAME
	LD	B,8
SET1:
	LD	A,(DE)
	INC	DE
	LD	(HL),A
	INC	HL
	DJNZ	SET1
;
	LD	(HL),"."
	INC	HL
;
	LD	B,3
SET2:
	LD	A,(DE)
	INC	DE
	LD	(HL),A
	INC	HL
	DJNZ	SET2
;
	LD	(HL),$20
	INC	HL
;
	LD	(HL),"S"
	INC	HL
	LD	(HL),"y"
	INC	HL
	LD	(HL),"s"
;
	LD	HL,(#SIZE)
	LD	(IX+18),L
	LD	(IX+19),H
;
	LD	HL,(DTADR)
	LD	(IX+20),L
	LD	(IX+21),H
;
	LD	HL,(#EXADR)
	LD	(IX+22),L
	LD	(IX+23),H
;
	LD	A,(DEVICE)
	CALL	#CHGDRV
;
	XOR	A
	LD	HL,(#FNAME+$1A)
	LD	DE,0
	CALL	#CLUST
	ADD	HL,DE
	ADD	HL,HL
	ADC	A,A
	ADD	HL,HL
	ADC	A,A
	LD	(IX+29),A
	LD	(IX+30),L
	LD	(IX+31),H
;
	LD	DE,0
	LD	HL,(#DTBUF)
	CALL	#DWTC
	CALL	#MOTOFF
	RET	C
	CALL	#MPRNT
	DB	"Ok",$0D
	DB	"completed !",$0D,0
	AND	A
	RET
;
COPY:
;
	CALL	#FPRNT
	CALL	#PRNTS
;
	CALL	#ROPEN
	RET	C
	LD	HL,(#DTADR)
	LD	A,H
	AND	L
	INC	A
	JR	NZ,BIN
	LD	HL,0
	LD	(#EXADR),HL
BIN:
	LD	(DTADR),HL
	PUSH	HL
	LD      HL,DATA
	LD	(#DTADR),HL
	CALL	#RDD
	POP	HL
	LD	(#DTADR),HL
	RET	C
;
	LD	A,(DEVICE)
	CALL	#CHGDRV
	RET	C
;
	LD	HL,(#FNAME+$16)
	PUSH	HL
	LD	HL,(#FNAME+$18)
	PUSH	HL
	CALL	#WOPEN
	POP	HL
	LD	(#FNAME+$18),HL
	POP	HL
	LD	(#FNAME+$16),HL
	RET	C
;
	CALL	FSIZE
	LD	DE,2
WRDF1:
	CALL	SPACE
	JR	Z,WRDF3
WRDF2:
	INC	DE
	CALL	#ENDCL
	JR	C,WRDF1
FULL:
	CALL	#MPRNT
	DB	"Device Full ",0
	SCF
	RET
;
WRDF3:
	LD	(#FNAME+$1A),DE
WRDF4:
	CALL	SPACE
	JR	NZ,WRDF2
	DEC	B
	JR	Z,WRDF5
;
	INC	DE
	CALL	#ENDCL
	JR	C,WRDF4
	JR	FULL
WRDF5:
	CALL	FSIZE
	LD	DE,(#FNAME+$1A)
;
WRDF6:
	LD	A,B
	DEC	A
	JR	NZ,WRDF7
	LD	HL,$FFFF
	JR	WRDF8
WRDF7:
	LD	H,D
	LD	L,E
	INC	HL
WRDF8:
	PUSH	BC
	PUSH	DE
	CALL	#SNCL
	POP	DE
	POP	BC
	INC	DE
	DJNZ	WRDF6
;
	CALL	#WTFAT
	RET	C
	CALL	#WTDIRX
	RET	C
;
	LD	HL,DATA
	LD	(#DTADR),HL
	LD	DE,(#FNAME+$1A)
	LD	B,0
	JP	#WRDD
;
FSIZE:
	LD	HL,(#SIZE)
	DEC	HL
	SRL	H
	SRL	H
	INC	H
	LD	B,H
	RET
;
SPACE:
	PUSH	BC
	PUSH	DE
	CALL	#GNCL
	LD	A,D
	OR	E
	POP	DE
	POP	BC
	RET
;
USAGE:
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" SYSEMM filename drv:",$0D
	DB	0
	AND	A
	RET
;
DEVICE:	DB	0
DTADR:	DW	0
