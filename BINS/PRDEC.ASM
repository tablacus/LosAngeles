;
;	PRDEC
;
	$INCLUDE	LA.DEF
;
	OFFSET	$C000-$3000
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"print decimal, prdec v1.01",$0D
	DB	" (C) 1994 by Larn M.R."
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	PRDEC
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
PRDEC:
	CALL	PRO
	RET	C
;
	LD	HL,(#PRTHLX+1)
	LD	(PRTHL),HL
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+6),HL
	AND	A
	LD	BC,LAST-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
;
	PUSH	HL
	LD	DE,PRTHLX-START
	ADD	HL,DE
	LD	(#PRTHLX+1),HL
	LD	DE,DECTAB-PRTHLX
	ADD	HL,DE
	LD	(PATCH1+1),HL
	POP	DE
	LD	HL,START
	LDIR
	AND	A
	RET
;
REMOVE:	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,6
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Ï¾Ý!",7,$0D,0
	AND	A
	RET
;
REM2:	CALL	#MPRNT
	DB	'release',$0D,'MEMAX:',0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	LD	H,A
	LD	(#PRTHLX+1),HL
	AND	A
	RET
;
PRO:	LD	HL,(#MEMAX)
PRO1:	LD	DE,(#MEMAX1)
	AND	A
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	RET	Z
;
	LD	A,(HL)
	CP	'['
	SCF
	CCF
	RET	NZ
	LD	DE,START+1
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,$1F
	CP	B
	JR	NC,PRO2
	DEC	HL
	LD	B,5
PRO2:
	PUSH	BC
	PUSH	HL
	CALL	CPSTR
	POP	HL
	POP	BC
	SCF
	RET	Z
	LD	C,B
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	PRO1
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" PRDEC    ¼Þ®³Á­³",$0D
	DB	" PRDEC /R  ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
;
;		 012345	 6 7
START:	DB	"[PRDEC",0,0
;
PRTHL:	DW	0
;
PRTHLX:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	HL
PATCH1:	LD	HL,DECTAB
	LD	BC,$0500
PRT1:
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	(SP),HL
	XOR	A
PRT2:
	SBC	HL,DE
	JR	C,PRT3
	INC	A
	JR	PRT2
PRT3:
	ADD	HL,DE
	LD	D,A
	LD	A,B
	DEC	A
	LD	A,D
	JR	Z,PRT5
	OR	C
	LD	A,D
	JR	Z,PRT4
PRT5:
	INC	C
	ADD	A,"0"-$20
PRT4:
	ADD	A,$20
	CALL	#PRINT
;
	EX	(SP),HL
	DJNZ	PRT1
;
	POP	HL
	POP	HL
	POP	DE
	POP	BC
	RET
;
DECTAB:	DW	10000,1000,100,10,1
;
LAST:
