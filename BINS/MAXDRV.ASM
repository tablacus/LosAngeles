;
;	MAXDRV	by Larn M.R.
;
	$INCLUDE	LA.DEF
;
	ORG	$3000
;
	PUSH	DE
	CALL	#MPRNT
	DB	$1A,"maxdrv v1.00"
	DB	" (C)1994 Lovers"
	DB	$0D,0
	POP	DE
;
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"R"
	JP	Z,REMOVE
	CP	"Z"
	JP	NZ,USAGE
	INC	DE
	CALL	#SPSK
MAIN:
	INC	DE
	LD	A,(DE)
	CP	":"
	JP	NZ,USAGE
	DEC	DE
	LD	A,(#MAXDRV)
	ADD	A,"A"
	LD	B,A
	LD	A,(DE)
	CALL	#CAP
	CP	B
	JP	C,USAGE
	CP	"Z"+1
	JP	NC,USAGE
;
	AND	$1F
;
	PUSH	AF
	LD	HL,(#DEVADX+1)
	LD	(@DEVADX),HL
	LD	HL,(#DEVADR+1)
	LD	(@DEVADR),HL
	LD	A,(#MAXDRV)
	LD	B,A
	LD	(@MAXDRV),A
	LD	(OLDMAX+1),A
	INC	A
	LD	(OMAXP1+1),A
	POP	AF
;
	LD	(#MAXDRV),A
	LD	(NEWMAX+1),A
;
	SUB	B
	LD	L,A
	LD	H,0
	ADD	HL,HL	;*2
	ADD	HL,HL	;*4
	ADD	HL,HL	;*8
	ADD	HL,HL	;*16
	ADD	HL,HL	;*32
	LD	(DATA),HL
;
	CALL	#MPRNT
	DB	"MEMAX:",0
	LD	HL,(#MEMAX)
	LD	(START+8),HL
	AND	A
	LD	BC,(DATA)
	SBC	HL,BC
	LD	(DATAX+1),HL
	LD	BC,TSREND-START
	SBC	HL,BC
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
;
	EX	DE,HL
	LD	HL,DEVADX-START
	ADD	HL,DE
	LD	(#DEVADX+1),HL
;
	LD	HL,DEVADR-START
	ADD	HL,DE
	LD	(#DEVADR+1),HL
;
	LD	HL,START
	LDIR
	EX	DE,HL
	LD	BC,(DATA)
CLEAR:
	LD	(HL),0
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,CLEAR
	RET
;
REMOVE:
	LD	HL,(#MEMAX)
	LD	DE,(#MEMAX1)
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,REME
;
	LD	DE,START
	LD	B,8
	CALL	CPSTR
	JR	Z,REM2
REME:	CALL	#MPRNT
	DB	"¶²¼Þ®ÃÞ·Ï¾Ý!",7,$0D,0
	AND	A
	RET
;
REM2:
	PUSH	HL
	CALL	CHECK
	POP	HL
	RET	NC
	CALL	#MPRNT
	DB	"release",$0D,"MEMAX:",0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL
	LD	(#MEMAX),HL
	CALL	#PRTHL
	CALL	#LTNL
	INC	DE
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	INC	DE
	LD	H,A
	LD	(#DEVADX+1),HL
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	LD	H,A
	LD	(#DEVADR+1),HL
	INC	DE
	LD	A,(DE)
	LD	(#MAXDRV),A
	LD	B,A
	CALL	#DEVADR
	RET	NC
	LD	C,"A"
CURRENT:
	LD	A,C
	CALL	#CHGDRV
	RET	NC
	DJNZ	CURRENT
;
	LD	A,"A"
	JP	#CHGDRV
;
CHECK:
	LD	DE,7
	ADD	HL,DE
	LD	C,(HL)
	LD	A,(#MAXDRV)
	SUB	C
	LD	B,A
	LD	L,0
CHECK1:
	INC	C
	LD	A,C
	CALL	#DEVADX
	CALL	NC,USING
	DJNZ	CHECK1
;
	INC	L
	DEC	L
	SCF
	RET	Z
;
	CALL	#MPRNT
	DB	"¿ÚÃÞÓ ¶²¼Þ®¼Ï½¶? (y/other):",0
	CALL	#FLGET
	CALL	#CAP
	CP	'Y'
	JR	Z,SURE1
	CP	'Ý'
	JR	Z,SURE1
;
	CALL	#MPRNT
	DB	"no",$0D,0
	AND	A
	RET
;
SURE1:	CALL	#MPRNT
	DB	"yes",$0D,0
	SCF
	RET
;
USING:
	INC	L
	CALL	#MPRNT
	DB	"ÄÞ×²ÌÞ",0
	LD	A,C
	ADD	A,$40
	CALL	#PRINT
	CALL	#MPRNT
	DB	":Ê ",0
	LD	A,(IY+$1C)
	CALL	#PRINT
	LD	A,(IY+$1D)
	CALL	#PRINT
	LD	A,(IY+$1E)
	CALL	#PRINT
	LD	A,(IY+$1F)
	CALL	#PRINT
	CALL	#MPRNT
	DB	" Ä¼Ã ¼Ö³»ÚÃ²Ï½",$0D
	DB	0
	RET
;
CPSTR:	LD	A,(DE)
	CP	(HL)
	RET	NZ
	INC	DE
	INC	HL
	DJNZ	CPSTR
	RET
;
USAGE:	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" MAXDRV <drv>:  ¼Þ®³Á­³",$0D
	DB	" MAXDRV /R       ¶²¼Þ®",$0D
	DB	0
	AND	A
	RET
;
DATA:	DW	0
;
;		 0  1  234567  8 9
START:	DB	"[",6,"MAXDRV",0,0
;
_DEVADX:DB	$C3	;JP
@DEVADX:DW	0
@DEVADR:DW	0
@MAXDRV:DB	0
;
DEVADR:
	LD	A,(#DSK)
DEVADX:
	DEC	A
	AND	$1F
NEWMAX:	CP	$00		;NEW MAXDRV
	CCF
	RET	C
OLDMAX:	CP	8		;OLD MAXDRV
	INC	A
	JR	C,_DEVADX
OMAXP1:	SUB	9		;OLD MAXDRV+1
	PUSH	HL
	LD	L,A
	LD	H,0
	ADD	HL,HL	;*2
	ADD	HL,HL	;*4
	ADD	HL,HL	;*8
	ADD	HL,HL	;*16
	ADD	HL,HL	;*32
	PUSH	DE
DATAX:	LD	DE,0
	ADD	HL,DE
	POP	DE
	PUSH	HL
	POP	IY
	POP	HL
	LD	A,(IY)
	CP	$01
	RET
;
TSREND:
