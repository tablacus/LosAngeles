;
;
;	ISH	by Larn M.R.
;
	ORG	$A000
;
	$INCLUDE	LA.DEF
;
_FILESW	EQU	$3000
	$INCLUDE	FILES.ASM
;
BLOCKS	EQU	66
FGBUF	EQU	$6000
DATA	EQU	$8000
DTEND	EQU	$A0
LENGTH	EQU	8
;
;
	CALL	#MPRNT
	DB	$1A,"ish file converter v1.00"
	DB	" Lovers",$0D
	DB	0
SW1:
	CALL	#SPSK
	LD	A,(DE)
	CP	"-"
	JR	Z,SWI
	CP	"/"
	JR	NZ,SWEND
SWI:
	INC	DE
SWX:
	LD	A,(DE)
	CALL	#CAP
	INC	DE
	CP	"S"
	JR	NZ,SW2
	JP	ENCODE
SW2:
SWE:
	LD	A,(DE)
	CP	$21
	JR	C,SW1
	JR	SWX
;
SWEND:
	LD	A,(DE)
	CP	$21
	JP	C,USAGE
;
	CALL	_FILESI
	XOR	A
	CALL	#FILE
	JP	C,END
;
	PUSH	DE
	CALL	FSET
	LDIR
	CALL	ROPEN
	POP	DE
	JP	C,END
;
	LD	A,1
	CALL	_CHGFILE
	XOR	A
	CALL	#FILE
	JP	C,END
MAIN:
	XOR	A
	CALL	_CHGFILE
	CALL	#MPRNT
	DB	"Restore from ",0
	CALL	FPRNT
	CALL	#LTNL
HEADER:
	CALL	DEC
	JP	C,END
HEADER1:
	OR	A
	JR	NZ,HEADER
;
	LD	A,1
	CALL	_CHGFILE
	LD	HL,(BUFFA+2)
	LD	(#SIZE),HL
	LD	(LSIZE),HL
	LD	DE,2
	ADD	HL,DE
	LD	(SIZEL),HL
	LD	HL,(BUFFA+4)
	LD	(#SIZEH),HL
	LD	(LSIZEH),HL
	LD	DE,0
	ADC	HL,DE
	LD	(SIZEH),HL
;
	LD	HL,BUFFA+12
	LD	DE,(#IBFAD)
	XOR	A
	LD	(DE),A
	INC	DE
	LD	BC,13
	LDIR
;
	LD	A,$20
	LD	(DE),A
	INC	DE
;
	LD	B,10
	XOR	A
HEAD1:
	LD	(DE),A
	DJNZ	HEAD1
;
	CALL	#FPRNT
	CALL	#PRNTS
;
	CALL	WOPEN
	JP	C,END
;
	LD	IY,(#IBFAD)
	LD	HL,(BUFFA+27)
	LD	(IY+1+$16),L
	LD	(IY+1+$17),H
	LD	HL,(BUFFA+29)
	LD	(IY+1+$18),L
	LD	(IY+1+$19),H
;
;
;	SKIP HEADER
;
SHEAD:
	CALL	DEC
	JP	C,END
	OR	A
	JR	Z,SHEAD
;
	LD	HL,0
	LD	(SEEDY),HL
	LD	(GETDX),HL
;
	LD	HL,(LSIZEH)
	LD	A,H
	OR	L
	LD	HL,(LSIZE)
	OR	H
	OR	L
	JR	Z,RCLOSE
RD2:
	PUSH	HL
	CALL	GETD
	POP	HL
	JP	C,HEADER
	CALL	CRCY
	CALL	FPUTC
	JP	C,END
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,RD2
;
	LD	HL,(LSIZEH)
	LD	A,H
	OR	L
	JR	Z,RCLOSE
	DEC	HL
	LD	(LSIZEH),HL
	LD	HL,0
	JR	RD2
;
RCLOSE:
	CALL	WCLOSE
	JP	C,END
;
	CALL	GETD
	JR	C,CRCERR
	PUSH	AF
	CALL	GETD
	POP	DE
	JR	C,CRCERR
	LD	E,A
	LD	HL,(SEEDY)
	XOR	A
	SBC	HL,DE
	JR	Z,NHEAD
CRCERR:
	CALL	#MPRNT
	DB	" CRC Error",$0D,0
	JR	NHEAD2
;
NHEAD:
	CALL	#MPRNT
	DB	" Ok",$0D,0
NHEAD2:
	LD	A,(BUFFA)
	OR	A
	JP	Z,HEADER1
	JP	HEADER
;
ENCODE:
	CALL	#SPSK
	LD	A,(DE)
	CP	$21
	JP	C,USAGE
;
	CALL	_FILESI
	XOR	A
	CALL	#FILE
	JP	C,END
;
	PUSH	DE
	CALL	FSET
	LDIR
	CALL	ROPEN
	POP	DE
	JP	C,END
	LD	HL,(#SIZE)
	LD	(LSIZE),HL
	LD	HL,(#SIZEH)
	LD	(LSIZEH),HL
;
	LD	IX,(#IBFAD)
	LD	A,(IX+1+8)
	CALL	#CAP
	CP	"I"
	JR	NZ,ENCM0
	LD	IX,(#IBFAD)
	LD	A,(IX+1+9)
	CALL	#CAP
	CP	"S"
	JR	NZ,ENCM0
	LD	IX,(#IBFAD)
	LD	A,(IX+1+10)
	CALL	#CAP
	CP	"H"
	JP	Z,END
ENCM0:
	LD	A,1
	CALL	_CHGFILE
	XOR	A
	CALL	#FILE
	JP	C,END
;
	LD	A,"I"
	LD	(WILD+8),A
	LD	HL,"S"+"H"*256
	LD	(WILD+9),HL
	CALL	FSET
	EX	DE,HL
	LDIR
;
	CALL	#MPRNT
	DB	"Store to ",0
	CALL	FPRNT
	CALL	#MPRNT
	DB	$0D
	DB	0
;
	CALL	WOPEN
	JP	C,END
;
	LD	HL,0
	LD	(SIZEL),HL
	LD	(SIZEH),HL
;
	LD	HL,MARKA
	CALL	FPUTS
	CALL	NC,FPUTFN
	LD	HL,MARKB
	CALL	NC,FPUTS
	RET	C
;
	XOR	A
	CALL	_CHGFILE
;
	CALL	#FPRNT
	CALL	#PRNTS
;
	LD	HL,BUFFA
;
	XOR	A
	LD	(HL),A
	INC	HL
	LD	(HL),A
	INC	HL
;
	LD	DE,(#SIZE)
	LD	(HL),E
	INC	HL
	LD	(HL),D
	INC	HL
	LD	DE,(#SIZEH)
	LD	(HL),E
	INC	HL
	LD	(HL),D
	INC	HL
;
	LD	(HL),$45
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),$44
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),$08
	INC	HL
	LD	(HL),$08
	INC	HL
;
	EX	DE,HL
	LD	HL,(#IBFAD)
	INC	HL
	PUSH	HL
	LD	BC,8+3
	LDIR
	EX	DE,HL
;
	LD	(HL),0
	INC	HL
	LD	(HL),$FE
	INC	HL
	LD	(HL),$01
	INC	HL
	LD	(HL),$01
	INC	HL
;
	EX	DE,HL
	POP	HL
	LD	BC,$16
	ADD	HL,BC
	LD	BC,4
	LDIR
;
	LD	B,66-30
	XOR	A
ENCM1:
	LD	(DE),A
	INC	DE
	DJNZ	ENCM1
;
	CALL	ENC
	JP	C,END
	CALL	ENC
	JP	C,END
	CALL	ENC
	JP	C,END
	LD	HL,0
	LD	(SEEDY),HL
	LD	HL,66*66
	LD	(GETDX),HL
	CALL	CLRBLK
;
	LD	HL,(LSIZEH)
	LD	A,H
	OR	L
	LD	HL,(LSIZE)
	OR	H
	OR	L
	JP	Z,END
ENCM2:
	PUSH	HL
	CALL	FGETC
	CALL	CRCY
	CALL	NC,SETD
	POP	HL
	JP	C,END
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,ENCM2
;
	LD	HL,(LSIZEH)
	LD	A,H
	OR	L
	JR	Z,ENCM3
	DEC	HL
	LD	(LSIZEH),HL
	LD	HL,0
	JR	ENCM2
ENCM3:
	LD	A,(SEEDY+1)
	CALL	SETD
	JP	C,END
	LD	A,(SEEDY)
	CALL	SETD
	JP	C,END
;
	LD	HL,66*66
	LD	DE,(GETDX)
	XOR	A
	SBC	HL,DE
	JP	Z,END
;
	LD	C,66
	CALL	DIV
	LD	B,L
;
	CALL	SETDX
	LD	A,$1A
	CALL	NC,FPUTCX
	JP	C,END
;
	LD	A,1
	CALL	_CHGFILE
	LD	IX,(#IBFAD)
	LD	HL,(SIZEL)
	LD	(IX+1+$1C),L
	LD	(IX+1+$1D),H
	LD	HL,(SIZEH)
	LD	(IX+1+$1E),L
	LD	(IX+1+$1F),H
	CALL	WCLOSE
	JP	C,END
	CALL	#MPRNT
	DB	" Ok",$0D,0
;
	JP	END
;
SETD:
	LD	HL,(GETDA)
	LD	(HL),A
	INC	HL
	LD	(GETDA),HL
;
	LD	HL,(GETDX)
	DEC	HL
	LD	(GETDX),HL
	LD	A,H
	OR	L
	RET	NZ
;
	LD	B,66
SETDX:
	LD	A,"."
	CALL	#PRINT
	LD	C,1
	LD	HL,BLOCK
SETDX1:
	PUSH	BC
	LD	A,C
	LD	(BUFFA),A
	LD	DE,BUFFA+1
	LD	BC,66
	LDIR
	PUSH	HL
	CALL	ENC
	POP	HL
	POP	BC
	RET	C
	INC	C
	DJNZ	SETDX1
;				SUM
	LD	C,66
	LD	HL,BLOCK
	LD	IX,BUFFA+1
	LD	(IX-1),67
SETDX3:
	XOR	A
	LD	B,66
	LD	DE,BLOCKS
SETDX4:
	SUB	(HL)
	ADD	HL,DE
	DJNZ	SETDX4
;
	LD	(IX),A
	INC	IX
	LD	(HL),A
	LD	DE,-BLOCKS*66+1
	ADD	HL,DE
	DEC	C
	JR	NZ,SETDX3
;
	CALL	ENC
;
	LD	A,68
	CALL	GETBLK
	LD	B,67
SETDX5:
	LD	(HL),B
	INC	HL
	DJNZ	SETDX5
;
	LD	C,67
	LD	HL,BLOCK
	LD	IX,BUFFA+1
	LD	(IX-1),69
SETDX6:
	XOR	A
	LD	B,68
	LD	DE,BLOCKS+1
SETDX7:
	SUB	(HL)
	ADD	HL,DE
	DJNZ	SETDX7
;
	LD	(IX+1),A
	INC	IX
	LD	DE,-BLOCKS*68-67
	ADD	HL,DE
	DEC	C
	JR	NZ,SETDX6
	LD	(BUFFA+1),A
;
	CALL	ENC
;
	LD	HL,MARKC
	CALL	NC,FPUTS
	CALL	NC,FPUTFN
	LD	HL,MARKC
	CALL	NC,FPUTS
	CALL	NC,FPUTCD
	RET	C
	LD	HL,66*66
	LD	(GETDX),HL
	CALL	CLRBLK
	XOR	A
	RET
;
GETD:
	LD	HL,(GETDX)
	LD	A,H
	OR	L
	CALL	Z,NEXTDT
	RET	C
	DEC	HL
	LD	(GETDX),HL
;
	LD	HL,(GETDA)
	LD	A,(HL)
	INC	HL
	LD	(GETDA),HL
	RET
;
NEXTDT:
;
;	CLEAR BLOCK
;
	CALL	CLRBLK
;
;	GET DATA
;
GETDATA:
	LD	A,(BUFFA)
	LD	(LLINE),A
	PUSH	AF
	LD	HL,BLOCK2
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	(HL),A
;
	CALL	GETBLK
	EX	DE,HL
	LD	HL,BUFFA+1
	LD	BC,66
	LDIR
	CALL	DEC
	POP	BC
	JR	C,GETEND
	INC	B
	CP	B
	JR	NC,GETDATA
;
GETEND:
	CALL	SUBBLK
	JR	C,LASTBLK
;
	LD	B,66
	CALL	CALCBLK
;
	LD	HL,66*66
	RET
;
LASTBLK:
	LD	HL,(SIZEL)
	LD	C,66
	CALL	DIV
	LD	B,L
	CALL	CALCBLK
	LD	HL,66*66
	RET
;
END:
	LD	A,$C9		;RET
	LD	(FGPAT),A
	CALL	#NL
	CALL	#BRKEY
	JR	NZ,NEXT
;
	CALL	#MPRNT
	DB	"Quit",$0D,0
	JR	END2
NEXT:
	XOR	A
	CALL	_CHGFILE
	LD	A,(#WILD)
	OR	A
	JR	Z,END2
;
	CALL	CLRBLK
;
	CALL	FSET
	EX	DE,HL
	LDIR
	CALL	_NROPEN
	EXX
	LD	HL,$8000
	EXX
	JP	NC,MAIN
;
END2:
	CALL	_FILESR
	XOR	A
	JP	#NL
;
;	A<-No.	HL->BLOCK
;
GETBLK:
	LD	B,8
	LD	DE,0
	LD	HL,BLOCKS
MUL1:
	RRCA
	JR	NC,MUL2
	EX	DE,HL
	ADD	HL,DE
	EX	DE,HL
MUL2:
	ADD	HL,HL
	DJNZ	MUL1
	LD	HL,BLOCK-BLOCKS
	ADD	HL,DE
	RET
;
DIV:
	LD	B,16
	XOR	A
DIV1:
	ADD	HL,HL
	ADC	A,A
	CP	C
	JR	C,DIV2
	SUB	C
	INC	L
DIV2:
	DJNZ	DIV1
;
	OR	A
	RET	Z
	INC	L
	RET
;
SUBBLK:				;CF=1 LAST BLOCK
	LD	HL,(SIZEH)
	LD	A,H
	OR	L
	LD	DE,66*66
	LD	HL,(SIZEL)
	JR	NZ,SUBBLK1
;
	AND	A
	SBC	HL,DE
	RET	C
	SCF
	RET	Z
	LD	(SIZEL),HL
	OR	A
	RET
;
SUBBLK1:
	AND	A
	SBC	HL,DE
	LD	(SIZEL),HL
	LD	DE,0
	LD	HL,(SIZEH)
	SBC	HL,DE
	LD	(SIZEH),HL
	RET
;
READBLK:
	LD	E,0
	LD	C,E
	LD	HL,BLOCK2+1
RDBLK1:
	INC	E
	LD	A,(HL)
	OR	A
	JR	NZ,RDBLK2
	INC	C
	DEC	C
	SCF
	RET	NZ
	LD	C,E
RDBLK2:
	INC	HL
	DJNZ	RDBLK1
	LD	A,C
	OR	A
	RET
;
CALCBLK:
	CALL	READBLK
	JR	NC,GETE1
CALCERR:
	CALL	#MPRNT
	DB	"x too many errors",$0D,0
	POP	AF
	POP	AF
	SCF
	RET
;
GETE1:
	JR	Z,GETE2
	CALL	GETBLK
	PUSH	HL
	POP	IX
	LD	A,(BLOCK2+67)
	OR	A
	JR	Z,CALCERR
;
	LD	C,66
	LD	HL,BLOCK
GETEB1:
	XOR	A
	LD	B,67
	LD	DE,BLOCKS
GETEB2:
	SUB	(HL)
	ADD	HL,DE
	DJNZ	GETEB2
;
	LD	(IX),A
	INC	IX
	LD	DE,-BLOCKS*67+1
	ADD	HL,DE
	DEC	C
	JR	NZ,GETEB1
;
	LD	A,"1"
	DB	$01	;LD	BC,
GETE2:
	LD	A,"o"
	JP	#PRINT
;
FGETD:
	PUSH	HL
	CALL	FGETC
	JR	C,FGETD1
	LD	HL,DTABLE
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	A,(HL)
FGETD1:
	POP	HL
	RET
DEC:
	LD	HL,BUFFA2
	LD	B,79
DEC1:
	PUSH	BC
	CALL	FGETD
	LD	(HL),A
	INC	HL
	LD	(LDATA),A
	POP	BC
	RET	C
;
	DEC	B
	JR	Z,DEC1E
	RRA
	JR	NC,DEC1
;
NEXTLINE:
	LD	A,(LDATA)
	DEC	A		;CP	1
	JR	Z,DEC
NLINE1:
	CALL	FGETD
	RET	C
	DEC	A
	JR	NZ,NLINE1
	JR	DEC
;
DEC1E:
	LD	IX,BUFFA
	LD	IY,BUFFA2
;
	LD	A,10
DEC2:
	EX	AF,AF'
;
	LD	A,(IY)
	LD	(IX),A
	LD	A,(IY+1)
	LD	(IX+1),A
;
	LD	A,(IY+7)
	LD	B,(IY+6)
	LD	C,(IY+5)
	LD	D,(IY+4)
	LD	E,(IY+3)
	LD	H,(IY+2)
;				7to8
	RRA
	RRA
	RR	B
	RRA
	RR	B
	RR	C
	RRA
	RR	B
	RR	C
	RR	D
	RRA
	RR	B
	RR	C
	RR	D
	RR	E
	RRA
	RR	B
	RR	C
	RR	D
	RR	E
	RR	H
	RRA
	RR	B
	RR	C
	RR	D
	RR	E
	RR	H
	RR	(IX+1)
	RRA
	RR	B
	RR	C
	RR	D
	RR	E
	RR	H
	RR	(IX+1)
	RR	(IX)
	LD	(IX+6),B
	LD	(IX+5),C
	LD	(IX+4),D
	LD	(IX+3),E
	LD	(IX+2),H
	LD	DE,7
	ADD	IX,DE
	INC	E
	ADD	IY,DE
;
	EX	AF,AF'
	DEC	A
	JP	NZ,DEC2
;
	CALL	CRCX
	LD	A,(BUFFA+67)
	LD	D,A
	LD	A,(BUFFA+68)
	LD	E,A
	XOR	A
	SBC	HL,DE
	JP	NZ,NEXTLINE
	XOR	A
	LD	A,(BUFFA)
	RET
;
ENC:
	CALL	CRCX
	LD	IX,BUFFA
	LD	IY,BUFFA2
;
	LD	(IX+67),H
	LD	(IX+68),L
	LD	(IX+69),0
;
	LD	A,10
ENC2:
	EX	AF,AF'
;
	LD	A,(IX)
	LD	(IY),A
;
	LD	B,(IX+6)
	LD	C,(IX+5)
	LD	D,(IX+4)
	LD	E,(IX+3)
	LD	H,(IX+2)
	LD	L,(IX+1)
;
	ADD	A,A
	RL	L
	RL	H
	RL	E
	RL	D
	RL	C
	RL	B
	RLA
	LD	(IY+1),L
	RL	L
	RL	H
	RL	E
	RL	D
	RL	C
	RL	B
	RLA
	LD	(IY+2),H
	RL	H
	RL	E
	RL	D
	RL	C
	RL	B
	RLA
	LD	(IY+3),E
	RL	E
	RL	D
	RL	C
	RL	B
	RLA
	LD	(IY+4),D
	RL	D
	RL	C
	RL	B
	RLA
	LD	(IY+5),C
	RL	C
	RL	B
	RLA
	LD	(IY+6),B
	RL	B
	RLA
	LD	(IY+7),A
	LD	DE,7
	ADD	IX,DE
	INC	E
	ADD	IY,DE
	EX	AF,AF'
	DEC	A
	JP	NZ,ENC2
;
	LD	IY,BUFFA2
	LD	B,79
ENC3:
	LD	D,0
	LD	E,(IY)
	RES	7,E
	INC	IY
	LD	HL,ETABLE
	ADD	HL,DE
	LD	A,(HL)
	CALL	FPUTCX
	RET	C
	DJNZ	ENC3
;
FPUTCD:
	LD	A,$0D
	CALL	FPUTCX
	LD	A,$0A
	RET	C
;
FPUTCX:
	CALL	FPUTC
	RET	C
	PUSH	HL
	LD	HL,(SIZEL)
	INC	HL
	LD	(SIZEL),HL
	LD	A,H
	OR	L
	JR	NZ,FPUTCX1
	LD	HL,(SIZEH)
	INC	HL
	LD	(SIZEH),HL
FPUTCX1:
	POP	HL
	RET
;
;
;	CRC
;
;	IX=TOP ADDR
;	E=LENGTH
;
SEED	EQU	$AA36
;
CRCX:
	LD	IX,BUFFA
	LD	DE,67
;
SEEDX:	LD	HL,SEED
;
CRC1:
	LD	B,8
	LD	C,(IX)
	CALL	CRC2
	INC	IX
	DEC	E
	JR	NZ,CRC1
	RET
;
CRCY:
	PUSH	AF
	PUSH	BC
	PUSH	HL
	LD	HL,(SEEDY)
	LD	B,8
	LD	C,A
;
	CALL	CRC2
	LD	A,H
	XOR	$1E
	LD	H,A
	LD	A,L
	XOR	$0F
	LD	L,A
	LD	(SEEDY),HL
	POP	HL
	POP	BC
	POP	AF
	RET
;
CRC2:
	RLC	C
	LD	A,1
	AND	C
	ADD	HL,HL
	JR	NC,CRC3
	INC	A
CRC3:
	AND	1
	JR	Z,CRC4
	LD	A,H
	XOR	$10
	LD	H,A
	LD	A,L
	XOR	$21
	LD	L,A
CRC4:
	DJNZ	CRC2
	RET
;
CLRBLK:
	LD	HL,BLOCK
	LD	(GETDA),HL
	LD	DE,BLOCK+1
	LD	BC,BLOCKS*69+69
	LD	(HL),0
	LDIR
	RET
;
FSET:
	LD	HL,(#IBFAD)
	INC	HL
	LD	DE,WILD
	LD	BC,22
	RET
;
CALLDE:
	PUSH	DE
	RET
;
ROPEN:
	XOR	A
	CALL	_CHGFILE
	CALL	#ROPEN
	RET	C
	LD	HL,$8000
	EXX
	RET
;
FGETC:
	EXX
	BIT	7,H
	JR	NZ,FGETB
FGETC1:
	LD	A,(HL)
	INC	HL
	EXX
	AND	A
FGPAT:	RET		;/EXX
;
FAPAT:	LD	DE,0
	EX	DE,HL
	ADD	HL,DE
	EX	DE,HL
	EXX
	RET	NC
	SBC	A,A
	DEC	HL
	RET
;
FGETB:
	EXX
	XOR	A
	CALL	_CHGFILE
	LD	DE,(#CLPS)
	CALL	#ENDCL
	JR	NC,FGETE2
	LD	HL,FGBUF
	LD	(#DTADR),HL
	LD	B,8
	CALL	#RDDX
	JR	C,FGETE1
	LD	A,B
	OR	A
	JR	Z,FGETB2
;
	LD	HL,(#SIZE)
	DEC	HL
	LD	A,H
	AND	$1F
	LD	H,A
	LD	DE,FGBUF
	ADD	HL,DE
	EX	DE,HL
	LD	HL,-2
	SBC	HL,DE
	LD	(FAPAT+1),HL
	LD	A,$D9		;EXX
	LD	(FGPAT),A
FGETB2:
	EXX
	LD	HL,FGBUF
	JR	FGETC1
;
FGETE1:
	XOR	A	;ERROR
	SCF
	RET
;
FGETE2:
	EXX
	LD	HL,$8000
	EXX
	SCF		;EOF
	SBC	A,A
	RET
;
WOPEN:
	CALL	#WOPENH
	RET	C
;
	EXX
	LD	BC,DATA
	EXX
	XOR	A
	RET
;
FPUTC:
	EXX
	LD	(BC),A
	INC	BC
	LD	A,B
	CP	DTEND
	EXX
	JR	NC,FPUTC2
	AND	A
	RET
FPUTC2:
	PUSH	BC
	PUSH	HL
	LD	A,1
	CALL	_CHGFILE
	EXX
	LD	BC,DATA
	LD	(#DTADR),BC
	EXX
	LD	B,LENGTH
	CALL	#WRDX
	POP	HL
	POP	BC
	RET
;
WCLOSE:
	LD	A,1
	CALL	_CHGFILE
	LD	HL,(#IBFAD)
	LD	DE,1+$1C
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	OR	(HL)
	INC	HL
	OR	(HL)
	INC	HL
	OR	(HL)
	RET	Z
;
	EXX
	PUSH	BC
	EXX
	POP	HL
	LD	DE,-DATA-1
	ADD	HL,DE
	LD	B,H
	SRA	B
	SRA	B
	INC	B
	JR	Z,WCLOSE2
;
	LD	HL,DATA
	LD	(#DTADR),HL
	CALL	#WRDX
WCLOSE2:
	CALL	NC,#WTFAT
	CALL	NC,#WTDIRX
	JP	#MOTOFF
;
FPUTS:
	LD	A,(HL)
	INC	HL
	OR	A
	RET	Z
	CALL	FPUTCX
	JR	FPUTS
;
FPUTFN:
	CALL	FPUTCS
;
	LD	HL,_FILESD
	LD	B,8
	CALL	NC,FPUTFNS
	LD	A,"."
	CALL	NC,FPUTCX
	LD	B,3
	CALL	FPUTFNS
	RET	C
FPUTCS:
	LD	A,$20
	JP	FPUTCX
;
FPUTFNS:
	INC	HL
	LD	A,(HL)
	CP	$21
	CCF
	JR	NC,FPUTFS1
	CALL	FPUTCX
	RET	C
FPUTFS1:
	DJNZ	FPUTFNS
	RET
;
FPRNT:
	LD	A,(#DSK)
	CALL	#PRINT
	LD	A,":"
	CALL	#PRINT
	JP	#FPRNT
;
USAGE:
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" ISH [½²¯Á] Ì§²ÙÈ°Ñ [ÄÞ×²ÌÞ:]",$0D
	DB	$0D
	DB	9,"/S store a JIS8 format ish file",$0D
	DB	0
	RET
;
MARKA:	DB	"<<<",0
MARKB:	DB	"for L-os Angeles ( use jis8 ish ) ver1.00 >>>",$0D,$0A,0
MARKC:	DB	"---",0
;
DTABLE:
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$01,$FF,$FF,$01,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$01,$FF,$FF,$00,$FF,$02,$04,$06
	DB	$FF,$08,$FF,$0A,$0C,$FF,$FF,$10
	DB	$FF,$FF,$12,$FF,$14,$16,$18,$1A
	DB	$1C,$1E,$20,$22,$24,$26,$28,$0E
	DB	$FF,$2A,$2C,$2E,$30,$32,$34,$36
	DB	$38,$3A,$FF,$3C,$3E,$FF,$40,$42
	DB	$FF,$FF,$44,$FF,$46,$48,$4A,$4C
	DB	$4E,$50,$52,$54,$56,$58,$5A,$FF
	DB	$FF,$5C,$5E,$60,$62,$64,$66,$68
	DB	$6A,$6C,$6E,$70,$72,$74,$76,$FF
	DB	$78,$7A,$7C,$7E,$80,$82,$84,$FF
	DB	$86,$88,$8A,$FF,$8C,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$8E,$90,$92,$94,$96,$98,$9A
	DB	$9C,$9E,$A0,$A2,$A4,$FF,$A6,$A8
	DB	$AA,$AC,$AE,$FF,$B0,$B2,$B4,$B6
	DB	$B8,$BA,$BC,$BE,$C0,$C2,$C4,$FF
	DB	$C6,$C8,$CA,$CC,$CE,$D0,$D2,$D4
	DB	$D6,$D8,$DA,$DC,$DE,$FF,$E0,$E2
	DB	$E4,$E6,$E8,$FF,$EA,$EC,$EE,$F0
	DB	$F2,$F4,$F6,$F8,$FA,$FC,$FE,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
;
ETABLE:
	DB	$23,$25,$26,$27,$29,$2B,$2C,$3F
	DB	$2F,$32,$34,$35,$36,$37,$38,$39
	DB	$3A,$3B,$3C,$3D,$3E,$41,$42,$43
	DB	$44,$45,$46,$47,$48,$49,$4B,$4C
	DB	$4E,$4F,$52,$54,$55,$56,$57,$58
	DB	$59,$5A,$5B,$5C,$5D,$5E,$61,$62
	DB	$63,$64,$65,$66,$67,$68,$69,$6A
	DB	$6B,$6C,$6D,$6E,$70,$71,$72,$73
	DB	$74,$75,$76,$78,$79,$7A,$7C,$A1
	DB	$A2,$A3,$A4,$A5,$A6,$A7,$A8,$A9
	DB	$AA,$AB,$AC,$AE,$AF,$B0,$B1,$B2
	DB	$B4,$B5,$B6,$B7,$B8,$B9,$BA,$BB
	DB	$BC,$BD,$BE,$C0,$C1,$C2,$C3,$C4
	DB	$C5,$C6,$C7,$C8,$C9,$CA,$CB,$CC
	DB	$CE,$CF,$D0,$D1,$D2,$D4,$D5,$D6
	DB	$D7,$D8,$D9,$DA,$DB,$DC,$DD,$DE
;
SIZEL:	DW	0
SIZEH:	DW	0
;
LSIZE:	DW	0
LSIZEH:	DW	0
;
LDATA:	DB	0
LLINE:	DB	0
;
GETDX:	DW	0
GETDA:	DW	0
;
SEEDY:	DW	0
;
FNAME:				;DS	32
DIR	EQU	FNAME+32	;DS	64
;
WILD:	EQU	DIR+64		;DS	22
;
BUFFA:	EQU	WILD+22		;DS	69
BUFFA2:	EQU	BUFFA+69	;DS	80
;
BLOCK	EQU	BUFFA2+80	;DS	BLOCKS*69
BLOCK2	EQU	BLOCK+BLOCKS*69	;DS	69
;
