;
;	SUBMIT
;
	$INCLUDE	LA.DEF
;
	ORG	$D000
;
START:
	CALL	TITLE
	CALL	#FILE
	RET	C
	LD	IX,(#IBFAD)
	LD	A,(IX+1)
	CP	$20
	JP	Z,USAGE
	CP	"-"
	JP	Z,USAGE
	CP	"/"
	JP	Z,USAGE
;
	CALL	#ROPEN
	RET	C
;
	LD	A,(IX+1+$10)
	CP	$A0
	JP	Z,USAGE
;
	LD	HL,DATA
	LD	DE,DATA+1
	LD	BC,2047
	LD	(HL),0
	LDIR
	LD	HL,DATA
	LD	(#DTADR),HL
	LD	B,2
	CALL	#RDDX
	RET	C
	LD	A,$1A
	CALL	#PRINT
	LD	HL,DATA
	LD	(POINT),HL
	LD	HL,(#MEMAX1)
	LD	(MEMAX1),HL
	LD	HL,START
	LD	(#MEMAX1),HL
	LD	HL,(#HOT+1)
	LD	(HOT),HL
	LD	HL,SUBMIT
	LD	(#HOT+1),HL
	JP	(HL)
;
REREASE:
	LD	HL,(#MEMAX1)
	LD	BC,START
	LD	DE,MESMEM
	CALL	CPVEC
	JR	C,RERE1
	LD	HL,(MEMAX1)
	LD	(#MEMAX1),HL
RERE1:
	LD	HL,(#HOT+1)
	LD	BC,SUBMIT
	LD	DE,MESHOT
	CALL	CPVEC
	JR	C,RERE2
	LD	HL,(HOT)
	LD	(#HOT+1),HL
RERE2:
	JP	#HOT
CPVEC:
	AND	A
	SBC	HL,BC
	RET	Z
	PUSH	DE
	CALL	#MPRNT
	DB	"Warning! ",0
	POP	DE
	CALL	#MSX
	CALL	#MPRNT
	DB	" ¶Þ ¶·¶´×ÚÃ²ÙÜ¡",$0D,0
	SCF
	RET
;
SUBMIT:
	LD	SP,(#STKAD)
	CALL	#CINIT
	LD	DE,(#KBFAD)
	PUSH	DE
	CALL	GETL
	POP	DE
	JP	C,REREASE
	CALL	#MSX
	CALL	#LTNL
	CALL	#CMDANL
	CALL	C,#ERROR
	JR	SUBMIT
;
GETL:
	XOR	A
	LD	B,80
	PUSH	DE
GETL1:
	LD	(DE),A
	INC	DE
	DJNZ	GETL1
	POP	DE
	LD	B,80
;
GETL2:
	CALL	#BRKEY
	SCF
	RET	Z
	LD	HL,(POINT)
	LD	A,(HL)
	INC	HL
	LD	(POINT),HL
;
	CP	$0D
	RET	Z
	CP	$0A
	JR	Z,GETL2
;
	OR	A
	SCF
	RET	Z
	CP	$1A
	SCF
	RET	Z
;
	CP	$09
	JR	NZ,GETL3
	LD	A,$20
GETL3:
	LD	(DE),A
	INC	DE
	DJNZ	GETL2
	AND	A
	RET
TITLE:
	PUSH	DE
	CALL	#MPRNT
	DB	$1A
	DB	"submit v1.01"
	DB	" Lovers",$0D
	DB	0
	POP	DE
	RET
USAGE:
	CALL	#MPRNT
	DB	"usage:",$0D
	DB	" SUBMIT filename",$0D
	DB	0
	AND	A
	RET
;
MESHOT:	DB	"#HOT   ",0
MESMEM:	DB	"#MEMAX1",0
;
HOT:	DW	0
MEMAX1:	DW	0
POINT:	DW	DATA
DATA:
