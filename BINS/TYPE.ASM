;
;	TYPE
;
	$INCLUDE	LA.DEF
;
	OFFSET	$C000-$3000
	ORG	$3000
;
START:
	CALL	#SPSK
	LD	A,(DE)
	CP	"/"
	JR	Z,SW2
	CP	"-"
	JR	Z,SW2
	JP	MAIN
;
SW2:	INC	DE
	LD	A,(DE)
	CALL	#CAP
	CP	"P"
	JR	Z,SWP
	CP	"L"
	JP	NZ,USAGE
;
	LD	(LFSW+1),A
SWP:
	INC	DE
	LD	HL,LPRINT
	LD	(PATCH1+1),HL
	LD	(PATCH2+1),HL
	JR	START	
MAIN:
	LD	(DEBUFFA),DE
;
	CALL	#FILE
	JP	C,USAGE
	LD	IX,(#IBFAD)
	LD	A,(IX+1)
	CP	$20
	JP	Z,USAGE
;
	CALL	INIT
;
	CALL	#ROPEN
;
TYPE1:	JR	C,TYPEBK
;
	LD	A,(IX+1+$0B)
	AND	$1C
	JR	NZ,TYPE5
;
	LD	A,(IX+1+$10)
	CP	$A0
	JR	Z,TYPE5
;
	LD	A,(#WILD)
	OR	A
	JR	NZ,FPRNT
	LD	A,(PATCH1+2)
	CP	$30
	JR	NC,TYPE3
FPRNT:
	CALL	#FPRNT		;Ì§²ÙÒ²ÉË®³¼Þ
	CALL	#MPRNT
	DB	$1A,$0D,0
;
TYPE3:	CALL	#FGETC		;1Ó¼ÞÃÞÊÞ²½¶×Æ­³Ø®¸
	JR	C,TYPE4
	CP	$1A		;for MS-DOS
	JR	Z,TYPE5
	CP	$0A		;for MS-DOS
	JR	Z,TYPE3
PATCH1:	CALL	#PRINT
	CALL	#PAUSE
	DW	TYPEBK
	JR	TYPE3
;
TYPE4:	JR	Z,TYPEBK2	;Z=1 ... ERROR
;
TYPE5:	LD	A,(#WILD)
	OR	A
	JR	Z,TYPEBK
	LD	DE,(DEBUFFA)
	CALL	#FILE
	JR	C,TYPEBK2
	CALL	#NROPEN
	JR	TYPE1
;
LPRINT:
	CP	$0D
	JR	NZ,LP1
;
LFSW:	LD	A,$00
	OR	A
	LD	A,$0D
	JR	Z,LP1
	CALL	#LPRNT
	JR	C,LPERR
	LD	A,$0A
LP1:
	CALL	#LPRNT
	RET	NC
LPERR:
	POP	HL	;(PC)
	CALL	TYPEBK2
	SCF
	RET
TYPEBK:
	LD	A,$0D
PATCH2:	CALL	#PRINT
TYPEBK2:
	CALL	#KYBFC
;
!GCBUF:	LD	HL,0
	LD	(#GCBUF),HL
!GCEH:	LD	A,$00
	LD	(#GCEH),A
!CLLN:	LD	A,$00
	LD	(#CLLN),A
;
	AND	A
	RET
;
USAGE:	CALL	#MPRNT
	DB	$1A,"type v1.05"
	DB	" Lovers",$0D
	DB	"usage:",$0D
	DB	" TYPE [½²¯Á] filename",$0D
	DB	$0D
	DB	"½²¯Á:",9,"/P ÌßØÝÀ",$0D
	DB	0
	AND	A
	RET
;
INIT:	LD	HL,(#GCBUF)
	LD	(!GCBUF+1),HL
	LD	A,(#GCEH)
	LD	(!GCEH+1),A
	LD	A,(#CLLN)
	LD	(!CLLN+1),A
;
	LD	HL,START+$400
	LD	A,(#MEMAX+1)
	SUB	H
	CCF
	RET	NC
	SRL	A
	SRL	A
	LD	(#GCBUF),HL
	LD	(#CLLN),A
	ADD	A,A
	ADD	A,A
	ADD	A,H
	DEC	A
	LD	(#GCEH),A
	AND	A
	RET
;
DEBUFFA:DW	0
;
