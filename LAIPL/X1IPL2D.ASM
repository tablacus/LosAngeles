; 2DIPL
;
DRIVE:	EQU	$FF87		;=PORT$FFC
;
	OFFSET	$E200-$FE00
;
	ORG	$FE00
;
LIPL:	DB	$EB,$FE,$90,$20,"L","O","V","E"
	DB	"R","S",$20,$00,$02,$02,$01,$00
	DB	$02,$70,$00,$D0,$02,$FD,$02,$00
	DB	$09,$00,$02,$00,$00,$00,$18,$FE
;
SA:	LD	A,$1D
	OUT	(0),A
;
	LD	SP,0
;
	XOR	A
	LD	($FF8C),A
;
	LD	L,A
	LD	H,A
	LD	($FF80),HL
	LD	A,$7
	LD	($FF86),A
	LD	DE,MES
	CALL	$3CB
;
	LD	A,$1E
	OUT	(0),A
;
	DI
	LD	HL,IPLERR
	PUSH	HL
	LD	HL,IPL0
	PUSH	HL
	LD	HL,IPL3
	PUSH	HL
	LD	HL,IPL2
	PUSH	HL
	XOR	A
IPL1:	LD	(DRIVE),A
	LD	A,1
	LD	(FIRDRV),A
	CALL	FILE
	RET	C
	JP	$0000
;
IPL0:	LD	A,3
	JR	IPL1
IPL2:	LD	A,2
	JR	IPL1
IPL3:	LD	A,1
	JR	IPL1
IPLERR:	CALL	MOTOFF
	LD	A,$1D
	OUT	(0),A
	JP	$00F5
;
FILE:	LD	DE,5
	LD	HL,0
	PUSH	HL
	LD	B,7
	CALL	DRDSB
	POP	HL
	RET	C
;
	LD	C,112
FILE1:	LD	B,11
	LD	DE,FNAME
	PUSH	HL
	CALL	COMP
	POP	HL
	JR	Z,FILE2
	LD	DE,$20
	ADD	HL,DE
	DEC	C
	JR	NZ,FILE1
	SCF
	RET
;
FILE2:
	LD	DE,0
	LD	BC,$20
	LDIR
	LD	HL,($001C)
	DEC	HL
	LD	B,H
	SRL	B
	INC	B
	LD	HL,($001A)
	ADD	HL,HL
	LD	DE,$0008
	ADD	HL,DE
	LD	E,L
	LD	D,H
	LD	HL,0
;
DRDSB:	PUSH	BC
	PUSH	DE
	CALL	DRD
	POP	DE
	POP	BC
	RET	C
	INC	DE
	DJNZ	DRDSB
;
MOTOFF:	PUSH	AF
	LD	HL,DRIVE
	LD	BC,$0FFC
	LD	A,(HL)
	AND	3
	OUT	(C),A
	LD	(HL),A
	POP	AF
	RET
;
DRD:	PUSH	HL
	CALL	SEEK		;ÃÞ¨½¸Ø°ÄÞ
	POP	HL
	RET	C
	DI
	LD	A,$80
	CALL	SECSET
	LD	C,$FB
DRD1:	LD	A,B
	IN	A,($F8)
	RRCA
	JR	NC,DRD3
	RRCA
	JR	NC,DRD1
	INI
	INC	B
	JR	DRD1
;
DRD3:	RLCA
	AND	$3F
	RET	Z
	SCF
	RET
;
SEEK:	LD	B,10
	LD	C,9
	XOR	A
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
DIV1:	ADD	HL,HL		;11
	ADC	A,A		;4
DIV2:	SUB	C		;4
	JR	NC,DIV3	;7	;12
	ADD	A,C		;4
	JP	DIV4		;10
DIV3:	INC	HL		;6
DIV4:	DJNZ	DIV1		;13
	LD	E,A		;±ÏØ
	LD	D,L		;¼®³
;
	LD	HL,DRIVE	;DE<Úº°ÄÞÅÝÊÞ°	;Ó°À°µÝ&¼°¸
	LD	A,D		;D>Ä×¯¸	E>¾¸À°
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	AND	$10		; SIDE
	LD	B,A
	LD	A,(HL)		; ÄÞ×²ÌÞ
	AND	3
	OR	B
	LD	B,$F
	OR	$80		;Ó°À°µÝ
	CP	(HL)
	JR	Z,SEEK0
;
	LD	C,$FC
	OUT	(C),A
	LD	(HL),A
	CALL	BUSY
	RET	C
;
SEEK0:	LD	A,(FIRDRV)
	OR	A
	JR	Z,SEEK1
	LD	A,2		;Ø½Ä±
	LD	C,$F8
	OUT	(C),A
	XOR	A
	LD	(FIRDRV),A
	CALL	BUSY
	RET	C
;
SEEK1:	SRL	D
	LD	A,80		;Ï¯¸½Ä×¯¸
	CP	D		;µ°ÊÞ°Ä×¯¸
	RET	C
	INC	E
	LD	C,$F9
	IN	A,(C)
	CP	D
	RET	Z
	LD	C,$FB
	OUT	(C),D
	LD	A,$1E	;¼°¸
	LD	C,$F8
	OUT	(C),A
;
BUSY:	LD	A,18
BUSY1:	DEC	A
	JR	NZ,BUSY1
	LD	C,$F8
	LD	H,0
BUSY2:	LD	L,0
BUSY3:	EX	(SP),HL
	EX	(SP),HL
	IN	A,(C)
	AND	$81
	RET	Z
	AND	1		;BUSY
	DEC	L
	OR	L
	JR	NZ,BUSY3
	DEC	H
	JR	NZ,BUSY2
	SCF
	RET
;
	DS	$20
;
SECSET:	LD	BC,$FFA
	OUT	(C),E
COMSET:	LD	C,$F8
	OUT	(C),A
	LD	A,7
DELAY:	DEC	A
	JR	NZ,DELAY
	RET
;
COMP:	LD	A,(DE)
	CP	(HL)
	JR	Z,COMP1
	ADD	A,$A0-$20
	CP	(HL)
	RET	NZ
COMP1:
	INC	DE
	INC	HL
	DJNZ	COMP
	RET
;
;
FNAME:	DB	"LA",$20,$20,$20,$20,$20,$20
	DB	"SYS"
;
FIRDRV:	DB	1		;FIRST ACCESS
;
MES:	DB	12,"Lovers IPL",0
;
PEND
;
	ORG	$FFE0
;
	DW	SA,SA,SA,SA,SA,SA,SA,SA
	DW	SA,SA,SA,SA,SA,SA,SA,SA
;
