;
;	X1¥X68K Dual IPL X1side
;
DRIVE	EQU	$FF87	;=PORT$FFC
START	EQU	$FD00
;
	OFFSET	$E5D0-$FFD0
;
	ORG	$FFD0
;
	LD	HL,$0034
	LD	DE,START
	LD	BC,PEND-SA
	LD	A,$1E
	OUT	($00),A
	LDIR
	JP	START
;
	DW	$FFD0,$FFD0,$FFD0
	DW	$FFD0,$FFD0,$FFD0,$FFD0
	DW	$FFD0,$FFD0,$FFD0,$FFD0
	DW	$FFD0,$FFD0,$FFD0,$FFD0
;
	OFFSET	$E634-START
;
	ORG	START
;
SA:	LD	A,$1D
	OUT	(0),A
	LD	A,2
	LD	($FF8C),A
;
	LD	A,$7
	LD	($FF86),A
	LD	HL,0
	LD	($FF80),HL
	LD	DE,MES
	CALL	$03CB
	LD	A,$1E
	OUT	(0),A
;
	DI
	LD	HL,IPLERR
	PUSH	HL
	LD	HL,IPL0
	PUSH	HL
	LD	HL,IPL3
	PUSH	HL
	LD	HL,IPL2
	PUSH	HL
	XOR	A
IPL1:	LD	(DRIVE),A
	LD	A,1
	LD	(FIRDRV),A
	CALL	FILE
	RET	C
	EI
	JP	$0000
;
IPL0:	LD	A,3
	JR	IPL1
IPL2:	LD	A,2
	JR	IPL1
IPL3:	LD	A,1
	JR	IPL1
IPLERR:	CALL	MOTOFF
	LD	A,$1D
	OUT	(0),A
	JP	$00F5
;
FILE:	LD	DE,5
	LD	HL,0
	PUSH	HL
	LD	B,6
	CALL	DRDSB
	POP	HL
	RET	C
;
	LD	C,192
FILE1:	LD	B,11
	LD	DE,FNAME
	PUSH	HL
	CALL	COMP
	POP	HL
	JR	Z,FILE2
	LD	DE,$20
	ADD	HL,DE
	DEC	C
	JR	NZ,FILE1
	SCF
	RET
;
FILE2:	LD	DE,0
	LD	BC,$20
	LDIR
	LD	HL,($001C)
	DEC	HL
	LD	B,H
	SRL	B
	SRL	B
	INC	B
	LD	HL,($001A)
	LD	DE,$0009
	ADD	HL,DE
	LD	E,L
	LD	D,H
	LD	HL,0
;
DRDSB:	PUSH	BC
	PUSH	DE
	CALL	DRD
	POP	DE
	POP	BC
	RET	C
	INC	DE
	DJNZ	DRDSB
;
MOTOFF:	PUSH	AF
	LD	HL,DRIVE
	LD	BC,$0FFC
	LD	A,(HL)
	AND	3
	OUT	(C),A
	LD	(HL),A
	POP	AF
	RET
;
COMP:	LD	A,(DE)
	CP	(HL)
	JR	Z,COMP1
	ADD	A,$A0-$20
	CP	(HL)
	RET	NZ
COMP1:
	INC	DE
	INC	HL
	DJNZ	COMP
	RET
;
DRD:	LD	(DMAD+11),HL
	CALL	SEEK		;ÃÞ¨½¸Ø°ÄÞ
	RET	C
	LD	D,$80
	LD	A,14
DISK:	LD	HL,DMAD
	LD	BC,$1F80
SDMA:	INC	B
	OUTI
	DEC	A
	JR	NZ,SDMA
	LD	BC,$1F80
	LD	HL,$D387	;BC=DMA
	OUT	(C),H
	OUT	(C),L
	LD	A,D		;D=FDC COMMAND
	CALL	SECSET
	LD	BC,$FF8
WAITD:	IN	A,(C)
	RRCA
	JR	C,WAITD
	LD	BC,$1F80	;READ DMA
	LD	HL,$BB60
	OUT	(C),H
	OUT	(C),L
	IN	L,(C)
	IN	H,(C)
	INC	HL
	AND	$3F
	RET	Z
	SCF
	RET
;
SEEK:	LD	HL,DRIVE	;DE<Úº°ÄÞÅÝÊÞ°	;Ó°À°µÝ&¼°¸
	LD	A,E		;D>Ä×¯¸	E>¾¸À°
	ADD	A,A
	AND	$10		; SIDE
	LD	B,A
	LD	A,(HL)	; ÄÞ×²ÌÞ
	AND	3
	OR	B
	LD	B,$F
	OR	$80		;Ó°À°µÝ
	CP	(HL)
	JR	Z,SEEK0
;
	LD	C,$FC
	OUT	(C),A
	LD	(HL),A
	CALL	BUSY
	RET	C
;
SEEK0:	LD	A,(FIRDRV)
	OR	A
	JR	Z,SEEK1
	XOR	A		;Ø½Ä±
	LD	C,$F8
	OUT	(C),A
	LD	(FIRDRV),A
	CALL	BUSY
	RET	C
;
SEEK1:	LD	A,E
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	EX	DE,HL
	AND	7
	INC	A
	LD	E,A
	LD	A,154		;Ï¯¸½Ä×¯¸
	CP	D		;µ°ÊÞ°Ä×¯¸
	RET	C
	LD	C,$F9
	IN	A,(C)
	CP	D
	RET	Z
	LD	C,$FB
	OUT	(C),D
	LD	A,$1C	;¼°¸
	LD	C,$F8
	OUT (C),A
;
BUSY:	LD	A,18
BUSY1:	DEC	A
	JR	NZ,BUSY1
	LD	C,$F8
	LD	H,0
BUSY2:	LD	L,0
BUSY3:	IN	A,(C)
	AND	$81
	RET	Z
	AND	1		;BUSY
	DEC	L
	OR	L
	JR	NZ,BUSY3
	DEC	H
	JR	NZ,BUSY2
	SCF
	RET
;
SECSET:	LD	BC,$FFA
	OUT	(C),E
COMSET:	LD	C,$F8
	OUT	(C),A
	LD	A,7
DELAY:	DEC	A
	JR	NZ,DELAY
	RET
;
DMAD:	DB	$83,$7D,$FB,$0F,$FF,$03,$2C,$10
	DB	$80,$92,$8D,$00,$00,$CF,$01,$CF
;
;
FIRDRV:	DB	1		;FIRST ACCESS
;
;		1234567890123456789012345678901234567890
MES:	DB	"Lovers IPL                              ",0
;		 12   3	  4   5	  6   7	  8
FNAME:	DB	"LA",$20,$20,$20,$20,$20,$20
	DB	"SYS"
;
PEND:
;
	DS	256
